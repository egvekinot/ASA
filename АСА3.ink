




VAR кощей = false
VAR кот  = false
VAR иван = false
VAR василиса = false
VAR дровосек = false
VAR дриада = false
VAR кол_арсенал = 0
VAR команда = ""
//VAR техническая_улика = 0
//VAR магическая_улика = 0
VAR всего_улик = 0
VAR катастрофа = false
VAR улика = 0
VAR попытка = 0

-> merlin

=== merlin ===
#Location: Kabinet_Merlina
Кабинет Верховного Мага Мерлина в Башне Вечных Сумерек. Комната выглядит так, будто по ней прошелся магический торнадо. Горы свитков нависают над странными артефактами, а в камине пляшет синий огонь. На одной из полок толстая книга в кожаном переплете тихонько скребется и вздыхает, требуя, чтобы ее, наконе-то, почитали... или хотя бы сдули пыль с обложки.
Великий волшебник нервно ходит по кабинету. Он явно чем-то взволнован!
МЕРЛИН: Это невообразимо! Наш мир сошел с ума! Все эти странные случаи... Ты ведь в курсе последних новостей?#З
ТЫ:
+ [Да, конечно! На курсе все говорят об этом!] ->dalee2
+ [Э-э... нет... Я был занят экзаменами...] ->case
===case===
МЕРЛИН: Гуси-Лебеди повадились воровать не только детей, но и взрослых, утаскивают скот из загонов. А три дня назад прихватили вывеску с таверны «Три медведя»! Хозяин в бешенстве, а клиенты не могут заведение найти!
МЕРЛИН: Змей Горыныч уснул прямо на лету и рухнул на огород местной ведьмы! Та теперь требует заплатить за помятые волшебные тыквы и грозит превратить дракона в крота!
МЕРЛИН: Избушка на Курьих Ножках потерялась в собственном лесу! Красная Шапочка а Волк ее неделю искали. А Бабе Яге все это время пришлось жить у Бабушки.
МЕРЛИН: А сегодня утром – русалки с Яузы пожаловались, что у них волосы седеют и выпадают! Говорят, то ли из-за плохой экологии, то ли кто-то порчу навел!
 ->dalee2
===dalee2===
МЕРЛИН: Мелочи? Возможно. Но что, если за этими курьезами кроется какая-то общая причина? 
МЕРЛИН: Я за тобой давно наблюдаю. На курсе ты был возможно не самым сильным, но, черт побери, самым сообразительным! Именно такой талант сейчас и нужен.
МЕРЛИН: Я создаю новый отдел. Так и назовем его - "Агентство Сказочных Аномалий". Мне самому некогда заниматься этими мелочами. Поэтому возглавить агентство должен ты.
ТЫ:
        + [Почему я? Есть же более опытные маги!]
            МЕРЛИН: Да, конечно! Старик Хоттабыч мог бы решить любую из этих проблем одним волоском со своей бороды. Но нам нужно выяснить причину аномалий. А она, я думаю, техномагическая!
            МЕРЛИН: Ты ведь знаешь, какой вечный спор ведут маги и техники. Инженеры считают волшебство "детскими игрушками" и презирают ее. А маги боятся электричества больше, чем разбушевавшегося дракона.
            МЕРЛИН: А ты... Я помню, как на первом курсе ты разгадал историю с заколдованным котлом. Все искали сложное проклятие, а ты нашел... засор в сливе и забытый носок.
             ТЫ: Э-э-э... ну да... было дело... пахло тогда еще так...
            -> accept_mission

        + [Агентство? Это звучит... официально. А каков мой бюджет?]
                МЕРЛИН: (закатывает глаза) Ах, молодежь... Сначала задумываются о бюджете, а уж потом – о приключении и славе!
            МЕРЛИН: Ладно. У тебя будет кабинет в Старом Городе. Очень... уютный. То есть, очень маленький. Я имею в виду о-о-очень маленький. И... мой старый летающий ковер. Он иногда чихает и сбрасывает седоков, но в резвости ему не откажешь!
             ТЫ: Что ж, тогда я берусь.
        -> accept_mission

        + [(С энтузиазмом) Вы правы, все эти случаи связаны! Я берусь!]
            МЕРЛИН: Так и знал, что не ошибся в тебе! В твоих глазах я вижу здоровое любопытство к хаосу!#Р
             -> accept_mission
             

=== accept_mission ===
МЕРЛИН: Но одному тебе не справиться. Эти аномалии – как гидра: отрубишь одну голову, вырастут две. Тебе нужна команда. 
Мерлин протягивает вам сверкающий свиток.

МЕРЛИН: Я подготовил список. Шесть выдающихся... ну, или, по крайней мере, очень... запоминающихся личностей. Испытанные агенты и настоящие специалисты. Тебе нужно выбрать троих.
ТЫ:
+ [Почему только троих? Чем больше, тем лучше!]
    МЕРЛИН: (ворчливо) Может, тебе еще и мою бороду в придачу? На каждого такого «уникума» требуется куча магической энергии, кофе и терпения. Наш бюджет не резиновый...
    МЕРЛИН: Три – идеальное число. Один может ошибиться, двое – перессориться, а трое - это уже команда. 
    МЕРЛИН: Меньше вопросов – они уже ждут в приемной.
    -> приемная

+ [Расскажите о них подробнее. На что мне смотреть?]
    МЕРЛИН: Смотри не на силу, а на сочетаемость, как в хорошем зелье. Бросишь в котел несовместимые ингредиенты – и получишь взрыв вместо эликсира.
    МЕРЛИН: Тебе нужна команда, способная заткнуть все дыры в этой тонущей лодке. Инженер, маг, следопыт... комбинации могут быть разными.
    МЕРЛИН:Меньше вопросов – они уже ждут в приемной.
    -> приемная

=== приемная ===
#Location: Ofis
Ты выходишь из кабинета Мерлина в небольшой кабинет твоего нового Агенства. На столе лежат шесть светящихся папок.
->ff

===ff===
#Location: Ofis
//{кол_арсенал}
 {кол_арсенал > 2 :->ffff |->q}
 =q
ТЫ:
+{дровосек == false  } [ЖЕЛЕЗНЫЙ ДРОВОСЕК]
 ->жд
+{дриада == false  } [ФЛОРА ЗЕЛЕНОЛИСТ, дриада]
 ->д
+{иван == false  }[ИВАН ЦАРЕВИЧ]
->и
+{кот == false  }[КОТ УЧЕНЫЙ]
->ку
+{кощей == false  }[КОЩЕЙ БЕССМЕРТНЫЙ]
->кб
+{василиса == false  }[ВАСИЛИСА ПРЕМУДРАЯ] 
->вп

===жд===
 Железный Дровосек входит по точно выверенной кратчайшей траектории. Его металлические суставы издают тихие щелчки. Садится с идеальной прямой спиной.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Система готова к интервью. Мои навыки включают проектирование, починку и создание механизмов любой сложности. Коэффициент полезного действия – 94.7%. ->sob
=sob
+[Посмотреть досье] -> d_k
+[Задать вопросы] 
#Location: Ofis
-> a

=d_k
#Location: Delo_ZH_D
Ты открываешь папку с досье
+[Задать вопросы] 
#Location: Ofis
-> a
=a
//#Location:Ofis
       +[Как вы относитесь к магии?]
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: (металлически щелкает) Магия – это недокументированный и часто нестабильный API. На нее нельзя положиться. Технология – вот чистый, откомментированный код. Она приносит порядок. #У
 ->a
       +[Что для вас самое важное в работе?]
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Эффективность. Точность. Предсказуемость результата. Беспорядок и хаос – это ошибка 404 в протоколе бытия.
 ->a
       +[Как вы справляетесь с неожиданными проблемами?]
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Алгоритм прост: анализ → план → действие. Любую проблему, даже самую иррациональную, можно разложить на составляющие и решить системно. Например, сильным электромагнитным импульсом. #З
    ->a
        +[Вы можете работать в команде?]
    ЖЕЛЕЗНЫЙ ДРОВОСЕК: (щелчок) Командная эффективность возрастает при четком распределении функций. Например, с магом я анализирую техническую составляющую артефакта, а он — магическую. Следопыт может предоставить данные с места происшествия, которые я обработаю. Главное — чтобы партнёр не вносил хаос в рабочий процесс.
->a
        +[закончить собеседование]
#Location:Ofis
  ->lk
  
  =lk
   *[принять в команду]
   ~дровосек = true
   ~кол_арсенал = кол_арсенал+1
   ~команда=команда+"Железного Дровосека, "
   ->ff
   +[попросить подождать]
   ->ff





===д===
Флора не входит, а вплывает в комнату, принося аромат свежескошенной травы и цветущей липы. 
ФЛОРА: Здравствуйте, родной! Я Флора. Я слышу, как поют растения и чувствую, когда животным больно. Я постараюсь успокоить этот прекрасный, но такой встревоженный мир.#Р
->соб
=соб
+[Посмотреть досье] -> d_f
+[Задать вопросы] 
#Location: Ofis
-> щ

=d_f
#Location: Delo_Flora
Ты открываешь папку с досье
+[Задать вопросы] 
#Location: Ofis
-> щ
=щ
       +[Что вы думаете о технологиях?]
       ФЛОРА: (слегка грустно, гладя цветок на своем платье) О, эти железные штуки... Они такие громкие, холодные... . Иногда они больно ранят землю-матушку. Но если они помогают и не слишком дымят – почему бы и нет? #У
 ->щ
       +[Как вы улаживаете конфликты?]ФЛОРА: Нужно просто поговорить по душам! Каждое живое существо, даже самый злой гоблин, хочет быть услышанным и понятым. Иногда достаточно предложить чаю из целебных трав и просто послушать.
 ->щ
       +[Ваше отношение к магии?]
        ФЛОРА: (зажмуривается от восторга) Магия – это дыхание мира! Она повсюду – в каждом шелесте листочка, в каждой капле росы! Мы должны бережно к ней относиться, как к хрупкому бутону.#Р
->щ
        +[Вы можете найти общий язык с теми, кто не чувствует природу?]
        ФЛОРА: (мягко улыбается) Я стараюсь показать им красоту живого. С инженерами, например, мы иногда дополняем друг друга — они видят механизм, а я вижу, как в нём пульсирует жизнь. А с магами... о, мы говорим на одном языке, только их магия — из книг, а моя — от земли.
->щ
        +[закончить собеседование]
  ->ц
  
  =ц
   *[принять в команду]
   ~дриада = true
    ~кол_арсенал = кол_арсенал+1
    ~команда=команда+"Флору Зеленолист, "
   ->ff
   +[попросить подождать]
   ->ff
   
   
   ===и===
   Царевич входит уверенной походкой человека, который привык, чтобы кусты перед ним расступались. Смотрит прямо в глаза, будто оценивая, насколько прочный у вас череп.
ИВАН ЦАРЕВИЧ: Иван. Если нужно найти, выследить или защитить – я ваш человек. Предпочитаю действие разговорам. 
->ц
=ц
+[Посмотреть досье] -> d_i
+[Задать вопросы] 
#Location: Ofis
-> о
=d_i
#Location: Delo_Ivan
Ты открываешь папку с досье
+[Задать вопросы] 
#Location: Ofis
->о
=о
       +[Как вы относитесь к магии?]
       ИВАН ЦАРЕВИЧ: (пожимает плечами) Не очень-то я доверяю тому, что нельзя ударить мечом. Ненадежно это все, искрится, меняется. Но если она на нашей стороне – почему нет? Главное, чтобы не мешала.#У
 ->о
       +[ Что делаете, когда сталкиваетесь с неизвестным?]
       ИВАН ЦАРЕВИЧ: Я действую по ситуации. Иногда нужно атаковать, иногда – затаиться и наблюдать. Однажды я три дня следил за говорящим мухомором, пока он не проболтался, где спрятал клад. Главное – не стоять на месте и не паниковать. Паника – враг хуже Змея Горыныча.
 ->о
       +[Ваша сильная сторона?]
        ИВАН ЦАРЕВИЧ: Я вижу то, что другие не замечают. След, взгляд, ложь в голосе... Мелочи решают всё. Ну и меткость, куда без нее. Я могу стрелой сбить муху с носа у Кощея.#З
->о
        +[Нужна ли вам поддержка в расследованиях?]
        ИВАН ЦАРЕВИЧ: (уверенно) Один в поле не воин. Хорошо, когда есть кто-то, кто разбирается в магии или древностях — я нахожу след, а они говорят, чей он. А ещё пригодится тот, кто сможет поговорить с потерпевшими или врагами, пока я караулю в засаде.
        ->о
        +[закончить собеседование]
  ->з
    =з
   *[принять в команду]
   ~иван = true
    ~кол_арсенал = кол_арсенал+1
    ~команда=команда+"Ивана Царевича, "
   ->ff
   +[попросить подождать]
   ->ff
   
      ===ку===
   Кот Ученый входит, спотыкаясь о собственную тень. Поправляет очки, съехавшие на кончик носа, и безуспешно пытается пригладить взъерошенную шерсть.
КОТ УЧЕНЫЙ: Вот... да... собственно... Кот Ученый, к вашим услугам... м-да... Специализация – история, мифология, древние языки и забытые диалекты. Если нужно что-то вспомнить – обращайтесь...->к
=к
    +[Посмотреть досье] -> d_ku
+[Задать вопросы] 
#Location: Ofis
-> ъ

=d_ku
#Location: Delo_kot
Ты открываешь папку с досье
+[Задать вопросы] 
#Location: Ofis
->ъ
=ъ
       +[Ваши знания могут помочь в современных расследованиях?]
       КОТ УЧЕНЫЙ: О! Каждая современная проблема – это просто новая обложка на старой книге. Нужно найти похожий случай в древних текстах. Вот, например, эти Гуси-Лебеди... их поведение очень напоминает случай, описанный в «Хрониках Смутного Времени для Птиц», глава третья...#З
 ->ъ
       +[ Отношение к современным технологиям?]
       КОТ УЧЕНЫЙ: (морщится, как от сквозняка) Шумные эти ваши механизмы... Вечно гудят и мигают. Но... признаю, интересно понаблюдать, как они работают. Древние цивилизации тоже пытались их использовать... Вот, например, в «Трактате о Выдумках Забытых Цивилизаций» сказано... Впрочем, я, кажется, снова отвлекся...#У
 ->ъ
       +[Что для вас самое сложное в работе?]
        КОТ УЧЕНЫЙ: (задумчиво) Заставить людей слушать, когда я, наконец-то, нахожу ответ. Все куда-то торопятся, бегут, суетятся... А истина требует неторопливой беседы
->ъ
        +[Как можно применить ваши знания на практике?]
        КОТ УЧЕНЫЙ: (поправляет очки) Одного знания, конечно, мало — нужен кто-то, кто сможет использовать его в деле. Скажем, инженер: я найду описание древнего механизма, а он поймёт, как его обезвредить. Или маг: я вспомню заклинание, а он его усилит. История любит повторяться, только в новых декорациях!#З
->ъ
        +[закончить собеседование]
  ->ю
  
  =ю
   *[принять в команду]
   ~кот = true
    ~кол_арсенал = кол_арсенал+1
    ~команда=команда+"Кота Ученого, "
   ->ff
   +[попросить подождать]
   ->ff
   
   ===кб===
   Кощей появляется бесшумно, словно он материализовался из самой тени. Смотрит с таким выражением лица, будто только что прочел инструкцию к мирозданию и нашел в ней досадные опечатки.
КОЩЕЙ: Кощей Бессмертный. Магия, проклятия, все виды защитных заклинаний. Если вам нужны разговоры по душам, зовите Василису. А если результат – договоримся. Мое время бесценно, но за определенную плату я готов его вам подарить. ->я
=я
    +[Посмотреть досье] -> d_ka
+[Задать вопросы] 
#Location: Ofis
-> э

=d_ka
#Location: Delo_Koshey
Ты открываешь папку с досье
+[Задать вопросы] 
#Location: Ofis
->э
=э
       +[Почему вы хотите работать в команде?]
      КОЩЕЙ: (холодно усмехается) Надоело над златом чахнуть. Решил понаблюдать за... коллективной динамикой примитивных социальных групп. Считайте это антропологическим экспериментом. 
 ->э
       +[Ваше отношение к технологиям?]
       КОЩЕЙ: Грубые и шумные попытки имитировать магию. Колотушка вместо телекинеза. Но иногда... удивительно эффективно. Не признаю, но использую.#У
 ->э
       +[Что вы цените в коллегах?]
       КОЩЕЙ: Компетентность. Молчаливость. И чтобы не задавали глупых вопросов и не пытались со мной «подружиться». Взаимное уважение на расстоянии – идеальный формат.
->э

        +[Как вы относитесь к другим специалистам?]
        КОЩЕЙ: (холодно усмехается) Не терплю некомпетентность. Но ценю специалистов, которые знают своё дело. Технолог, который не боится магии, или ученый, который не путает даты — вот с кем можно работать. #З
->э
        +[закончить собеседование]
          ->а
  
  =а
   *[принять в команду]
   ~кощей = true
    ~кол_арсенал = кол_арсенал+1
    ~команда=команда+"Кощея Бессмерного, "
   ->ff
   +[попросить подождать]
   ->ff
   
   
      
   ===вп===
   Василиса Премудрая входит с неспешной грацией лебедя. Ее появление словно приглушает звуки в комнате и делает воздух чище. На ее лице — теплое, ободряющее выражение..
ВАСИЛИСА: Очень приятно, Василиса. Я умею находить общий язык почти со всеми. Даже с теми, у кого этого языка и нет. #Р
->в
=в
   +[Посмотреть досье] -> d_vas
+[Задать вопросы] 
#Location: Ofis
-> э

=d_vas
#Location: Delo_Vas
Ты открываешь папку с досье
+[Задать вопросы] 
#Location: Ofis
->э
=э
       +[Почему вы ушли из дворца?]
    ВАСИЛИСА: Дворцовые интриги и бесконечные балы мне так надоели! Я хочу приносить реальную пользу, а не просто украшать собой трон.
 ->э
       +[Ваша сильная сторона?]
     ВАСИЛИСА: Я вижу ситуацию с разных сторон. Технология, магия, природа, людские суеверия... Всё это — нити одного полотна. Вы согласны со мной? А еще я умею заваривать такой чай, ради которого даже Кощей согласился бы на светскую беседу.#З
 ->э
       +[Как вы относитесь к конфликтам в команде?]
    ВАСИЛИСА: Конфликт – это чаще всего непонимание. А любое непонимание можно преодолеть, если выслушать друг друга. #У
->э
     +[Вы умеете работать в команде?]
    ВАСИЛИСА: (спокойно) Главное — понять, что каждый смотрит на проблему со своей стороны. Техник видит механизм, маг — энергию, следопыт — следы. Когда эти взгляды складываются — получается полная картина. 
->э
        +[закончить собеседование]
  ->а

    =а
   *[принять в команду]
   ~василиса = true
    ~кол_арсенал = кол_арсенал+1
    ~команда=команда+"Василису Премудрую, "
   ->ff
   +[попросить подождать]
   ->ff
   
     ===ffff===
Поздравляем! Ваша команда набрана. Вы приняли {команда}
Вы только что проводили последнего кандидата. В воздухе еще витает запах машинного масла от Дровосека, аромат цветов от Флоры и легкое чувство превосходства, оставленное Кощеем.

Дверь приоткрывается и в проеме появляется молодой человек в помятой мантии мага-академика. Его глаза скрыты за толстыми линзами очков, а за спиной парят несколько пергаментных свитков и магический артефакт, похожий на хрустальный шар.

ФОЛИАНТ: Приветствую, босс. Меня прислал Мерлин. Я – Архивариус Фолиант. Можно просто Фол. Ваш техномаг и аналитик. #Р

*   [Техномаг? Редкий специалист.] -> choice_technomage
*   [Проходи скорее, Фол. У нас полно работы!] -> choice_welcome

=== choice_technomage ===
ФОЛИАНТ: Точно, босс. Соединять магию и технологию непросто. Приходится выслушивать: «Как? Ты используешь магию? Это же прошлый век! Нельзя доверять этим дедовским методам!?» или «Как ты только терпишь все эти шумные железяки? Выбрось их все разом, и твоя комната станет намного чище!»
ФОЛИАНТ: А как вы считаете, босс, что круче — магия или технология?
ТЫ:
*   [Ну, это даже мудрецы в Большом Совете решить не могут!] -> fol_response_1
*   [Конечно технология, за ней будущее!] -> fol_response_2
*   [Магия рулит, она дает столько возможностей!] -> fol_response_3
*   [Ну, я за разумный компромисс.] -> fol_response_4

=== fol_response_1 ===
ФОЛИАНТ: Это точно, босс! #Р
-> fol_enters

=== fol_response_2 ===
ФОЛИАНТ: Человек с разумным взглядом на прогресс! Хотя мои свитки иногда обижаются на такие заявления.
-> fol_enters

=== fol_response_3 ===
ФОЛИАНТ: Уважаю. Главное - не говорите этого моему паяльнику. Он очень ранимый.
-> fol_enters

=== fol_response_4 ===
ФОЛИАНТ: Мой любимый ответ! Зачем выбирать, если можно взять лучшее? Как этот магический кристалл, подключенный к облачному хранилищу.#З
-> fol_enters

=== choice_welcome ===
ФОЛИАНТ: Отлично! Я люблю, когда дело не стоит на месте. Прямо как заряженный кристалл.#Р
-> fol_enters

=== fol_enters ===

Фолиант щелкает пальцами. Откуда-то появляется сложная конструкция: парящие в воздухе полупрозрачные синие экраны, между которыми текут строки магических символов. Несколько свитков сами разворачиваются на столе, а хрустальный шар занимает место в центре композиции.
ФОЛИАНТ: Знакомы с нашими последними разработками, босс? Это – «Когнитивный Кристалл», четвертого поколения! 
ФОЛИАНТ: Магический интерфейс для сбора, анализа и систематизации данных. Все, что мы узнаем: улики, гипотезы, даже сплетни – все будет вноситься сюда. Впечатляет, верно? Магические технологии не стоят на месте.
Он подходит к одному из экранов, и на нем появляются пустые графы с забавными анимированными иконками.
В этот момент Когнитивный Кристалл издает оглушительный писк и начинает мерцать тревожным алым светом.

ФОЛИАНТ: Босс! Мы засекли первую аномалию!#У
Из Кристалла доносится искаженный, полный паники голос.

КОГНИТИВНЫЙ КРИСТАЛЛ: ...Кто-нибудь! Слышит кто-нибудь?! В Гроте... в Хрустальном Гроте Эхо... это невозможно вынести! Этот звук... он сверлит мозги, он сводит с ума! 
КОГНИТИВНЫЙ КРИСТАЛЛ: Это положительно невыносимо!.. Ну, сделайте же что-нибудь, наконец!
ФОЛИАНТ: Босс, сигнал поступил из Хрустального Грота Эхо. Уровень искажения зашкаливает! Возможно что-то техническое. Собирать команду? -> fol_enters2
===fol_enters2===
ТЫ:
*   [Давай, Фол, зови всех! Время работать.] -> call_team
*   [Что это за Хрустальный Грот? Давай сначала справку.] -> grotto_info

=== grotto_info ===
КОГНИТИВНЫЙ КРИСТАЛЛ: Хрустальный Грот Эхо. Находится глубоко в недрах горы. Добраться можно через потайной ход, известный горным духам, или наведенным порталом. Природная и магическая достопримечательность. Место силы. Горные элементали используют его для накопления первичной магической энергии.
-> fol_enters2 // Возвращаемся к моменту выбора после справки

=== call_team ===
Вы киваете Фолианту. Похоже, ваше новое подразделение приступает к работе. Первый вызов ждет.
->grot

===grot===
// Начало сцены в Хрустальном Гроте
#Location: Grot_Anomalia
Вы внутри Хрустального Грота Эхо. Грот обычно ослепляет первозданной красотой, но сейчас он наполнен не благозвучным перезвоном сталактитов и сталагмитов, а оглушительным, монотонным, почти механическим визгом. Звук исходит не из одного места, а будто бы из самих стен, вызывая неестественную, синхронную вибрацию кристаллов.
Несколько мелких сталактитов уже лопнули, их осколки усеяли пол. Слышен слабый треск, похожий на короткое замыкание. На поверхности крупнейшего сталагмита виден сложный геометрический узор из оплавленных точек, словно кто-то поработал лазером.

ФОЛИАНТ ПО СВЯЗИ: (голос из коммуникатора, пробивающийся сквозь шум) Вы на месте! Сенсоры кричат от перегрузки. Если мы не остановим это сейчас, грот может рухнуть!#У
~попытка=0

// Начало первого этапа
-> Этап1_Обнаружение

=== Этап1_Обнаружение ===
ТЫ: С чего начнем? Как найдем источник шума?

+{ дровосек == true }[Спросить Железного Дровосека] -> Дровосек_предложение
+{ дриада == true }[Спросить Флору] -> Флора_предложение
+{ кощей == true }[Спросить Кощея] -> Кощей_предложение
+{ иван == true }[Спросить Ивана Царевича] -> Иван_предложение
+{ кот == true }[Спросить Кота Ученого] -> Кот_предложение
+{ василиса == true }[Спросить Василису] -> Василиса_предложение

=== Дровосек_предложение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Проведу акустическое сканирование. Построю 3D-карту резонансных полей для точного определения координатов источников.#З

+[Действуй!] -> Дровосек_действие
+[Послушаем, что скажут остальные] -> Этап1_Обнаружение

=== Дровосек_действие ===
{
    - кот == true:
        // КОМБИНАЦИЯ: Дровосек + Кот
        Железный Дровосек замирает на месте, издавая низкочастотный гул. Из его глазниц проецируются невидимые волны.
        
        КОТ УЧЕНЫЙ: (взволнованно) Погодите! Эти частотные паттерны... Я видел нечто подобное в "Анналах Древней Акустики"! Это "Звуковые Стражи" — устройства третьей эпохи!#З
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Подтверждаю. Сканирование выявило пять источников. Архитектура соответствует описанию Кота. Обнаружено клеймо: "Громовые Шестерни".#Р
        
        {попытка==0:
            ~улика=улика+2
            ФОЛИАНТ ПО СВЯЗИ: Идентификация и происхождение — отличная работа!#Р
        }
    - else:
        // Только Дровосек
        Металлический великан замирает на месте, издавая низкочастотный гул. Из его глазниц проецируются невидимые волны.
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Сканирование завершено. Обнаружено пять источников акустической атаки. Расположение неслучайно, образует деструктивный резонансный контур. Координаты занесены. Это инженерная работа, а не природный феномен.#Р
        
        {попытка==0:
            ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ:  Пришлите мне все данные, я занесу их в базу.
        }
}
-> После_обнаружения

=== Кот_предложение ===
КОТ УЧЕНЫЙ: Давайте поищем в летописях. Кажется, в «Каталоге Акустических Чудес» есть описание подобных случаев...#У

+[Действуй!] -> Кот_действие
+[Послушаем, что скажут остальные] -> Этап1_Обнаружение

=== Кот_действие ===
{
    - дровосек == true:
        // КОМБИНАЦИЯ: Кот + Дровосек
        КОТ УЧЕНЫЙ: Так-так-так, "Звуковые Стражи" — устройства для нарушения акустической гармонии! Они должны крепиться к звуковым отражателям!#У
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Подтверждаю. На сталактитах обнаружены крепления. Сканирую структуру...Сохраняю для анализа.
        
        {попытка==0:
            ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ: Идеально! Заношу в базу данных!#Р
        }
    - else:
        // Только Кот
        Кот в панике ныряет в груду парящих свитков.
        КОТ УЧЕНЫЙ: Минуточку, минуточку!.. Где же... Вот! «Звуковые Стражи» — устройства для нарушения акустической гармонии! Они должны крепиться к звуковым отражателям! Смотрите, на тех сталактитах что-то есть!
        ФОЛИАНТ ПО СВЯЗИ: Хорошее начало, но нужны технические детали...#У
      -> После_обнаружения
}
-> После_обнаружения

=== Кощей_предложение ===
КОЩЕЙ: Проведу спектральный анализ магических искажений. Эта техномагия оставляет такие же грубые следы, как сапог тролля на чистом полу.#З

+[Действуй!] -> Кощей_действие
+[Послушаем, что скажут остальные] -> Этап1_Обнаружение

=== Кощей_действие ===
{
    - дриада == true:
        // КОМБИНАЦИЯ: Кощей + Флора
        Щелкнув пальцами, Кощей создает в воздухе бледно-зеленоватый светящийся узор.
        
        ФЛОРА: (вздрагивая) Ой! Я чувствую... это не просто магия. Это что-то чужеродное, что враждует с природой кристаллов!#У
        
        КОЩЕЙ: Точно. Источники помех. Пять точек. Их энергия конфликтует с естественной магией грота. По следам можно определить производителя.#Р
        
        {попытка==0:
            ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ: Конфликт энергий — важная улика! Записываю.
        }
    - else:
        // Только Кощей
        Щелкнув пальцами, Кощей создает в воздухе бледно-зеленоватый светящийся узел.
        
        КОЩЕЙ: Как я и предполагал. Техномагические источники, расположенные в нескольких точках грота. Пять штук. Примитивно, но эффективно.#Р
        
        {попытка==0:
            ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ:  Отлично. Соберите все данные, я занесу их в базу.#Р
        }
        }
        
-> После_обнаружения

=== Флора_предложение ===
ФЛОРА: Я попробую понять боль кристаллов. Если прислушаться к их вибрациям, они сами покажут, где находятся инородные предметы...Они ведь немножко живые, правда?#У

+[Действуй!] -> Флора_действие
+[Послушаем, что скажут остальные] -> Этап1_Обнаружение

=== Флора_действие ===
{
    - кощей == true:
        // КОМБИНАЦИЯ: Флора + Кощей
        Флора со слезами на глазах касается ближайшего кристалла, но сразу отскакивает, словно от удара током.
        
        ФЛОРА: Ай! Их боль... она повсюду! я чувствую, как чужеродная энергия разрывает их изнутри!#П
        
        КОЩЕЙ: (хладнокровно) Да. Пять точек разрыва. Техномагия. Подожди, дриада, я проанализирую структуру... Вон там, в основании салактитов.
		ФЛОРА: Да-да! Чужая энергия идет оттуда!        
        {попытка==0:
            ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ:  Чувствительность природы + магический анализ — мощно!#Р
                    }
    - else:
        // Только Флора
        Дриада со слезами на глазах касается ближайшего кристалла, но сразу отскакивает, словно от удара током.
                ФЛОРА: Ай! Их боль... она повсюду! Я не могу выделить отдельный источник, это просто сплошная стена страдания! Это техногенная угроза, я ничего не могу сделать...#П
       		~попытка=1
         -> Этап1_Обнаружение
           
}
            -> После_обнаружения


=== Иван_предложение ===
ИВАН ЦАРЕВИЧ: Я предлагаю осмотреть все внимательнее. Возможно, удастся что-то заметить - неестественный блеск, отпечатки, следы вмешательства.#У

+[Действуй!] -> Иван_действие
+[Послушаем, что скажут остальные] -> Этап1_Обнаружение

=== Иван_действие ===
{
    - василиса == true:
        // КОМБИНАЦИЯ: Иван + Василиса
        Царевич, щурясь от боли в ушах, медленно обходит грот по периметру. Внезапно он замирает и указывает на один из крупных сталактитов.
        
        ИВАН ЦАРЕВИЧ: Вот! Видите? Маленькая металлическая штуковина. И там еще одна. Похоже, они расставлены по всему гроту.
        
        ВАСИЛИСА: Дай-ка я спрошу у духов, что они об этом знают... (закрывает глаза) Да, они говорят, что устройства установлены недавно... Приходили два невысоких человека в плащах...
        
        {попытка==0:
            ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ:  Отлично! Заношу улику в базу...
        }
    - else:
        // Только Иван
        Царевич, щурясь от боли в ушах, медленно обходит грот по периметру. Внезапно он замирает и указывает на один из крупных сталактитов.
        
        ИВАН ЦАРЕВИЧ: Вот! Видите? Маленькая металлическая штуковина. И там еще одна. Похоже, они расставлены по всему гроту. Источник обнаружен, но принцип ее работы мне непонятен.
        // Без улики
}
-> После_обнаружения

=== Василиса_предложение ===
ВАСИЛИСА: Предлагаю поговорить с духом пещеры. Если он не слишком расстроен, то, возможно, подскажет, где находится источник проблем.#У

+[Действуй!] -> Василиса_действие
+[Послушаем, что скажут остальные] -> Этап1_Обнаружение

=== Василиса_действие ===
{
    - кот == true:
        // КОМБИНАЦИЯ: Василиса + Кот
        Василиса закрывает глаза, пытаясь послать успокаивающий импульс.
        
        ВАСИЛИСА: Дух пещеры слишком напуган, но... он показывает образы... устройства, которые издают звук...#У
        
        КОТ УЧЕНЫЙ: О! Это напоминает описание в "Хрониках подземных эхо"... Устройства, нарушающие акустическую гармонию! Их устанавливали древние инженеры для разрушения пещер!#З
        
        ВАСИЛИСА: Да, и дух говорит, что установщики были в плащах с эмблемой молота и шестерни.
        
        {попытка==0:
            ~улика=улика+1
		ФОЛИАНТ ПО СВЯЗИ: Это уже улика!           
        }
    - else:
        // Только Василиса
        Василиса закрывает глаза, пытаясь послать успокаивающий импульс.
        
        ВАСИЛИСА: Увы! Дух пещеры слишком напуган. Его голос заглушен этим визгом. К сожалению, он не может мне помочь.#П
		~попытка=1
         -> Этап1_Обнаружение
              }  
        -> После_обнаружения

=== После_обнаружения ===
ФОЛИАНТ ПО СВЯЗИ:  Отлично, источники найдены! Это какие-то устройства. Теперь главное - обезвредить их, не превратив грот в груду битого хрусталя.

-> Этап2_Решение

=== Этап2_Решение ===
 ТЫ:  Кто возьмется?

+{ дровосек == true}[Спросить Железного Дровосека] -> Дровосек_предложение2
+{ дриада == true}[Спросить Флору] -> Флора_предложение2
+{ кощей == true}[Спросить Кощея] -> Кощей_предложение2
+{ иван == true}[Спросить Ивана Царевича] -> Иван_предложение2
+{ кот == true}[Спросить Кота Ученого] -> Кот_предложение2
+{ василиса == true}[Спросить Василису] -> Василиса_предложение2

=== Дровосек_предложение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Мое предложение: точечные электромагнитные импульсы. Они не повредят хрупкие кристаллы, переведут устройства в спящий режим и сохранят их для последующего анализа. Это наиболее эффективный и предсказуемый метод.#З

+[Действуй!] -> Дровосек_нейтрализация
+[Послушаем другие варианты] -> Этап2_Решение

=== Дровосек_нейтрализация ===
{
    - кот == true:
        // КОМБИНАЦИЯ: Дровосек + Кот
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Посылаю точечные импульсы...
        
        КОТ УЧЕНЫЙ: В архивах есть указание на уязвимость таких устройств к определенной частоте!#З
        
        Дровосек использует подсказку Кота. Устройства отключаются идеально.
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Деактивация успешна. Устройства в идеальном состоянии. Получены полные данные.#Р
        
        КОТ УЧЕНЫЙ: Я узнаю клеймо гильдии "Громовые Шестерни". Это их эмблема.#Р
        ФОЛИАНТ ПО СВЯЗИ:  Отлично! Заношу в базу данных! #Р
        ~улика=улика+2
    - else:
        // Только Дровосек
        Дровосек наводит указательный палец на одно из устройств. Раздается тихий щелчок, и визг от этого источника мгновенно прекращается. Он методично повторяет процедуру для остальных четырех.
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Задача выполнена. Устройства деактивированы, целы и готовы к анализу. На корпусе обнаружено клеймо: «Громовые Шестерни».#Р
        
        ФОЛИАНТ ПО СВЯЗИ:  Я что-то слышал об этой гильдии! Явная улика!#Р
        
        ~улика=улика+1
}
-> После_нейтрализации

=== Кот_предложение2 ===
КОТ УЧЕНЫЙ: Я только что нашёл! В «Манускрипте Молчания» описан именно этот случай! Специальное заклинание «Прерывание алгоритма»! Оно не пытается разрушить устройство, а обманывает его. Устройства перейдут в спящий режим, как будто их выключили с пульта, все данные останутся нетронутыми! Проверенная методика!#Р

+[Действуй!] -> Кот_нейтрализация
+[Послушаем другие варианты] -> Этап2_Решение

=== Кот_нейтрализация ===
{
    - кощей == true:
        // КОМБИНАЦИЯ: Кот + Кощей
        КОТ УЧЕНЫЙ: Произношу заклинание "Прерывание алгоритма"...
        
        КОЩЕЙ: Позволь мне усилить его магией тьмы, чтобы оно прошло через любую защиту.
        
        Совместное заклинание работает безупречно. Устройства отключаются, сохраняя все данные.
        
        КОЩЕЙ: Готово. И я добавил отслеживающий след на каждое устройство.#Р
        
        КОТ УЧЕНЫЙ: И данные полностью сохранены! Мы можем проанализировать их!#Р
        ФОЛИАНТ ПО СВЯЗИ:  То, что нужно!
        ~улика=улика+2
    - else:
        // Только Кот
        Кот, зажмурившись, бормочет сложное заклинание. Устройства один за другим издают тихий щелчок и гаснут, как уснувшие светлячки.
        КОТ УЧЕНЫЙ: Получилось! Заклинание «Прерывание алгоритма» сработало! Устройства полностью обезврежены и готовы для изучения.#Р
        ФОЛИАНТ ПО СВЯЗИ:  Спасибо, профессор. Это улика!#Р
      
        ~улика=улика+1
}
-> После_нейтрализации

=== Флора_предложение2 ===
ФЛОРА: Эти железные штучки так больно впились в бедные кристаллики! Давайте я попрошу корни и лианы аккуратно опутать их и вытащить. Природа сделает это бережно... Разве что парочка кристаллов немного поцарапается, но зато мы спасем весь грот, правда?#У

+[Действуй!] -> Флора_нейтрализация
+[Послушаем другие варианты] -> Этап2_Решение

=== Флора_нейтрализация ===
{
    - дровосек == true:
        // КОМБИНАЦИЯ: Флора + Дровосек
        ФЛОРА: Я попрошу растения аккуратно обвить устройства...
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: (Рисует чертеж на камне) Предлагаю направить рост корней по такой траектории, чтобы минимизировать повреждения.
        
        Флора и Дровосек работают вместе. Лианы осторожно обвивают устройства и вытаскивают их, почти не повреждая кристаллы.
        
        ФЛОРА: Готово! Устройства целы, и кристаллы почти не пострадали!#Р
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Устройства сохранены для анализа. На них есть клеймо.
		ФОЛИАНТ ПО СВЯЗИ:  Заношу в базу данных! #Р
        
        ~улика=улика+1
    - else:
        // Только Флора
        Флора что-то напевает, и по стенам грота начинают стремительно ползти лианы и корни. Они плотно опутывают устройства и вырывают их. Несколько кристаллов при этом получают небольшие сколы. Визг прекращается.
        
        ФЛОРА: Готово! Правда, бедные кристаллики немного пострадали... А эти железки, кажется, сломались...
}
-> После_нейтрализации

=== Кощей_предложение2 ===
КОЩЕЙ: Эта техномаговая безвкусица оскорбляет взгляд. Я применю целевое разлагающее заклятье. Чисто и эффективно. Оно уничтожит устройства, но сохранит кристаллы невредимыми.#У

+[Действуй!] -> Кощей_нейтрализация
+[Послушаем другие варианты] -> Этап2_Решение

=== Кощей_нейтрализация ===
{
    - кот == true:
        // КОМБИНАЦИЯ: Кощей + Кот
        КОЩЕЙ: Я растворю эти устройства...
        
        КОТ УЧЕНЫЙ: Погодите! Я знаю заклинание, которое скопирует магическую схему перед уничтожением!#З
        
        Кот быстро шепчет заклинание. Кощей добавляет свое. Устройства исчезают, но в воздухе остаются светящиеся схемы.
        
        КОЩЕЙ: Хм... неожиданно полезно. Теперь у нас есть схемы для изучения.#Р
        ФОЛИАНТ ПО СВЯЗИ:  То что нужно!#Р
            ~улика=улика+1
    - else:
        // Только Кощей
        Кощей проводит рукой, и устройства один за другим растворяются в клубах черной дымки. Визг прекращается.
        
        КОЩЕЙ: Дело сделано.
        
        ФОЛИАНТ ПО СВЯЗИ:  Босс, я потерял энергетические следы на устройствах! Они полностью уничтожены!#П
}
-> После_нейтрализации

=== Иван_предложение2 ===
ИВАН ЦАРЕВИЧ: Давайте я просто собью устройства стрелами. Быстро и надежно, хотя устройства, возможно, повредятся.#У

+[Действуй!] -> Иван_нейтрализация
+[Послушаем другие варианты] -> Этап2_Решение

=== Иван_нейтрализация ===
{
    - дровосек == true:
        // КОМБИНАЦИЯ: Иван + Дровосек
        ИВАН ЦАРЕВИЧ: Я собью их стрелами!
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: (Рисует чертеж на камне) Рассчитаем траекторию полета стрелы, чтобы устройства упали на мягкую поверхность.
        
        Иван стреляет. Устройства падают на заранее подготовленные мягкие моховые подушки.
        
        ИВАН ЦАРЕВИЧ: Все сбиты!#Р
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Устройства повреждены, но основные компоненты целы. Можно анализировать.
        ФОЛИАНТ ПО СВЯЗИ:  Тащите их на базу, проведем полный анализ!
        ~улика=улика+1
    - else:
        // Только Иван
        Иван быстрыми и точными выстрелами сбивает все пять устройств. Они со звоном разбиваются о каменный пол.
        ИВАН ЦАРЕВИЧ: Враг повержен! От этих штуковин теперь одни осколки.
        ФОЛИАНТ ПО СВЯЗИ: И от улик тоже...#П
}
-> После_нейтрализации

=== Василиса_предложение2 ===
ВАСИЛИСА: Может быть, я попробую поговорить с ними? Всё в этом мире имеет каплю сознания, даже эти механизмы. Если я смогу донести до них, какой вред они причиняют, может быть, они сами остановятся? Это стоит попробовать, не правда ли?#У

+[Действуй!] -> Василиса_нейтрализация
+[Послушаем другие варианты] -> Этап2_Решение

=== Василиса_нейтрализация ===
  // Только Василиса (катастрофа)
        ВАСИЛИСА: Уважаемые источники, пожалуйста, поймите, вы причиняете вред этим прекрасным созданиям...#У
        
        Визг на секунду многократно усиливается, вынуждая всех зажать уши. Устройства, вместо того чтобы отключиться, начинают мигать в тревожном ритме.
        
        ФОЛИАНТ ПО СВЯЗИ:  (панически) Босс, что вы делаете?! Уровень звукового давления зашкаливает! Магический резонанс нарастает!#З
#Location: Grot_KATASTROFA        
        Монотонный визг сменяется оглушительным, нарастающим грохотом. Гигантские сталактиты один за другим откалываются от потолка и с гулким треском обрушиваются вниз, разбиваясь на миллионы сверкающих осколков. Потолок и стены начинают покрываться трещинами, грот, веками хранивший своё эхо, погружается в гробовую тишину, похороненный под тоннами битого хрусталя и камня. Вы еле-еле успеваете спастись и возвращаетесь на базу.
        
        ~катастрофа = true
    -> Итоги

=== После_нейтрализации ===
#Location: Grot
Визг смолк. В гроте воцаряется блаженная, звенящая тишина.

-> Итоги

=== Итоги ===
#Location: Ofis
~всего_улик=всего_улик+улика
ФОЛИАНТ:Передаю итоговый отчет.

{ катастрофа:
ФОЛИАНТ: Мы не справились, босс. Гроту нанесён серьёзный ущерб. Потребуются месяцы, если не годы, магической реставрации. Эх... Нам нужно быть осторожнее. Не каждый меч годится для резьбы по хрусталю. Урок, как говорится, выучен. Дорогой ценой.#П
}

{улика>=3:
ФОЛИАНТ: Босс, это блестящий результат! Мы не только спасли грот, но и получили четкие зацепки. У нас есть образец устройства и данные о том, кто его создал и устанавливал. Цепочка ведет к гильдии «Громовые Шестерни». Мы на верном пути!#Р
}

{ улика ==2 && катастрофа==false:
ФОЛИАНТ: Грот спасен, и это главное. Босс, мы получили интересную информацию. Похоже, улики ведут к гильдии «Громовые Шестерни». Но пока еще доказательств недостаточно.
}

{улика<=1 && катастрофа==false:
ФОЛИАНТ: Мы потушили пожар, но не нашли поджигателя. Грот цел, но виновные пока неизвестны... Нам необходимо найти улики!#У
}

{ дровосек == true && дриада == true:
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Нужен более рациональный и логичный подход. Эмоциональность снижает эффективность принимаемых решений на 38%.#У
ФЛОРА: (презрительно) Бездушная железяка!#З
}
{ дриада == true && иван == true:
ФЛОРА: Это было так ужасно! Так ужасно! Я чуть не закричала от ужаса! #П
ИВАН ЦАРЕВИЧ: Флора, я бы тебя с собой в разведку не взял. Ты слишком шумная!#З
}

КОГНИТИВНЫЙ КРИСТАЛЛ: Всего в базу данных занесено улик: {всего_улик} 
~катастрофа=false


ФОЛИАНТ: Первое дело закрыто. Поздравляю, босс. А у нас новая проблема: Нужно спасти облачные корабли в небесной верфи. Там какая-то ржавчина появилась... Откуда на облаках ржавчина?#У
ТЫ:
+[Перебрасывай нас туда немедленно!] -> НЕБЕСНАЯ_ВЕРФЬ
+[Небесная верфь? Это еще что такое?] 
КОГНИТИВНЫЙ КРИСТАЛЛ: Небесная Верфь — это мастерская воздушных духов-альвов, парящая высоко в небе на облачном плато. Альвы  вдыхают жизнь в облака, придавая им изящные формы фрегатов и каравелл. Они даже считают свои корабли живыми существами. Каждый такой корабль связан с создавшим его альвом незримой магической нитью. 
 -> НЕБЕСНАЯ_ВЕРФЬ

=== НЕБЕСНАЯ_ВЕРФЬ ===
~улика=0
~попытка=0
#Location: Verf_Anomalia
Вы стоите на парящем в небе плато — Небесной Верфи.

Величественные облачные корабли, обычно парящие с невесомой грацией, теперь медленно опускаются на землю. Ржавая пыль не просто оседает на них — она словно разъедает их эфемерные борта. Ветер на Верфи, раньше ласковый и послушный, теперь мечется в судорожных порывах, постоянно вздымая железную пыль и создавая из нее хаотичные вихри и непредсказуемые потоки. 
Стайки ветряных фей мечутся над плато, встревоженно перекрикиваясь. Духи-альвы бесцельно бродят по палубам корабля, но в их обычно мелодичных голосах чувствуется явная растерянность. Они не в состоянии помочь своим творениям.

ФОЛИАНТ ПО СВЯЗИ:  Босс, вы на Верфи! Сенсоры показывают аномальную концентрацию частиц небесного железа в атмосфере. Корабли теряют магическую плавучесть и тяжелеют! Если они упадут — это будет катастрофа. Где-то должен быть источник этой железной пыли!

-> Этап2_Решение1

=== Этап2_Решение1 ===
ТЫ: Команда, какие будут предложения?
+ { дровосек == true} [Спросить Железного Дровосека]->Дровосек_предложение3
+ { дриада == true} [Спросить Флору]->Флора_предложение3
+ { кощей == true} [Спросить Кощея]->Кощей_предложение3
+ { иван == true} [Спросить Ивана Царевича]->Иван_предложение3
+ { кот == true} [Спросить Кота Ученого]->Кот_предложение3
+ { василиса == true} [Спросить Василису]->Василиса_предложение3

=== Дровосек_предложение3 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК:Попытаюсь проанализировать состав и плотность пыли на разных участках верфи. Это поможет вычислить эпицентр распространения пыли по градиенту плотности.#У

+ [Действуй!]->Дровосек_действие1
+ [Послушаем другие варианты]->Этап2_Решение1

=== Дровосек_действие1 ===
        {  иван == true || василиса == true:
        // КОМБИНАЦИЯ: Дровосек с помощником
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Начинаю анализ градиента плотности...
        
        {
            - иван == true:
                ИВАН ЦАРЕВИЧ: Я заметил, что на северной стороне плато пыль лежит толще. Может, там искать?
            - василиса == true:
                ВАСИЛИСА: Альвы говорят, что ветер обычно дует с севера по утрам. Возможно, это поможет?
        }
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Корректирую алгоритм с учётом дополнительных данных. Источник определён: Пик Грифона, северо-восточный склон. Анализ показывает техногенное происхождение пыли.
        
        {попытка==0:
           ~улика=улика+1  
            ФОЛИАНТ ПО СВЯЗИ:  Командный анализ дал результат!
        }
    - else:
        // Только Дровосек
        ЖЕЛЕЗНЫЙ ДРОВОСЕК:Ветер перемешивает пыль, снижая эффективность анализа на 75,7 %. Мои сенсоры не могут корректно построить модель распространения. Логика тут не эффективна. Зафиксировал аномальную концентрацию частиц с севера и северо-востока. Рекомендую провести дополнительные замеры в этом направлении.
}
~попытка=1
->Этап2_после_обнаружения

=== Флора_предложение3 ===
ФЛОРА: Это же экологическая катастрофа! Я поговорю с ветром и ветряными феями. Они видят все и наверняка знают, откуда дует этот «железный» ветер.#З

+ [Действуй!]->Флора_действие1
+ [Послушаем другие варианты]->Этап2_Решение1

=== Флора_действие1 ===
{
    - василиса == true || кощей == true:
        // КОМБИНАЦИЯ: Флора с Василисой или Кощеем
        Дриада закрывает глаза, подставляя лицо ветру. Ее волосы колышутся в такт невидимым ритмам.
        
        {
            - василиса == true:
                ВАСИЛИСА: Можно я помогу? (обращаясь к ветряным феям) Не бойтесь, ответьте нам, мы друзья, мы хотим помочь...
                ФЛОРА: Они показывают... железного паука на Пике Грифона! И говорят... у него на боку клеймо молота и шестерни!#З
            - кощей == true:
                КОЩЕЙ: Позволь мне усилить вашу связь с помощью магии....#З
                ФЛОРА: Спасибо! Теперь я вижу гораздо чётче... Механизм в виде паука на Пике Грифона! Пыль идет оттуда!#Р
        }
        
        {попытка==0:
           ~улика=улика+2 
            ФОЛИАНТ ПО СВЯЗИ:  Отличная командная работа! Детализированная информация!#Р
        }
    - else:
        // Только Флора (как в оригинале)
        Дриада закрывает глаза, подставляя лицо ветру. Ее волосы колышутся в такт невидимым ритмам.
        ФЛОРА: Ветер с севера... он несет пыль и холодную злобу! Там, на Пике Грифона, скрывается ее источник! Ветряные феи показывают мне железного паука, что плюется этой ржавой дымкой!
           {попытка==0:
           ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ:  Отлично! Я поищу в базе похожие механизмы!
        }
}
~попытка=1
->Этап2_после_обнаружения

=== Кощей_предложение3 ===
КОЩЕЙ: Проведу отслеживание по магическому шлейфу. Эта пыль не природная, а значит, несет на себе отпечаток создателя.#З

+ [Действуй!]->кощей_действие1
+ [Послушаем другие варианты]->Этап2_Решение1

=== кощей_действие1 ===
{
    - дриада == true || василиса == true:
        // КОМБИНАЦИЯ: Кощей с Флорой или Василисой
        Кощей проводит рукой, и частицы пыли в воздухе начинают светиться зловещим фиолетовым светом.
        
        {
            - дриада == true:
                ФЛОРА: (вздрагивая) Эта магия губит растения. На Пике Грифона уже весь склон безжизненный!..#У
                КОЩЕЙ: Именно. Шлейф как раз и ведет на север. И судя по характерным следам... это работа "Громовых Шестерен".#З
            - василиса == true:
                ВАСИЛИСА: Магия оставляет эмоциональный след... в ней есть гнев, но и страх тоже. Кто-то боится быть обнаруженным.#У
                КОЩЕЙ: Интересное наблюдение. Шлейф ведет к Пику Грифона. И да, в магии есть нотки страха. Значит, преступник не уверен в себе.#З
        }
                {попытка==0:
           ~улика=улика+2  
            ФОЛИАНТ ПО СВЯЗИ:  Анализ с дополнительным контекстом — ценная улика!#Р
        }
    - else:
        // Только Кощей (как в оригинале)
        Кощей проводит рукой, и частицы пыли в воздухе начинают светиться зловещим фиолетовым светом, выстраиваясь в четкую линию, ведущую на север.
        
        КОЩЕЙ: Как я и думал. Грубая работа. Шлейф ведет прямиком к одинокой скале. Пик Грифона, кажется. Никакой маскировки. Самоуверенные недоучки.#Р
        {попытка==0:
           ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ:  Это улика, заношу ее в базу.
        }
}
~попытка=1
->Этап2_после_обнаружения

=== Иван_предложение3 ===
ИВАН ЦАРЕВИЧ: Я обойду край плато в поисках следов. Для установки устройства нужен был доступ, а значит, точно должны остаться следы.#У

+ [Действуй!]->иван_действие1
+ [Послушаем другие варианты]->Этап2_Решение1

=== иван_действие1 ===
{
    - дровосек == true || дриада == true:
        // КОМБИНАЦИЯ: Иван с Дровосеком или Флорой
        Иван Царевич внимательно изучает край плато.
        
        {
	            - дриада == true:
                ФЛОРА: Иван, обрати внимание на примятые растения. Кто-то недавно прошел по этой тропе с тяжелой ношей.#З
                ИВАН ЦАРЕВИЧ: Точно, вот следы грубой обуви. И металлические опилки... Ведут на север.#З
                        }
            - дровосек == true:
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Изучаю найденные следы... Металлическая стружка имеет уникальный сплав. Анализирую...
                ИВАН ЦАРЕВИЧ: И здесь царапины от чего-то тяжелого. Смотри — ведут на север, к Пику Грифона.#З
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Подтверждаю. По составу стружки — производство "Громовых Шестерен".#З
        
        {попытка==0:
           ~улика=улика+1 
           ФОЛИАНТ ПО СВЯЗИ: Команда нашла убедительные улики!#Р 
                   }
    - else:
        // Только Иван
        Иван Царевич внимательно изучает край плато.
        ИВАН ЦАРЕВИЧ: К сожалению, большинство следов засыпано металлической пылью. Но сюда точно поднимались. Недавно. Видны следы грубой обуви и... царапины от чего-то тяжелого и металлического. Источник должен быть где-то там . Он указывает в сторону Пика Грифона.#У
}
~попытка=1
->Этап2_после_обнаружения

=== Кот_предложение3 ===
КОТ УЧЕНЫЙ: Предлагаю свериться с манускриптами по атмосферной магии. Возможно там есть описания аналогичных случаев.#У

+ [Действуй!]->кот_действие1
+ [Послушаем другие варианты]->Этап2_Решение1

=== кот_действие1 ===
{
    - дровосек == true:
        // КОМБИНАЦИЯ: Кот с Дровосеком
        КОТ УЧЕНЫЙ: Так, м-да... «Трактат о летающих островах»...#У
        
        {
            - дровосек == true:
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Профессор, если мы совместим исторические данные с анализом состава пыли...
                КОТ УЧЕНЫЙ: Да, да! Вот! "Металлические туманы третьей эпохи, создаваемые устройствами горных кузнецов"... И описание симптомов совпадает!#З
                   }
        
        {попытка==0:
           ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ: Совместный поиск дал результат!#Р
        }
    - else:
        // Только Кот
        КОТ УЧЕНЫЙ: Так, м-да, «Трактат о летающих островах»... нет, не то. «Песни небесных мореходов»... э-э-э... Здесь слишком много теорий и ничего конкретного!.. Редкий случай, когда древние летописи бесполезны... Нужно мнение другого специалиста...#П
       ~попытка=1
->Этап2_Решение1
}
       ->Этап2_после_обнаружения

=== Василиса_предложение3 ===
ВАСИЛИСА: Я вежливо расспрошу альвов. Они могли заметить что-то необычное в последние дни.#У

+ [Действуй!]->василиса_действие1
+ [Послушаем другие варианты]->Этап2_Решение1

=== василиса_действие1 ===
{
    - дриада == true || иван == true:
        // КОМБИНАЦИЯ: Василиса с Флорой или Иваном
        Василиса подходит к группе напуганных альвов.
        
        {
            - дриада == true:
                ФЛОРА: Я с тобой, Василиса. Я помогу успокоить их... #З
                ВАСИЛИСА: Они говорят, что видели вспышки на Пике Грифона три дня назад. 
                ФЛОРА: Ветер принёс пыль с севера.
            - иван == true:
                ИВАН ЦАРЕВИЧ: Я помогу задать точные вопросы. Когда именно? В какое время? С какой стороны пика?#З
                ВАСИЛИСА: Они точно говорят: северо-восточный склон, на закате. И слышали странный гул.#З
                ИВАН ЦАРЕВИЧ: Значит, устройство расположена на Пике Грифона.
        }
        
        {попытка==0:
           ~улика=улика+1 
		ФОЛИАНТ ПО СВЯЗИ:  Отлично, заношу улики в базу.#Р     
}
    - else:
        // Только Василиса
        Василиса подходит к группе напуганных альвов. После нескольких минут тихого разговора один из них указывает дрожащей рукой на север.
         ВАСИЛИСА: Альвы видели странные вспышки на том пике ровно три дня назад. Тогда все и началось.
}
~попытка=1
->Этап2_после_обнаружения

=== Этап2_после_обнаружения ===
ФОЛИАНТ ПО СВЯЗИ:  Источник на Пике Грифона! Желательно его обезвредить и очистить воздух. Ну, и про улики не забывайте!

->Этап2_Решение2

=== Этап2_Решение2 ===
ТЫ: Команда! Ваши предложения?
+ { дровосек == true} [Спросить Железного Дровосека]->Дровосек_предложение4
+ { дриада == true} [Спросить Флору]->Флора_предложение4
+ { кощей == true} [Спросить Кощея]->Кощей_предложение4
+ { иван == true} [Спросить Ивана Царевича]->Иван_предложение4
+ { кот == true} [Спросить Кота Ученого]->Кот_предложение4
+ { василиса == true} [Спросить Василису]->Василиса_предложение4

=== Дровосек_предложение4 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК:Я создам сеть магнитных фильтров и ветровых турбин. Таким образом, мы не только деактивируем источник, но и соберем уже выброшенную пыль, сохранив её для анализа. Это комплексное и контролируемое решение.#З

+ [Действуй!]->Дровосек_нейтрализация2
+ [Послушаем другие варианты]->Этап2_Решение2

=== Дровосек_нейтрализация2 ===
{
    - кот == true || иван == true:
        // КОМБИНАЦИЯ: Дровосек с Котом или Иваном
        Железный Дровосек устанавливает несколько устройств по краю плато.
        
        {
            - кот == true:
                КОТ УЧЕНЫЙ: В архивах есть схема подобных распылителей! У них есть резервный источник питания вот здесь...#З
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Учитываю. Корректирую параметры импульса для одновременного отключения основной и резервной систем.
            - иван == true:
                ИВАН ЦАРЕВИЧ: Я могу подняться на пик и визуально подтвердить отключение. И если что — вручную отключить.#З
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Хорошо. Координирую действие. Мои сенсоры будут отслеживать ваше продвижение.
        }
        
               ЖЕЛЕЗНЫЙ ДРОВОСЕК: Источник деактивирован. Пыль собрана. Анализ показывает уникальный сплав, патентованный гильдией «Громовые Шестерни». И мы получили данные о системе защиты устройства.
        
        ФОЛИАНТ ПО СВЯЗИ:  Отличная командная работа! #Р
        
       ~улика=улика+2 
    - else:
        // Только Дровосек 
        Железный Дровосек устанавливает несколько устройств по краю плато. С щелчком они активируются, создавая невидимую сеть. Железная пыль начинает притягиваться к ним, а с Пика Грифона видна вспышка короткого замыкания.
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Источник деактивирован. Пыль собрана. Анализ показывает уникальный сплав, патентованный гильдией «Громовые Шестерни».#Р
        
        ФОЛИАНТ ПО СВЯЗИ:  Отличная работа! Заношу в базу данных.


       ~улика=улика+1
}
->После_нейтрализации2

=== Флора_предложение4 ===
ФЛОРА: Эта ржавая дымка душит небо! Я призову чистый, омывающий ливень. Он будет нежным, как роса, и смоет всю эту гадость, не повредив облакам.#З

+ [Действуй!]->Флора_нейтрализация2
+ [Послушаем другие варианты]->Этап2_Решение2

=== Флора_нейтрализация2 ===
{
    - василиса == true || кощей == true:
        // КОМБИНАЦИЯ: Флора с Василисой или Кощеем
        {
            - василиса == true:
                ВАСИЛИСА: Альвы, помогите направить дождь так, чтобы он очистил, но не повредил корабли!
                      - кощей == true:
                КОЩЕЙ: А я пока, пожалуй, займусь отключением распылителя...
                    }
        
        Флора воздевает руки к небу, и с ясного неба обрушивается теплый ливень, мягко омывающий борта кораблей.
        ФЛОРА: Вот так-то лучше! Чистота! Проблема решена, предлагаю взять пробы воды, воздуха и грунта. Проведем анализ этой гадости.#Р
        ФОЛИАНТ ПО СВЯЗИ: Согласен, это может дать дополнительную информацию. Отличная командная работа!
           ~улика=улика+2  
    - else:
        // Только Флора
        Флора воздевает руки к небу, и с ясного неба обрушивается теплый, сильный ливень. Он смывает пыль с кораблей, и те сразу вздымаются выше, расправляя свои туманные паруса.
        
        ФЛОРА: Вот так-то лучше! Чистота! Проблема решена, предлагаю взять пробы воды, воздуха и грунта. Проведем анализ этой гадости.#Р
       ФОЛИАНТ ПО СВЯЗИ:  Согласен, это может дать дополнительную информацию.
               ~улика=улика+1
}
->После_нейтрализации2

=== Кощей_предложение4 ===
КОЩЕЙ: Я могу наложить локальную область анти-магии. Внутренняя магическая схема распылителя сотрётся. Пыль перестанет поступать и осядет вниз.#У

+ [Действуй!]->Кощей_нейтрализация2
+ [Послушаем другие варианты]->Этап2_Решение2

=== Кощей_нейтрализация2 ===
{
    - дриада == true || василиса == true:
        // КОМБИНАЦИЯ: Кощей с Флорой или Василисой
        {
            - дриада == true:
                ФЛОРА: Я постараюсь смягчить воздействие на окружающую природу...#З
                КОЩЕЙ: Хорошо. Тогда я сфокусирую заклинание только на устройстве.
            - василиса == true:
                ВАСИЛИСА: Кощей, если ты сотрёшь магическую схему, сможешь ли ты сохранить её копию для изучения?#З
                КОЩЕЙ: ...Интересная мысль... Да, пожалуй, смогу.
        }
        
        Кощей щелкает пальцами в сторону пика. Раздается глухой хлопок, и распылитель замирает.
        
        КОЩЕЙ: Готово. Магическая схема стёрта, но я сохранил её отпечаток. Пыль перестала поступать.#Р
        ФОЛИАНТ ПО СВЯЗИ: Отпечаток схемы? Это же готовая улика! Теперь мы узнаем принцип работы.#Р        
       ~улика=улика+1
    - else:
        // Только Кощей 
        Кощей щелкает пальцами в сторону пика. Раздается глухой хлопок, и распылитель замирает. Пыль перестает поступать, а та, что в воздухе, тяжело оседает на плато, покрывая его ржавым налетом. Работа сделана.
        
        ФОЛИАНТ ПО СВЯЗИ: Правда, придется альвам теперь убирать за собой.#У
}
->После_нейтрализации2

=== Иван_предложение4 ===
ИВАН ЦАРЕВИЧ: Вижу конструкцию - простую, но с хитрым креплением. Подобраться можно только по скалам. Я проберусь и аккуратно отключу его, перекусив питание и сбросив давление в резервуаре. Никаких взрывов, устройство останется целым. Иногда лучший ключ - не магия, а верный инструмент и ловкие руки.#З

+ [Действуй!]->Иван_нейтрализация2
+ [Послушаем другие варианты]->Этап2_Решение2

=== Иван_нейтрализация2 ===
{
    - дровосек == true || василиса == true:
        // КОМБИНАЦИЯ: Иван с Дровосеком или Василисой
        Иван, проявляя чудеса ловкости, карабкается по скалам к Пику Грифона.
        {
            - дровосек == true:
                ЖЕЛЕЗНЫЙ ДРОВОСЕК (по связи): Иван, рекомендую сначала отключить синий провод, затем красный. В противном случае сработает защита.#З
                ИВАН ЦАРЕВИЧ: Понял. Синий... красный... Готово. Устройство затихло.
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Подтверждаю деактивацию. Устройство цело, логи сохранены.
            - василиса == true && дровосек == false:
                ВАСИЛИСА (по связи): Иван, альвы говорят, что с восточной стороны есть более безопасный путь.
                ИВАН ЦАРЕВИЧ: Вижу, спасибо! ... Готово. Устройство отключено без повреждений.
                ВАСИЛИСА: Пока ты поднимался, альвы рассказали, что видели, как устройство устанавливали — двое людей в плащах с эмблемой шестерни.
				ИВАН ЦАРЕВИЧ: Вижу следы сапог. Альвы правы, злодеи поднимались здесь.
        }
        
        ФОЛИАНТ ПО СВЯЗИ:  Мастерская командная работа!#Р
        
       ~улика=улика+2 
    - else:
        // Только Иван 
        Иван, проявляя чудеса ловкости, карабкается по скалам к Пику Грифона. Он находит распылитель, изучает его несколько минут, затем перекусывает провода и откручивает несколько винтов в нужной последовательности. Устройство затихает.
        
        ИВАН ЦАРЕВИЧ: Готово. Механика - она простая, главное понять, какой проводок дернуть.#Р
        ФОЛИАНТ ПО СВЯЗИ:  Мастерская работа! Тащи механизм на базу, изучим и занесем в базу данных!
           ~улика=улика+1
}
->После_нейтрализации2

=== Кот_предложение4 ===
КОТ УЧЕНЫЙ: Кажется, нашёл... В «Трактате о тяжёлом небе» описан ритуал «Утяжеления Воздуха»! Если воздух станет плотнее, пыль не сможет в нём держаться и осядет! Правда, тут не сказано, что будет с самими облачными конструкциями... Теоретически это должно сработать!#У

+ [Действуй!]->Кот_нейтрализация2
+ [Послушаем другие варианты]->Этап2_Решение2

=== Кот_нейтрализация2 ===
{
    - кощей == true || дровосек == true:
        // КОМБИНАЦИЯ: Кот с Кощеем или Дровосеком (избегает катастрофы)
        КОТ УЧЕНЫЙ: Начинаю ритуал...
        
        {
            - кощей == true:
                КОЩЕЙ (резко): Стоп! Твой ритуал не учитывает магическую природу облаков! Дай я скорректирую...#З
                Кощей быстро произносит модифицированное заклинание. Воздух уплотняется, облака остаются стабильными. Пыль постепенно оседает.
				КОТ УЧЕНЫЙ: Ох, спасибо... Кажется мое заклинание чуть не привело к катастрофе...#П
            - дровосек == true:
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Профессор, мои расчёты показывают, что при уплотнении воздуха на 15% облачные структуры потеряют целостность. Предлагаю ограничить уплотнение 7%.#З
                КОТ УЧЕНЫЙ: Ох, точно! Спасибо! ... Ритуал работает, пыль оседает, корабли целы!..Постараюсь быть осторожнее...
        }
          
          - else:
        // Только Кот 
        Кот бормочет заклинание, и над верфью возникает небольшой вихрь, который беспорядочно носится по плато, поднимая еще больше пыли и заставляя альвов в ужасе разбегаться. Воздух становится тяжелым, заставляя корабли еще быстрее опускаться на дно ущелья.
        #Location: Verf_KATASTROFA
        // КАТАСТРОФА
        Облачные корабли один за другим теряют форму, превращаясь в бесформенные клочья тумана. Альвы, отчаянно цепляющиеся за свои творения, вместе с ними падают в бездну, их полупрозрачные тела растворяются в воздухе. Через несколько минут от величественной верфи не остаётся ничего, кроме ржавой пыли, медленно оседающей на опустевшем плато, и тишины, нарушаемой лишь завыванием ветра, который теперь некому укрощать.
        
        КОТ УЧЕНЫЙ: Ох... кажется, что-то пошло не так...#П
        
        ФОЛИАНТ ПО СВЯЗИ:  Быстрее возвращайтесь на базу! Дышать этой пылью вредно для здоровья!#З
        
       ~катастрофа = true
       ->Итоги2
}

=== Василиса_предложение4 ===
ВАСИЛИСА: Не нужно ничего отключать и рисковать! Первостепенная задача - спасти корабли! Мы можем позвать на помощь альвов, организовать живую цепь и аккуратно перенести самые пострадавшие корабли в безопасную зону! У нас появится время, чтобы спокойно обдумать, как отключить устройство. Работа в команде творит чудеса!#У

+ [Действуй!]->Василиса_нейтрализация2
+ [Послушаем другие варианты]->Этап2_Решение2

=== Василиса_нейтрализация2 ===
          Альвы, под руководством Василисы, начинают согласовано действовать. Им удаётся переместить несколько кораблей. Однако процесс идёт медленно, и не все корабли удаётся спасти вовремя.
        
        ФОЛИАНТ ПО СВЯЗИ:  Босс, большая часть кораблей спасена. Думаю, это неплохой результат. На верфь только что прибыли техномаги из столицы, они обезвредят источник. Жаль только, что вряд ли они дадут нам возможность покопаться в уликах... Возвращаетесь на базу?
->После_нейтрализации2

=== После_нейтрализации2 ===
#Location: Verf
Воздух на Верфи снова стал чистым и прозрачным. Облачные корабли медленно, но верно набирают высоту, их краски вновь заиграли на солнце. Альвы, успокоенные, возобновляют свою работу, бросая команде благодарные взгляды.

->Итоги2

=== Итоги2 ===
~всего_улик=всего_улик+улика
#Location: Ofis
ФОЛИАНТ:Передаю итоговый отчет.

{ катастрофа:
ФОЛИАНТ: Мы почти потеряли Небесную Верфь, босс. Для ее восстановления потребуются дополнительные ресурсы и месяцы работы лучших облакопрядов. Урок ясен, как неба после грозы: нам нужно тщательнее подбирать специалистов для каждой задачи. Молоток - не универсальный инструмент.#П
}

{улика>=3 :
ФОЛИАНТ: Великолепно! Мы не только остановили экологическую катастрофу, но и получили неопровержимые доказательства! Гильдия «Громовые Шестерни» точно стоит за этим. Их след становится все горячее!#Р
}

{ улика==2  && катастрофа==false:
ФОЛИАНТ: Верфь спасена, и альвы безмерно благодарны. Мы получили важную информацию, но ее пока недостаточно для полной картины. Расследование продолжается.
}

{улика<=1 && катастрофа==false:
ФОЛИАНТ: Мы не получили никаких новых зацепок! Корабли спасены, но противник остался в тени. Нам нужно быть удачливее в следующий раз.#П
}

{ кощей == true && иван == true:
ИВАН ЦАРЕВИЧ: Мы слишком часто используем магию. Иногда нужен простой подход.#У
КОЩЕЙ: (презрительно) Простота — удел примитивных умов.#З
}

{ кощей == true && дровосек == true:
КОЩЕЙ: (презрительно) Технология должна служить магам. Тогда и результат будет! #З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: (щелкает) Имею диаметрально противоположное мнение.#У
ФОЛИАНТ:  Не спорьте, у каждого свои методы! Но согласитесь, иногда вы вполне эффективно работаете вместе!#Р
}

КОГНИТИВНЫЙ КРИСТАЛЛ: В этом деле обнаружено улик: {улика}. Всего улик в базе данных: {всего_улик}
~катастрофа=false
ФОЛИАНТ: Поздравляю, босс, второе дело завершено. А у нас новый экстренный вызов! Проблема в затонувшей библиотеке! Медлить нельзя, срочная переброска!#У
ТЫ:
+[Перебрасывай нас туда немедленно!]
-> ЗАТОНУВШАЯ_БИБЛИОТЕКА
+[Затонувшая библиотека? Это еще что такое?]
КОГНИТИВНЫЙ КРИСТАЛЛ: Затонувшая библиотека — тысячелетний архив эльфов, покоящийся на дне Серебряного озера. Её светящиеся стены хранят тысячи магических манускриптов, запечатанных в пузыри замедленного времени. Более десяти тысяч свитков содержат все забытые языки, песни драконов, рецепты зелий из времён, когда текли медовые реки. Есть здесь и зеркальные гримуары, показывающие не прошлое, а возможное будущее.
->  ЗАТОНУВШАЯ_БИБЛИОТЕКА



=== ЗАТОНУВШАЯ_БИБЛИОТЕКА ===
#Location: Bibl_anomalia
Вы находитесь в затопленной эльфийской библиотеке, скрытой на дне озера. Дышать здесь можно только с помощью магии. Бесценные манускрипты и свитки плавают в толще воды, заключенные в пузыри воздуха. Но с ними творится нечто ужасное: чернила на пергаментах расплываются, буквы тают, как сахар, и исчезают в воде.
В углу все заросло какими-то странными водорослями. Они разрастаются прямо на глазах с невероятной скоростью. В толще воды мелькает тень духа-хранителя библиотеки. Он в отчаянии мечется между стеллажами, хватая то один, то другой рассыпающийся свиток.

ФОЛИАНТ ПО СВЯЗИ: Команда, вы на месте! Это катастрофа! Каждая секунда на счету. Нужно понять, что происходит и спасти то, что еще возможно!

~ улика = 0
~ попытка = 0

// Начало первого этапа
-> Этап1_Библиотека_Обнаружение

=== Этап1_Библиотека_Обнаружение ===
ТЫ: Почему чернила растворяются? Как можно найти причину?

+ { дровосек == true} [Спросить Железного Дровосека] -> Дровосек_библиотека_предложение
+ { дриада == true} [Спросить Флору] -> Флора_библиотека_предложение
+ { кощей == true} [Спросить Кощея] -> Кощей_библиотека_предложение
+ { иван == true} [Спросить Ивана Царевича] -> Иван_библиотека_предложение
+ { кот == true} [Спросить Кота Ученого] -> Кот_библиотека_предложение
+ { василиса == true} [Спросить Василису] -> Василиса_библиотека_предложение

=== Дровосек_библиотека_предложение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Я просканирую структурную целостность защитного поля. Выявлю точки наивысшей энтропии.#У
+ [Действуй!] -> Дровосек_библиотека_действие
+ [Послушаем, что скажут остальные] -> Этап1_Библиотека_Обнаружение

=== Дровосек_библиотека_действие ===
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Обнаружил аномалию: магическое поле деградирует с равномерной скоростью, что указывает на внешний катализатор. Источник - центральный зал. Проблема локализована, но ее природа мне не ясна.
~ попытка = 1
-> Библиотека_После_Обнаружения

=== Флора_библиотека_предложение ===
ФЛОРА: Я попробую вырастить водоросли-биоиндикаторы. Они чувствительны к магическим примесям и покажут самый «грязный» участок.#У
+ [Действуй!] -> Флора_библиотека_действие
+ [Послушаем, что скажут остальные] -> Этап1_Библиотека_Обнаружение

=== Флора_библиотека_действие ===
{
    - кощей == true:
        // Комбинация Флора + Кощей
        ФЛОРА: Я начинаю выращивать водоросли-индикаторы, Кощей, проследишь за магической реакцией?
        КОЩЕЙ: Договорились. Только не переборщи с удобрением, дриада.
        Флора выпускает споры, а Кощей направляет магический поток на них. Водоросли растут, вытягиваясь по направлению к источнику.
        ФЛОРА: Водоросли показывают, что реагент идет из вентиляционной шахты. #З
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Команда, вы великолепны! Мы нашли источник и природу реагента!#Р
        }
        ~ попытка = 1
        -> Библиотека_После_Обнаружения
    - else:
        Флора выпускает в воду споры магических водорослей. Они быстро прорастают, но начинают буйно цвести по всей библиотеке, опутывая свитки и еще сильнее нарушая хрупкое магическое равновесие.
        ФЛОРА: Ой... Кажется, реагент действует на них как удобрение! Я только усугубила ситуацию...#П
        ~ попытка = 1
        -> Этап1_Библиотека_Обнаружение
}

=== Кощей_библиотека_предложение ===
КОЩЕЙ: Проведу общий анализ магического вырождения. Определю, что за сила разъедает заклятья.#У
+ [Действуй!] -> Кощей_библиотека_действие
+ [Послушаем, что скажут остальные] -> Этап1_Библиотека_Обнаружение

=== Кощей_библиотека_действие ===
{
    - кот == true:
        // Комбинация Кощей + Кот
        Кощей проводит рукой, и вода в центральном зале начинает светиться.
        КОТ УЧЕНЫЙ: О, это же следы реагента «Чернильная смерть»! Я читал о нем! Он был изобретен гильдией «Громовые Шестерни» для уничтожения магических текстов!#З
        КОЩЕЙ: Точно. И следы ведут к вентиляционной шахте. #Р
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Идеально! Мы знаем реагент и виновника!#Р
        }
    - else:
        Кощей проводит рукой, и вода в центральном зале начинает светиться зловещим фиолетовым светом.
        КОЩЕЙ: Энергия распада исходит из центрального зал. Грубая техномагическая атака.
}
~ попытка = 1
-> Библиотека_После_Обнаружения

=== Иван_библиотека_предложение ===
ИВАН ЦАРЕВИЧ: Я поищу механические повреждения - трещины, вмятины. Если целостность стен нарушена, мог просочиться внешний загрязнитель.#У
+ [Действуй!] -> Иван_библиотека_действие
+ [Послушаем, что скажут остальные] -> Этап1_Библиотека_Обнаружение

=== Иван_библиотека_действие ===
{
    - василиса == true:
        // Комбинация Иван + Василиса
        ВАСИЛИСА: Иван, хранитель говорит, что видел, как кто-то возился у вентиляционной решетки в восточном углу.#З
        ИВАН ЦАРЕВИЧ: Понял. Иду смотреть.
        Иван идет к указанному месту и находит следы взлома решетки и капли неизвестной жидкости.
        ИВАН ЦАРЕВИЧ: Вот! Здесь явно проникали. И есть капли реагента. Мы можем взять образец.#Р
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Образец реагента и следы взлома — отличные улики!#Р
        }
        ~ попытка = 1
        -> Библиотека_После_Обнаружения
    - else:
        Иван тщательно простукивает стены, но не находит повреждений.
        ИВАН ЦАРЕВИЧ: Кладка цела. Значит, яд проник как-то иначе... Мой метод не может обнаружить способ проникновения реагента.#П
        ~ попытка = 1
        -> Этап1_Библиотека_Обнаружение
}

=== Кот_библиотека_предложение ===
КОТ УЧЕНЫЙ: Проведу диагностику защитных печатей библиотеки. Они явно повреждены. Нужно найти, какое именно заклятье было нарушено первым.#З
+ [Действуй!] -> Кот_библиотека_действие
+ [Послушаем, что скажут остальные] -> Этап1_Библиотека_Обнаружение

=== Кот_библиотека_действие ===
        Кот, не обращая внимания на воду, лихорадочно листает парящий перед ним каталог печатей.
        КОТ УЧЕНЫЙ: Нашел! Печать Целостности Знаний нарушена! Я вижу след... реагент, медленно разъедающий магическую основу! Его источник — вентиляционная шахта древней системы охлаждения!#З
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Отличная работа! Заношу улику в базу данных!#Р
        }
~ попытка = 1
-> Библиотека_После_Обнаружения

=== Василиса_библиотека_предложение ===
ВАСИЛИСА: Попробую поговорить с Хранителем этого места. Духи места обычно знают больше, чем кажется.#З
+ [Действуй!] -> Василиса_библиотека_действие
+ [Послушаем, что скажут остальные] -> Этап1_Библиотека_Обнаружение

=== Василиса_библиотека_действие ===
{
    - кот == true:
        // Комбинация Василиса + Кот
        ВАСИЛИСА: Хранитель говорит, что сюда приходил странный посеитель.  Черноволосый невысокий человек средних лет. В больших очках, с растрепанными волосами... Он что-то влил в вентиляционную шахту!
        КОТ УЧЕНЫЙ: (шепчет) Спроси про его одежду, может,на ней был герб или знак.
        Василиса задает наводящие вопросы, и хранитель описывает эмблему на плаще: скрещенные молот и шестерня.
        КОТ УЧЕНЫЙ: Это же символ гильдии «Громовые Шестерни»! И в летописях упоминается, что они разрабатывали реагенты для уничтожения магических текстов!#З
        {
            - попытка == 0:
                ~ улика = улика + 2
                ФОЛИАНТ ПО СВЯЗИ:  Потрясающе! Мы имеем прямое указание на гильдию и знаем их методы!#Р
        }
    - else:
        Василиса тихо беседует с духом Хранителем. Разговор продолжается довольно долго.
        ВАСИЛИСА: Хранитель рассказал мне о необычном посетителе. Черноволосый невысокий человек средних лет. В больших очках, с растрепанными волосами... Он что-то влил в вентиояционную шахту!
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ:  Описание злоумышленника - отличная зацепка!
        }
}
~ попытка = 1
-> Библиотека_После_Обнаружения

=== Библиотека_После_Обнаружения ===
ФОЛИАНТ ПО СВЯЗИ: Хорошо, причина ясна! В библиотеку залили какой-то реагент через систему вентиляции. Теперь нужно его нейтрализовать и остановить распад, пока не погибли все свитки!
-> Этап2_Библиотека_Решение

=== Этап2_Библиотека_Решение ===
ТЫ: Надо спасать библиотеку! Команда, предлагайте решение!

+ { дровосек == true} [Спросить Железного Дровосека] -> Дровосек_библиотека_предложение2
+ { дриада == true} [Спросить Флору] -> Флора_библиотека_предложение2
+ { кощей == true} [Спросить Кощея] -> Кощей_библиотека_предложение2
+ { иван == true} [Спросить Ивана Царевича] -> Иван_библиотека_предложение2
+ { кот == true} [Спросить Кота Ученого] -> Кот_библиотека_предложение2
+ { василиса == true} [Спросить Василису] -> Василиса_библиотека_предложение2

=== Дровосек_библиотека_предложение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Я могу создать локальный герметичный купол вокруг зоны наибольшего поражения. Это остановит распространение реагента, но не обратит процесс вспять. Мы сохраним до 60% текстов.#У
+ [Действуй!] -> Дровосек_библиотека_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Библиотека_Решение

=== Дровосек_библиотека_нейтрализация ===
{
    - кот == true:
        // Комбинация Дровосек + Кот
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Создаю герметичный купол. Профессор, вам нужно определить самые ценные свитки, которые находятся на грани разрушения, и мы их сразу скопируем.
        КОТ УЧЕНЫЙ: Уже делаю! У меня есть список из «Каталога особо ценных манускриптов». Вот эти десять нужно спасти в первую очередь!#З
        Дровосек и Кот работают слаженно: купол останавливает распад, а Кот успевает сделать копии самых важных текстов до того, как они разрушатся.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Уцелело 80% текстов. Устройство-источник изолировано, для его изучения потребуется время.#Р
        КОТ УЧЕНЫЙ: И я сохранил копии самых ценных манускриптов! #Р
		 ~ улика = улика + 1
   			ФОЛИАНТ ПО СВЯЗИ: Принято!
           - else:
        Железный Дровосек создает силовое поле, изолирующее центральный зал. Распад останавливается.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Уцелело 65% текстов. Это даже выше предварительных расчетов. Устройство-источник осталось внутри купола, для его изучения потребуется время.
}
-> Библиотека_После_Нейтрализации

=== Флора_библиотека_предложение2 ===
ФЛОРА: Я чувствую, как вода пытается очиститься сама, но у неё не хватает сил! Давайте поможем ей - я выращу особый био-фильтр из глубинного мха. Он впитает всю эту скверну, как губка!#У
+ [Действуй!] -> Флора_библиотека_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Библиотека_Решение

=== Флора_библиотека_нейтрализация ===
{
    - кощей == true:
        // Комбинация Флора + Кощей
        ФЛОРА: Я выращу био-фильтр, но боюсь, что он выйдет из-под контроля.#У
        КОЩЕЙ: Я поставлю магический барьер, чтобы ограничить его рост. Начинай.#З
        Флора выпускает споры мха, а Кощей контролирует его рост магией. Мох впитывает реагент, но не распространяется на свитки.
        ФЛОРА: Получилось! Мох впитал реагент, и теперь его можно собрать для анализа.#Р
        КОЩЕЙ: Я выделил реагент в чистом виде. Это именно тот, что производится «Громовыми Шестернями».#Р
        ~ улика = улика + 1
		ФОЛИАНТ ПО СВЯЗИ:  Отлично сработали! #Р
        -> Библиотека_После_Нейтрализации
    - дровосек == true:
        // Комбинация Флора + Дровосек
        ФЛОРА: Я выращу мох, который впитает реагент, но он может повредить свитки.#У
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Я создам микро-силовые поля вокруг каждого свитка, чтобы защитить их.#З
        Дровосек устанавливает защиту, а Флора выращивает мох. Реагент поглощен, свитки не пострадали.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Реагент собран. Анализ показывает состав, характерный для гильдии «Громовые Шестерни».
        ~ улика = улика + 1
		ФОЛИАНТ ПО СВЯЗИ: Отлично сработали! #Р
        -> Библиотека_После_Нейтрализации
    - else:
        // Только Флора - катастрофа
        Флора выпускает в воду споры магического мха. На мгновение кажется, что это сработало - но затем под влиянием реагента мох начинает буйствовать с ужасающей скоростью. Нежные ростки за секунды превращаются в толстые слизистые побеги, которые обволакивают свитки мертвой хваткой.
#Location: Bibl_KATASTROFA
        Вода внезапно вспенивается и чернеет. Дух-хранитель издаёт последний беззвучный крик и тает, словно капля в грязной воде. Стены библиотеки покрываются мерзкой чёрной плесенью, пожирающей последние фрески. От веками хранимых знаний остаётся лишь мутная, зловонная жижа, в которой плавают обломки полок.
ФОЛИАНТ ПО СВЯЗИ:  (в панике) Что это, босс?!? Что происходит? #З
        ~ катастрофа = true
        -> Библиотека_Итоги
}

=== Кощей_библиотека_предложение2 ===
КОЩЕЙ: Я инвертирую химико-магическую формулу реагента. Это превратит его в безвредный осадок. Мы получим и спасённые свитки, и вещественное доказательство.#З
+ [Действуй!] -> Кощей_библиотека_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Библиотека_Решение

=== Кощей_библиотека_нейтрализация ===
{
    - кот == true:
        // Комбинация Кощей + Кот
        КОЩЕЙ: Я нейтрализую реагент, а ты, Кот, постарайся найти в летописях информацию о том, как его разложить без остатка.#З
        КОТ УЧЕНЫЙ: Уже ищу! Вот: «Чернильная смерть» разлагается под воздействием заклинания обратного отсчета, но для этого нужно знать точную формулу.
        КОЩЕЙ: Формула у меня. Начинаем.
        Кощей и Кот произносят совместное заклинание. Реагент разлагается без осадка.
        КОЩЕЙ: Реагент уничтожен.#Р
        КОТ УЧЕНЫЙ: И я нашел упоминание, что этот реагент использовался в 1235 году «Громовыми Шестернями» для уничтожения библиотеки соперников.#Р
        ~ улика = улика + 2
		ФОЛИАНТ ПО СВЯЗИ:  Вот что значит командная работа! #Р
    - else:
        Кощей произносит короткую, режущую слух формулу. Вода в центральном зале мутнеет, и со дна поднимается облако бурого осадка, а распад текстов мгновенно прекращается.
        КОЩЕЙ: Готово. Реагент нейтрализован.
        ФОЛИАНТ ПО СВЯЗИ:  Отлично! Теперь у вас есть время чтобы тщательно собрать следы и улики!
        ~ улика = улика + 1
}
-> Библиотека_После_Нейтрализации

=== Иван_библиотека_предложение2 ===
ИВАН ЦАРЕВИЧ: Вижу сосуд с реагентом - источник заражения. Проберусь к нему и помещу его в герметичный контейнер. Рискну, но это единственный способ остановить распад мгновенно. И мы получим реагент для изучения.#З
+ [Действуй!] -> Иван_библиотека_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Библиотека_Решение

=== Иван_библиотека_нейтрализация ===
{
    - василиса == true:
        // Комбинация Иван + Василиса
        ИВАН ЦАРЕВИЧ: Я ныряю за сосудом.
        ВАСИЛИСА: Я попрошу хранителя помочь тебе, он укажет точное местоположение и защитит от остаточного реагента.#З
        Иван, с помощью хранителя, быстро находит сосуд и поднимает его и помещает в контейнер.
        ИВАН ЦАРЕВИЧ: Сосуд в целости. И здесь есть гравировка: «Собственность гильдии «Громовые Шестерни».#Р
        ВАСИЛИСА: Хранитель говорит, что видел, как злоумышленник ставил сосуд, и запомнил его лицо. Мы можем создать его портрет.#Р
        ~ улика = улика + 2
		ФОЛИАНТ ПО СВЯЗИ:  Здорово, мы все ближе к цели!#Р
    - дровосек == true:
        // Комбинация Иван + Дровосек
        ИВАН ЦАРЕВИЧ: Я достану сосуд, а ты, Дровосек, подготовь контейнер для его анализа.#З
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Готово. Я даже могу провести предварительный анализ прямо на месте.#З
        Иван достает сосуд, Дровосек немедленно помещает его в контейнер и сканирует.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Сосуд изготовлен из сплава, патентованного гильдией «Громовые Шестерни». Реагент внутри соответствует их формуле.#Р
        ~ улика = улика + 2
		ФОЛИАНТ ПО СВЯЗИ:  Здорово, мы все ближе к цели!#Р
    - else:
        Иван, задержав дыхание, ныряет в эпицентр заражения. Ловкостью и силой он достает небольшой кристаллический стержень - сосуд с реагентом. Распад прекращается.
        ИВАН ЦАРЕВИЧ: Готово. Реагент в контейнере и готов к изучение.#Р
        ФОЛИАНТ ПО СВЯЗИ:  Здорово! Возвращайтесь скорее, мне не терпится провести химический анализ!
        ~ улика = улика + 1
}
-> Библиотека_После_Нейтрализации

=== Кот_библиотека_предложение2 ===
КОТ УЧЕНЫЙ: Применю заклинание мгновенного копирования! Успею скопировать десяток ценнейших манускриптов до их полного разрушения. Знания будут спасены, хоть сами артефакты и утрачены.#У
+ [Действуй!] -> Кот_библиотека_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Библиотека_Решение

=== Кот_библиотека_нейтрализация ===
{
    - василиса == true && кощей == false:
        // Комбинация Кот + Василиса
        КОТ УЧЕНЫЙ: Я начну копирование, но мне нужно время.#П
        ВАСИЛИСА: Я организую духов библиотеки, чтобы они помогли тебе. Они могут замедлить распад на время.#З
        Василиса призывает духов, и они стабилизируют магическое поле вокруг свитков, замедляя распад. Кот успевает скопировать в два раза больше манускриптов.
        КОТ УЧЕНЫЙ: Удалось спасти больше половины текстов! #Р
    - кощей == true:
        // Комбинация Кот + Кощей
        КОТ УЧЕНЫЙ: Я копирую, но реагент разрушает слишком быстро.#П
        КОЩЕЙ: Я приостановлю время в небольшой области вокруг тебя. У тебя будет минут десять.#З
        Кощей замедляет время для Кота, и тот успевает скопировать почти все манускрипты.
        КОТ УЧЕНЫЙ: Я сохранил почти все тексты! И в одном из древних свитков нашел формулу реагента и способ его нейтрализации, который мы можем использовать.#Р
		ФОЛИАНТ ПО СВЯЗИ:  Отлично!
        ~ улика = улика + 1
    - else:
        Кот успевает создать магические копии нескольких десятков самых важных манускриптов, прежде чем оригиналы полностью растворяются.
        КОТ УЧЕНЫЙ: Знания спасены, но артефакты утрачены навсегда... Я смог сохранить около трети текстов.
}
-> Библиотека_После_Нейтрализации

=== Василиса_библиотека_предложение2 ===
ВАСИЛИСА: Мы теряем драгоценное время на споры! Нужно организовать рабочих-гномов из соседних тоннелей и альвов-писцов. Пусть гномы оперативно перенесут наименее поврежденные свитки в сухие боксы, а альвы начнут их экстренное копирование! Мы создадим систему быстрого реагирования и спасем максимум знаний!#У
+ [Действуй!] -> Василиса_библиотека_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Библиотека_Решение

=== Василиса_библиотека_нейтрализация ===
{
    - дровосек == true:
        // Комбинация Василиса + Дровосек
        ВАСИЛИСА: Я организую рабочих, но нужно быстро перенести свитки.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Я соберу транспортные платформы и защитные коконы для каждого свитка.#З
        Дровосек создает устройства, и рабочие быстро переносят свитки. Удается спасти 80% манускриптов.
    	ФОЛИАНТ ПО СВЯЗИ: Неплохая работа, команда!
        ~ улика = улика + 1
    - else:
        С присущим ей дипломатическим тактом Василиса начинает организовывать рабочих-гномов и альвов-писцов, терпеливо объясняя каждому их роль в спасении свитков. Но пока она улаживает споры, пока альвы церемонно распределяют обязанности, а гномы с дотошной неторопливостью проверяют герметичность боксов, магический реагент продолжает свою разрушительную работу.
        За эти долгие минуты переговоров и организации десятки бесценных манускриптов успевают полностью раствориться в воде - от них остаются лишь призрачные облачка чернильной дымки.
        ВАСИЛИСА: Наконец-то! Начинаем операцию по спасению!..
...........
		ВАСИЛИСА: У нас получилось спасти около половины манускриптов.
}
-> Библиотека_После_Нейтрализации

=== Библиотека_После_Нейтрализации ===
#Location: Bibl
Вода в библиотеке постепенно светлеет. Распад прекратился, и уцелевшие свитки медленно опускаются на свои полки. Дух-хранитель успокаивается, его полупрозрачная форма становится чуть ярче.
-> Библиотека_Итоги

=== Библиотека_Итоги ===
#Location: Ofis
~ всего_улик = всего_улик + улика
ФОЛИАНТ: Передаю итоговый отчет.

{ катастрофа:
ФОЛИАНТ: Босс... это катастрофа. Столько знаний пропало! Боюсь, с нас еще спросят на Совете...#П
}

{ улика>=3:
ФОЛИАНТ: Фантастика! Мы не только остановили уничтожение знаний, но и добыли вещественные доказательства. Цепочка ведет все к тем же «Громовым Шестерням», но теперь мы знаем и их методы. Все указывает на Мастера Грома - главу гильдии!#Р
}

{ улика == 2 && катастрофа==false:
ФОЛИАНТ: Библиотека спасена, и это главное. Мы получили важный фрагмент головоломки, но полной картины все еще нет. Возможно, здесь замешан Мастер Гром, глава гильдии Громовых Шестерней
}

{ улика<=1 && катастрофа==false:
ФОЛИАНТ: Знания, к счастью, частично сохранены. Но материальные доказательства утеряны. Наш противник почти не оставляет следов.#У
}
{ кот == true && иван == true:
КОТ УЧЕНЫЙ: Если бы у меня было больше времени на изучение архивов, я бы уже распутал это дело! #П
ИВАН ЦАРЕВИЧ: Но времени-то у нас и нет! Иногда нужно действовать, а не бумажки перекладывать!#З
КОТ УЧЕНЫЙ: Вам этого не понять, молодой человек! #У
}

КОГНИТИВНЫЙ КРИСТАЛЛ: Найдено улик в этом деле: {улика}. Всего улик: {всего_улик}
~катастрофа=false
ФОЛИАНТ: Третье дело закрыто. Готовы к новым приключениям? На кухне Великана Грумбеля происходит что-то неладное. Туда и отправимся дальше!
ТЫ:
+ [Перебрасывай нас к Великану!] -> КУХНЯ_ВЕЛИКАНА
+ [Кухня Великана? Расскажи подробнее.] -> Инфо_Кухня_Великана

=== Инфо_Кухня_Великана ===
КОГНИТИВНЫЙ КРИСТАЛЛ: Великан Грумбель, знаменитый шеф-повар, известен своими кулинарными экспериментами. Автор "Монструозной Поваренной Книги" и сборника рецептов "Здоровое питание троллей". Его последний рецепт вызывает климатические аномалии. Нужно выяснить, кто его надоумил использовать опасные ингредиенты, и уговорить его остановиться.
-> КУХНЯ_ВЕЛИКАНА








=== КУХНЯ_ВЕЛИКАНА ===
#Location: Kuhnia_anomalia
Вы перенеслись в огромную, дымящуюся кухню в пещере великана по имени Грумбель. Горшки размером с дома кипят на кострах, с потолка свисают гирлянды из перцев чихач-дракон и луковиц плакса-великан. На столе валяется пергамент с рецептом, исписанный убористыми письменами.

Сам Грумбель, в заляпанном фартуке, бормочет себе под нос и с энтузиазмом помешивает в главном котле что-то ядовито-фиолетовое. Над пещерой бушует ад: бьют молнии, ветер сносит крыши, а с неба падает град размером с кулак. Источник бури - испарения от котла.

ФОЛИАНТ ПО СВЯЗИ:  (голос из коммуникатора, пробивающийся сквозь шум грозы и бульканье котла) Команда, Грумбель снова за своё! Его новый фирменный рецепт вызывает климатическую аномалию. Будьте осторожны: сейчас он в прекрасном настроении, но характер у него взрывной!

~ улика = 0
~ попытка = 0

// Начало первого этапа
-> Этап1_Кухня_Обнаружение

=== Этап1_Кухня_Обнаружение ===
Перед тобой стоят члены твоей команды. Ты можешь спросить мнение каждого или сразу отправить вместе тех, кто, по твоему мнению, может сработаться. Ты понимашь, что командная работа может принести гораздо больше пользы... Если, конечно, команда составлена правильно...

+ { иван == true} [Спросить Ивана Царевича] -> Иван_кухня_предложение
+ { дриада == true} [Спросить Флору] -> Флора_кухня_предложение
+ { кот == true} [Спросить Кота Ученого] -> Кот_кухня_предложение
+ { василиса == true} [Спросить Василису] -> Василиса_кухня_предложение
+ { дровосек == true} [Спросить Железного Дровосека] -> Дровосек_кухня_предложение
+ { кощей == true} [Спросить Кощея] -> Кощей_кухня_предложение

+{ иван == true && василиса == true } [Отправить Ивана и Василису работать вместе] -> Иван_Василиса_комбо_действие
+{ кощей == true && иван == true } [Отправить Кощея и Ивана работать вместе] -> Кощей_Иван_комбо_действие
+{ кощей == true && дриада == true } [Отправить Кощея и Флору работать вместе] -> Кощей_Флора_комбо_действие
+{ кощей == true && дровосек == true } [Отправить Кощея и Дровосека работать вместе] -> Дровосек_Кощей_кухня_комбо_обнаружение
+{ дровосек == true && дриада == true } [Отправить Дровосека и Флору работать вместе] -> Дровосек_Флора_комбо_действие
+{ кот == true && василиса == true } [Отправить Кота и Василису работать вместе] -> Кот_Василиса_комбо_действие
+{ василиса == true && дриада == true } [Отправить Флору и Василису работать вместе] -> Василиса_Флора_комбо_обнаружение
+{ кощей == true && василиса == true } [Отправить Кощея и Василису работать вместе] -> Василиса_Кощей_кухня_обнаружение
+{ иван == true && кот == true } [Отправить Кота и Ивана работать вместе] ->Иван_Кот_комбо_действие 
+{ дриада == true && кот == true } [Отправить Кота и Флору работать вместе] ->Флора_Кот_кухня_комбо_обнаружение


=== Василиса_Кощей_кухня_обнаружение ===
ВАСИЛИСА: (подходя с дружелюбной улыбкой) Грумбель, это пахнет… волшебно! Ты не подскажешь, откуда у тебя рецепт?#Р
КОЩЕЙ: Не трать время на светские беседы. Искомый ингредиент прямо на столе.#У
Он указывает пальцем на мешок с синими ягодами. От его жеста ягоды на мгновение вспыхивают зловещим фиолетовым светом, выдавая магическую природу.
ГРУМБЕЛЬ: (насупливается) Эй, ты! Не порть товар своими фокусами! И не перебивай даму!#У
ВАСИЛИСА: Грумбель, не сердись! Он лишь хотел подчеркнуть их необычность! Такие редкие ягоды, наверное, сложно достать?
ГРУМБЕЛЬ: Маленький человечек дал… на пробу. А теперь не мешайте, у меня бульон убегает!#У
Кощей фыркает и отворачивается, потеряв интерес. Возможность расспросить подробнее упущена.
-> Кухня_После_Обнаружения

=== Дровосек_Кощей_кухня_комбо_обнаружение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Для анализа аномалии необходимо взять пробы пара, почвы и образец супа. Я проведу химико-магическую спектрометрию на месте.#З
КОЩЕЙ: (с усмешкой) Ты собираешься измерять хаос? Позволь мне сделать это проще.#З
Кощей щёлкает пальцами. Над кипящим котлом возникает бледная, мерцающая сфера, втягивающая в себя клубы пара. Внутри сферы проступают искажённые символы — следы чужеродной магии.
КОЩЕЙ: Видишь? Грубая техномагическая примесь. Источник — те синие ягоды. Но кто их произвёл, этот метод не покажет.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Мой анализ подтверждает аномальный состав ягод
ГРУМБЕЛЬ: Эй! Что вы там колдуете? Не портите атмосферу! Или я вас половником выгоню!#У
ФОЛИАНТ ПО СВЯЗИ: Мы установили техногенную природу ягод. Великан раздражён, рекомендую вам отступить. 
{
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Вы определили угрозу, но напугали великана. Он теперь ни за что не отдаст образцы. Без вещественных доказательств наша улика — лишь теория.
                }
~ попытка = 1
-> Кухня_После_Обнаружения



=== Флора_Кот_кухня_комбо_обнаружение ===
ФЛОРА: Котик, обрати внимание на те ягодки. С ними что-то не так... Они выросли в темноте, без солнца, без ласкового дождика... их корни ни разу не касались земли! #П
КОТ УЧЕНЫЙ: М-да... действительно, внешний вид аномальный. Структура слишком правильная для природного плода... Посмотрю в "Каталоге запрещённой алхимии", раздел "Растения искусственные"... #У
Грумбель перестаёт мешать суп и смотрит на них с подозрением.
ГРУМБЕЛЬ: Эй, вы что там шепчетесь? Не трогайте мои ягодки! Они для супа!
ФЛОРА: Грумбель, они же не настоящие! Они не могут дать вкуса! Они сделаны, чтобы причинять вред!#У
КОТ УЧЕНЫЙ: Нашёл! "Грозоягода синтетическая", продукт Гильдии Горных Инженеров, позднее — "Громовых Шестерён". Боже мой, это же метеорологическое оружие, замаскированное под пищевой продукт!#З
Грумбель хмурится, его огромная рука накрывает мешок с ягодами.
ГРУМБЕЛЬ: Какое ещё оружие? Это просто ягодки для вкуса и гро-о-ома! Человечек сказал!#У
ФЛОРА: Он обманул тебя! Посмотри — они даже пахнут не ягодами, а... ржавым металлом!#З
ГРУМБЕЛЬ: (раздражённо) Хватит! Вы тут со своими книжками и цветочками меня сбиваете! Я шеф! Я знаю, что делаю! Убирайтесь с моей кухни!#У
ФОЛИАНТ ПО СВЯЗИ: Вы определили природу угрозы и даже вычислили производителя, но... напугали великана. Он теперь ни за что не отдаст образцы и не признает ошибку. Без вещественных доказательств ваша улика — лишь теория.
{
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Вы определили угрозу, но напугали великана. Он теперь ни за что не отдаст образцы. Без вещественных доказательств наша улика — лишь теория.
                }
~ попытка = 1
-> Кухня_После_Обнаружения









=== Иван_кухня_предложение ===
ИВАН ЦАРЕВИЧ: Я прокрадусь, поищу следы постороннего присутствия. Прослежу, откуда Грумбель берет добавки и подслушаю, что он бормочет. #З
+ [Действуй!] -> Иван_кухня_действие
+ [Послушаем других] -> Этап1_Кухня_Обнаружение

=== Иван_кухня_действие ===
Иван, как тень, скользит между гигантскими кухонными принадлежностями. Он замирает у стола, где лежат мешки с ингредиентами.

ИВАН ЦАРЕВИЧ: (возвращаясь) Вот. Мешок с синими ягодами. На нём вышит знак — скрещенные молот и шестерня. И Грумбель что-то бормочет про "маленького человечка", который ему их дал. Говорит: "Спасибо тому маленькому человечку. И верно, грозности добавляет..."
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Отличная работа! Мешок с клеймом "Громовые Шестерни" - явная улика!#Р
        }
~ попытка = 1
-> Кухня_После_Обнаружения

=== Флора_кухня_предложение ===
ФЛОРА: Я определю, какие травы Грумбель использует. Один из ингредиентов будет явно чужеродным, не из здешних лесов.#З
+ [Действуй!] -> Флора_кухня_действие
+ [Послушаем других] -> Этап1_Кухня_Обнаружение

=== Флора_кухня_действие ===
        Флора закрывает глаза, вдыхая ароматы кухни.
        ФЛОРА: Вот он! Источник хаоса! Те синие ягоды... Такие у нас не растут! Их вырастили не в лесу, а в лаборатории!
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: След магической селекции на ягодах - отличная улика!#Р
        }
~ попытка = 1
-> Кухня_После_Обнаружения

=== Кот_кухня_предложение ===
КОТ УЧЕНЫЙ: Проведу исторический анализ рецептов великанов. Узнаю, использовали ли они раньше такие мощные ингредиенты.#У
+ [Действуй!] -> Кот_кухня_действие
+ [Послушаем других] -> Этап1_Кухня_Обнаружение

=== Кот_кухня_действие ===
        Кот листает парящие свитки, которые он каким-то образом умудрился развернуть даже в этой дымящейся кухне.

        КОТ УЧЕНЫЙ: Так, м-да, в "Кулинарных хрониках" подобные рецепты не описаны. Это новшество. И весьма опасное. Похоже, дело вон в тех ягодах... Я нигде не нашел их описания...
        ~ попытка = 1
-> Кухня_После_Обнаружения

=== Василиса_кухня_предложение ===
ВАСИЛИСА: Я расспрошу Грумбеля о рецепте. Узнаю, не делился ли кто недавно с ним особым секретом.  Он доверчив, если подойти правильно. Главное — не спугнуть его и не задеть гордость шефа.#У
+ [Действуй!] -> Василиса_кухня_действие
+ [Послушаем других] -> Этап1_Кухня_Обнаружение

=== Василиса_кухня_действие ===
        Василиса подходит к великану с дружелюбной улыбкой.
        ВАСИЛИСА: Грумбель, это пахнет просто божественно! А кто тебе подсказал такой необычный рецепт?#Р
        ГРУМБЕЛЬ: А, какой-то маленький человечек! Дал попробовать - ягодка как ягодка, а грому-то сколько! Я опять стану победителем в "Битве шефов"! Третий раз подряд! #Р
		ГРУМБЕЛЬ: Отойди, не мешай. или ты хочешь выведать мои секреты?#У
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ: Отлично, заношу данные в базу#Р
}
~ попытка = 1
-> Кухня_После_Обнаружения

=== Дровосек_кухня_предложение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Попробую провести спектральный анализ паров из котла. Вычислю химический состав.#У
+ [Действуй!] -> Дровосек_кухня_действие
+ [Послушаем других] -> Этап1_Кухня_Обнаружение

=== Дровосек_кухня_действие ===
        Железный Дровосек проводит анализ паров.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Обнаружены аномальные соединения, указывающие на неизвестный магический катализатор в продукте питания. Источник проблемы - эти синие ягоды, но кто их предоставил, неизвестно.

~ попытка = 1
-> Кухня_После_Обнаружения

=== Кощей_кухня_предложение ===
КОЩЕЙ: Просто просканирую зелье котла диагностическим заклятьем. Это может показать нестабильный элемент.#У
+ [Действуй!] -> Кощей_кухня_действие
+ [Послушаем других] -> Этап1_Кухня_Обнаружение

=== Кощей_кухня_действие ===
        Кощей проводит быстрый магический анализ.
        КОЩЕЙ: Зелье перенасыщено магией бури. Источник - вон те синие ягоды. Их происхождение мне неизвестно. Я не разбираюсь в ботанике.
~ попытка = 1
-> Кухня_После_Обнаружения

// ========== КОМБИНАЦИОННЫЕ ДЕЙСТВИЯ (без предварительного опроса) ==========
=== Иван_Василиса_комбо_действие ===
Вы киваете Ивану и Василисе. Они обмениваются понимающими взглядами и начинают действовать синхронно.

ВАСИЛИСА: (подходя с улыбкой) Грумбель, какой аромат! Ты, как всегда, на высоте. А этот новый ингредиент — так интересно! Кто же такой гений, что подсказал тебе его?#Р

Пока Василиса отвлекает великана, Иван, как тень, скользит вдоль стола. Его глаза выхватывают детали: помятый пергамент, торчащий из кармана фартука.

ГРУМБЕЛЬ: (бормочет, помешивая) А, человечек один... Маленький, в очках. Говорит, «попробуй, сила грома в каждой ягодке!» Ну я и попробовал!

ИВАН ЦАРЕВИЧ: (шепотом, показывая бумажку) Босс, смотрите. На бумаге — печать. Молот и шестерня. И подпись: «Поставка №45-Г для эксперимента ГК».#З

ВАСИЛИСА: (продолжая мягко) А он не сказал, как его зовут, этого человечка?#Р

ГРУМБЕЛЬ: Не-а... Но бумажку оставил! Где же она... Куда-то задевалась...

{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ:  Блестяще! Дипломатия плюс ловкость рук. Теперь у нас материальная улика с печатью!#Р
}

~ попытка = 1
-> Кухня_После_Обнаружения

=== Дровосек_Кот_комбо_действие ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Начинаю спектральный анализ частиц в воздухе. Обнаружена аномальная концентрация ионизированных магических частиц возле мешка с синими ягодами.#З

КОТ УЧЕНЫЙ: (лихорадочно листая свиток) Погодите! Я видел подобные показатели в «Трактате о Запрещённых Алхимических Усилителях» Третьей эпохи! Эти ягоды — искусственный гибрид «Грозовника» и «Снежной ягоды», созданный для усиления атмосферных явлений!#З

ЖЕЛЕЗНЫЙ ДРОВОСЕК: Сопоставляю данные. Технологическая подпись соответствует заводским методикам гильдии «Громовые Шестерни». Вероятность совпадения: 96.3%. Обнаружены микрочипы для контроля магического выброса.

{
    - попытка == 0:
    ~ улика = улика + 1
    ФОЛИАНТ ПО СВЯЗИ: Идеально! Неоспоримая улика! Теперь мы знаем и состав, и производителя!#Р
}
~ попытка = 1
-> Кухня_После_Обнаружения

=== Кощей_Флора_комбо_действие ===
Кощей проводит рукой  в воздухе. Его пальцы оставляют в воздухе мерцающий тёмно-фиолетовый след.
КОЩЕЙ: Дриада, обрати внимание на те синие ягоды. Мне кажется, это не природная магия. Чувствуется металлический привкус... 
ФЛОРА: (протягивая ладонь к ягодам) Ой! И правда! Они... они неживые! В них нет солнечного света! Их вырастили в железных чанах, а не в земле!#З 
КОЩЕЙ: Точно. Это лабораторный продукт. И судя по резонансу магии тьмы... кто-то очень хотел, чтобы они вызывали именно хаос, а не урожай. 
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ:Теперь мы знаем метод изготовления ягод — это важная улика!#Р
}

~ попытка = 1
-> Кухня_После_Обнаружения

=== Кот_Василиса_комбо_действие ===
Василиса и Кот Ученый быстро договариваются о чем-то. Кот торжественно разворачивает древний свиток, а Василиса с улыбкой приближается к Грумбелю.

ВАСИЛИСА: Грумбель, дорогой, ты же знаешь, что Совет Мудрецов высоко ценит твоё искусство? #Р
ГРУМБЕЛЬ: (надуваясь от гордости) Ну да... мне даже грамоту давали! Висит там, на стене!

КОТ УЧЕНЫЙ: (важно) Согласно Статье 12 Устава «О Технике Безопасности в великанской кулинарной традиции», любая попытка приготовления запрещенных рецептов является преступлением! Наказание — лишение права на участие в "Битве шефов" на 10 лет!
ГРУМБЕЛЬ: Неужели на 10 лет?!?#П
ВАСИЛИСА: Ты не виноват, Грумбель. Скажи, кто-то подсунул тебе этот рецепт, верно? Тебя использовали, чтобы дискредитировать твоё имя!

Грумбель хмурится, его огромное лицо выражает сначала недоумение, потом обиду, а потом — растущее негодование.

ГРУМБЕЛЬ: Значит... Это... противозаконно? Меня... обманули?#П

Василиса кивает с сочувствием. Великан начинает рыться в ящике и достаёт помятую визитку.

ГРУМБЕЛЬ: Вот, он её оставил... «Лаборатория Громовых Шестерён. Инновации в магии и кулинарии. Мастер Гром».#П

{
    - попытка == 0:
    ~ улика = улика + 3
    ФОЛИАНТ ПО СВЯЗИ: Вот это удача! Визитка с именем — прямое доказательство и указание на главу гильдии!#Р
}
~ попытка = 1
-> Кухня_После_Обнаружения

=== Кощей_Иван_комбо_действие ===
Вы отправляете Ивана и Кощея работать вместе. Иван начинает методично осматривать помещение, а Кощей тут же приступает к магическому сканированию.

ИВАН ЦАРЕВИЧ: (шепчет) Я начну с осмотра пола и стен. Если здесь кто-то посторонний бывал, останутся следы.#У
КОЩЕЙ: (пренебрежительно) Следы? Зачем тебе пыль и грязь, когда можно увидеть энергетический отпечаток? Дай место.#У
Иван пытается рассмотреть что-то на полу, но магическое свечение Кощея создаёт блики и тени, искажая видимость. Кощей, в свою очередь, не может сконцентрироваться — движение Ивана создаёт помехи в энергетическом поле.

ИВАН ЦАРЕВИЧ: (раздражённо) Если бы ты не светил этой фиолетовой аурой, я бы уже нашёл улики!
КОЩЕЙ: (холодно) Если бы ты не топтался, как слон, я бы уже прочитал энергетический след доставщика.

В этот момент Грумбель оборачивается, услышав спор.

ГРУМБЕЛЬ: Чего шумите? Мешаете творческому процессу! Или вас конкуренты прислали, чтобы рецепт выведать? А ну прочь отсюда, пока я в вас половник не запустил!#У
ФОЛИАНТ ПО СВЯЗИ: Похоже, ничего выяснить не удалось...
~ попытка = 1
-> Этап1_Кухня_Обнаружение

=== Дровосек_Флора_комбо_действие ===
ФЛОРА: Дайте-ка я прикоснусь к стенам пещеры... Камень помнит всё. Он расскажет, что изменилось в последние дни.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Нелогично. Камень не обладает памятью в привычном понимании. Я проведу анализ тепловых следов и акустических отпечатков.

Дровосек прикладывает металлическую ладонь к стене. Из неё исходят звуковые волны, которые отражаются и возвращаются обратно.

ФЛОРА: (отскакивая) Ай! Ты пугаешь духов камня! Они теперь затаились и не будут говорить со мной!#З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Духи камня — метафорическое понятие. Моя методика основана на физических. #З
ФЛОРА:Бездушная железяка! Ты всех распугиваешь своими вибрациями!#З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Мои вибрации находятся в безопасном диапазоне. Ваше восприятие субъективно и ненадёжно.#З

Их спор привлекает внимание Грумбеля.

ГРУМБЕЛЬ: Что это вы тут вынюхиваете? Убирайтесь из моей кухни!#У
ФОЛИАНТ ПО СВЯЗИ: Похоже, ничего выяснить не удалось...#У
~ попытка = 1
-> Этап1_Кухня_Обнаружение

=== Василиса_Флора_комбо_обнаружение ===
ВАСИЛИСА: Давайте подойдём к этому системно. Флора, ты чувствуешь что-то необычное в самой атмосфере пещеры?#З
ФЛОРА: Да... Воздух тяжёлый, наполнен тревогой. Но это не просто погода... что-то искусственное внесло дисбаланс.#П
Флора закрывает глаза, а Василиса начинает мягко расспрашивать Грумбеля, наблюдая за его реакцией.
ВАСИЛИСА: Грумбель, когда ты заметил, что погода стала реагировать на твой суп?
ФЛОРА: (не открывая глаз) Там, где он берёт специи... есть холодное пятно. Не природный холод...
Грумбель показывает на полку с синими ягодами.
ГРУМБЕЛЬ: Да вот с тех пор, как начал эти ягодки добавлять. Человечек один дал, говорит: "Для остроты ощущений!"
ВАСИЛИСА: (осторожно) А этот человечек не говорил, откуда ягоды?
ГРУМБЕЛЬ: Я не знаю. Он сказал, суп громкий будет. #П
ФЛОРА: Они не из этого мира. Их растили без солнца, без земли... в металлическом ящике.
Василиса замечает на полке рядом с ягодами крошечный ярлык, который не видно с пола.
{
    - попытка == 0:
    ~ улика = улика + 3
   ФОЛИАНТ ПО СВЯЗИ:   Отлично! Командная работа дала результат!#Р
}
->Кухня_После_Обнаружения

===Иван_Кот_комбо_действие===
ИВАН ЦАРЕВИЧ: Я быстро осмотрю всё здесь. Мне не нужна помощь.#У
КОТ УЧЁНЫЙ: Нет, сначала нужно составить план исследования! Возможно, стоит свериться с классификациями аномалий...#У
Иван начинает быстро и методично осматривать кухню, открывая крышки, заглядывая в углы. Кот пытается остановить его, размахивая свитками.
КОТ УЧЁНЫЙ: Погодите! Не трогайте ничего, пока мы не изучим возможные риски! В "Трактате об Осмотрительности" сказано...
ИВАН ЦАРЕВИЧ: Трактаты подождут. Я уже нашёл странное: вот эти ягоды светятся в темноте.#П
КОТ УЧЁНЫЙ: Светятся? Дайте-ка посмотреть в "Каталоге Светящихся Растений"...#У
Пока Кот листает свитки, Иван берёт одну ягоду, чтобы рассмотреть её ближе. 
КОТ УЧЕНЫЙ: (кричит) Не трогай ничего руками! Что за варварские методы!
ГРУМБЕЛЬ:(замечает их) ЭЙ! КТО РАЗРЕШАЛ ТРОГАТЬ МОИ ИНГРЕДИЕНТЫ?!#У
КОТ УЧЁНЫЙ: (в панике) Мы исследовали! Согласно параграфу 7...#П
ГРУМБЕЛЬ: ВОН ОТСЮДА ОБА! НИКАКИХ ИССЛЕДОВАНИЙ БЕЗ СПРОСУ!#У
Возможность спокойного исследования утеряна. Грумбель теперь бдит за каждым движением.
ФОЛИАНТ ПО СВЯЗИ:  Да уж, босс! Один слишком быстро действует, другой слишком медленно думает. Вместо слаженной работы получился хаос.#П
~ попытка = 1
-> Этап1_Кухня_Обнаружение


=== Кухня_После_Обнаружения ===
Гроза над пещерой бушует с новой силой. Грумбель, довольный своим творением, продолжает помешивать котёл, напевая что-то себе под нос.
ФОЛИАНТ ПО СВЯЗИ:  Так, ясно! «Громовые Шестерни» подсунули великану грозовые ягоды, чтобы устроить хаос! Нужно уговорить Грумбеля выкинуть этот «подарок». Он доверчивый, но его легко разозлить. И он не терпит лжи.
-> Этап2_Кухня_Решение

=== Этап2_Кухня_Решение ===

ТЫ: Как мы убедим великана, что его обманули?
+ { дровосек == true} [Спросить Железного Дровосека] -> Дровосек_кухня_предложение2
+ { дриада == true} [Спросить Флору] -> Флора_кухня_предложение2
+ { кощей == true} [Спросить Кощея] -> Кощей_кухня_предложение2
+ { иван == true} [Спросить Ивана Царевича] -> Иван_кухня_предложение2
+ { кот == true} [Спросить Кота Ученого] -> Кот_кухня_предложение2
+ { василиса == true} [Спросить Василису] -> Василиса_кухня_предложение2

+{ кощей == true && иван == true } [Отправить Кощея и Ивана работать вместе] -> Кощей_Иван_комбо_решение
+{ дровосек == true && кот == true } [Отправить Дровосека и Кота работать вместе] -> Дровосек_Кот_комбо_решение
+{ дровосек == true && дриада == true } [Отправить Дровосека и Флору работать вместе] -> Дровосек_Флора_комбо_решение
+{ кощей == true && дриада == true } [Отправить  Кощея и Флору работать вместе] -> Кощей_Флора_комбо_решение
+{ василиса == true && дриада == true } [Отправить Василису и Флору работать вместе] -> Флора_Василиса_кухня_решение
+{ кощей == true && дровосек == true } [Отправить Кощея и Дровосека работать вместе] -> Кощей_Дровосек_комбо_решение
+{ кощей == true && василиса == true } [Отправить Кощея и Василису работать вместе] -> Кощей_Василиса_комбо_решение
+{ дровосек == true && василиса== true } [Отправить Дровосека и Василису работать вместе] ->Дровосек_Василиса_кухня_комбо_решение
+{ иван == true && василиса== true } [Отправить Ивана и Василису работать вместе] ->Иван_Василиса_кухня_комбо_решение

  === Иван_Василиса_кухня_комбо_решение ===
ВАСИЛИСА: Грумбель, посмотри сюда. Иван принёс тебе кое-что снаружи.#З
Иван показывает сломанную ветку дуба толщиной в руку, и градины, размером с кулак. На плече у него сидит перепуганная белка, дрожащая от холода.
ИВАН ЦАРЕВИЧ: Это — с окраины леса. Твой «грозовой суп» уже ломает деревья и пугает животных. Через час будет ещё хуже.#З
ВАСИЛИСА: (мягко) Ты же не хотел никому вредить, правда? Ты хотел победить честно, своим мастерством, а не разрушением.
Грумбель смотрит на сломанную ветку и дрожащую белку, и его лицо мрачнеет. Он тихо бормочет что-то, глядя на свой котёл.
ГРУМБЕЛЬ: Я... я не знал, что так получится. Я просто хотел, чтобы суп был самым громким! Чтобы все ахнули!#П
ВАСИЛИСА: Мы знаем. И поэтому мы хотим помочь тебе. Отдай нам эти ягоды, и мы сделаем так, чтобы тот человечек больше никогда никого не обманул.
Великан тяжко вздыхает и протягивает мешок. Его рука дрожит от обиды.
ГРУМБЕЛЬ: Забирайте. И накажите его хорошенько. Скажите... скажите, что Грумбель не игрушка в чужих руках.#П
~ улика = улика + 2
ФОЛИАНТ: Отличная командная работа!!#Р
-> Кухня_После_Нейтрализации

=== Дровосек_Василиса_кухня_комбо_решение ===
ВАСИЛИСА: Грумбель, буря усиливается. Нужно безопасно изъять эти ягоды из твоего котла и из атмосферы. Доверься нам.#З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Предлагаю двухэтапный протокол. Этап 1: Точечный термальный шок для деактивации катализаторов в котле. Этап 2: Создание локального гравитационного поля для сбора рассеянных частиц. Риск повреждения котла: 0.5%.
ГРУМБЕЛЬ: (кивает) Делайте. Только... котёл не трогайте. Он у меня с душой.
ВАСИЛИСА: Конечно. Мы здесь, чтобы помочь, а не навредить.
Дровосек выполняет ювелирно точную операцию. Яркая вспышка в котле, затем тихий гул. Частицы ржавой пыли и магического налёта стягиваются в маленькую сферу над ладонью Дровосека.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Процесс завершён. Атмосферные показатели нормализуются. Образцы загрязнителя сохранены. В котле остался только оригинальный бульон.
ВАСИЛИСА: (обращаясь к Грумбелю) Видишь? Твой суп в порядке. И он будет вкусным, потому что будет честным.
Гроза за окном пещеры стихает так же быстро, как и началась.

ФОЛИАНТ ПО СВЯЗИ: Прекрасный пример командной работы! Техническое решение, выполненное с хирургической точностью, и постоянное поддержание психологического контакта с клиентом. Все цели достигнуты, улики сохранены, отношения с великаном укреплены.
~ улика = улика + 2 // Максимум за решение
-> Кухня_После_Нейтрализации


=== Флора_Василиса_кухня_решение ===
ВАСИЛИСА: Грумбель, у тебя же такой талант! Ты можешь создавать вкусы из настоящих, живых продуктов! Зачем тебе эти фальшивки?
ФЛОРА: Я знаю травы, которые дают настоящую "громкость"! Не грозу на небе, а взрыв вкуса во рту! Острый перец Громотравка, ароматный чеснок Браво... Они растут на солнечных склонах, а не в лабораториях!
ГРУМБЕЛЬ: Но... "Битва шефов" через два дня! Я не успею вырастить!
ВАСИЛИСА: А мы поможем. Флора ускорит рост, а я договорюсь с жюри о небольшой отсрочке — объясню, что ты стал жертвой мошенничества.
ФЛОРА: (протягивает семена) Вот. Они ещё пахнут солнцем. Давай попробуем?
Грумбель смотрит на фальшивые синие ягоды, потом на живые семена в руке Флоры. Его лицо медленно проясняется.
ГРУМБЕЛЬ: Ладно. Выбросите эти железные ягодки. И... спасибо. Что не дали мне опозориться
Он протягивает мешок с ягодами. Василиса аккуратно берёт его для анализа.
ФОЛИАНТ ПО СВЯЗИ: Великолепно! Великан не только отказался от опасных ингредиентов, но и доверяет вам. И мы получили образцы ягод как улику!
~ улика = улика + 3 
-> Кухня_После_Нейтрализации

=== Флора_кухня_предложение2 ===
ФЛОРА: Эти ягоды чужды природе его кухни. Давайте предложим ему альтернативу - цветы Шепчущего Ветра - они гармонично впишутся в рецепт и нейтрализуют побочные эффекты! Грумбель поймёт, что мы предлагаем не запрет, а улучшение!#З
+ [Действуй!] -> Флора_кухня_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Кухня_Решение

=== Флора_кухня_нейтрализация ===
        ФЛОРА: Слушай, Грумбель, — говорит Флора, протягивая букет серебристых цветов, — эти ягодки-то фальшивые, пахнут неаппетитно! Вот попробуй цветочки, от них и суп наваристее, и добавят летние ароматы! Представляешь как другие шефы удивятся?#Р
        Великан с обидой швыряет мешок с ягодами и радостно хватает цветы.
ГРУМБЕЛЬ: Ах, жулики! Мне фальшивку подсунули!  #П
Буря стихает.
        ФОЛИАНТ ПО СВЯЗИ:  Образец ягод как вещдок - отличная работа!#Р
        ~ улика = улика + 1
        -> Кухня_После_Нейтрализации

=== Василиса_кухня_предложение2 ===
ВАСИЛИСА: Он ведёт себя как обманутый ребёнок! Нужно не приказывать, а раскрыть правду, показав, что им манипулируют. Я спокойно объясню, что кто-то просто использовал его для создания хаоса. Его гордость повара не позволит быть орудием в чужих руках!#З
+ [Действуй!] -> Василиса_кухня_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Кухня_Решение

=== Василиса_кухня_нейтрализация ===
        ВАСИЛИСА: (с искренним огорчением) Грумбель, дорогой, тот человечек над тобой просто смеялся. Он хотел, чтобы твой суп все ругали, а твою кухню - боялись!
        Лицо великана наливается кровью. 
ГРУМБЕЛЬ: ЧТООО?! Да я его... Я его! Так и знал, это конкуренты с "Битвы шефов"! Боятся меня! #У
Он хватает полный мешок ягод и швыряет его в стену. "Больше никогда! Только проверенные рецепты!"
       ФОЛИАНТ ПО СВЯЗИ:  Василиса, захвати немного ягод для анализа! Нам надо изучить их!#З
        ~ улика = улика + 1
-> Кухня_После_Нейтрализации

=== Кот_кухня_предложение2 ===
КОТ УЧЕНЫЙ: У меня есть старинный эльфийский кулинарный рецепт! Я преподнесу этот свиток в дар Грумбелю. Уверен, он тут же начнет готовить новое блюдо и забудет о грозовых ягодах.#У
+ [Действуй!] -> Кот_кухня_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Кухня_Решение

=== Кот_кухня_нейтрализация ===
        Кот с важным видом вручает великану пожелтевший свиток.
        КОТ УЧЕНЫЙ: Многоуважаемый шеф! Позвольте вручить вам рецепт Супа Спокойного Неба от самого повара Короля-Солнце!#Р
        ГРУМБЕЛЬ:(внимательно изучает свиток) О, теперь я знаю, как победить на "Битве шефов"!
        ГРУМБЕЛЬ: Эх, жаль, ягодки были неплохие, использую их для следующих рецептов.
        Он достает огромную стеклянную банку с этикеткой "Сомнительные ингредиенты" и аккуратно укладывает туда ягоды.
        ФОЛИАНТ ПО СВЯЗИ:  Босс, великан запер улики в свою личную коллекцию. Юридически мы не можем их изъять без разрешения Совета...#У
-> Кухня_После_Нейтрализации

=== Иван_кухня_предложение2 ===
ИВАН ЦАРЕВИЧ: Хватит уговоров! Я вытащу его из пещеры и ткну носом в последствия - пусть сам увидит снесённые крыши и перепуганных соседей! Иногда нужно показать правду напрямую!#У
+ [Действуй!] -> Иван_кухня_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Кухня_Решение

=== Иван_кухня_нейтрализация ===
 ИВАН ЦАРЕВИЧ: Эй, приятель! Твой суп устраивает ураган и не дает спать соседям! Прекрати кидать эти ягоды!#З
        Великан хмурится, но, выглянув из пещеры и увидев последствия, смущенно бормочет: "Ладно, ты прав, пожалуй..."
        Грумбель берет мешок с ягодами и швыряет их в огонь. "Плохие, плохие ягодки, устроили беспорядок!.."
        ФОЛИАНТ ПО СВЯЗИ: Буря прекращена, но ягоды сожжены. Никаких улик не осталось.#П
        -> Кухня_После_Нейтрализации

=== Дровосек_кухня_предложение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Предлагаю продемонстрировать логические расчёты и диаграммы атмосферного дисбаланса. Когда он увидит, что его блюдо нарушает законы физики на 347%, он будет вынужден согласиться с необходимостью немедленного прекращения процесса.#У
+ [Действуй!] -> Дровосек_кухня_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Кухня_Решение

=== Дровосек_кухня_нейтрализация ===

        Железный Дровосек безупречно демонстрирует голографические схемы и диаграммы, выводя в воздухе сложные формулы атмосферной турбулентности.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Вот видите, коэффициент температурной аномалии превышает допустимые нормы.
        Грумбель смотрит на мерцающие цифры с нарастающим недоумением. 
        ГРУМБЕЛЬ: Значит, эти ягодки - плохие? Жаль, а они такие красивые...Но я не хочу творить катастрофы! #П
        Великан швыряет мешок с ягодами в огонь. Буря постепенно стихает.
ФОЛИАНТ ПО СВЯЗИ: Проблема решена, но ягоды сгорели. Улики потеряны.#У
-> Кухня_После_Нейтрализации

=== Кощей_кухня_предложение2 ===
КОЩЕЙ: Создам иллюзию, что суп превращается в ядовитую жижу. Пусть он увидит, как его 'шедевр' разлагается на глазах... Мгновенный шок - лучший учитель.#У
+ [Действуй!] -> Кощей_кухня_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Кухня_Решение

=== Кощей_кухня_нейтрализация ===
        // Только Кощей - катастрофа
        Кощей создает перед великаном иллюзию, что суп в котле превращается в зловещую зеленую жижу.
        КОЩЕЙ: Отравой балуешься, глупец?#У
        Великан в ужасе отскакивает от котла, а потом приходит в ярость. 
ГРУМБЕЛЬ: Это был мой лучший суп! Для "Битвы шефов"! Ты его сглазил!#З
Он пытается поймать Кощея и случайно опрокидывает котел.
#Location: Kuhnia_katastrofa
        Котел с оглушительным бульканьем взрывается, разбрызгивая ядовито-фиолетовую похлёбку по всей пещере. Грумбель в ярости начинает крушить всё вокруг, но внезапно над пещерой сходится настоящий ураган. Молнии бьют в землю, сметая деревья, а град размером с голову человека засыпает окрестности. Великан забивается в дальний угол своей разрушенной кухни, бормоча проклятия, а вы еле успеваете телепортироваться в офис.
        ~ катастрофа = true
        -> Кухня_Итоги
        
      === Дровосек_Кот_комбо_решение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Профессор, мне нужна точная температура распада магической связи в этих ягодах.
КОТ УЧЕНЫЙ: (листая свиток) Так-так... Вот! «Грозовник теряет активность при ровно 47 градусах по Цельсию в вакуумной среде»! И есть антидот — пыльца лунного мха!#P
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Создаю микровакуумную камеру с точным терморегулятором.
Дровосек направляет луч на мешок с ягодами. Те тихо светятся, затем гаснут. Из них выходит лёгкий пар.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Процесс завершён. Магическая составляющая нейтрализована. Ягоды теперь безопасны и пригодны для анализа как вещественное доказательство.#Р
КОТ УЧЕНЫЙ: И я записал весь процесс! Это будет отличным материалом для обвинения! #Р
~ улика = улика + 1
ФОЛИАНТ ПО СВЯЗИ:   Вот это научный подход! И улики сохранены, и великан не пострадал!#Р
->Кухня_После_Нейтрализации 
  
 === Кощей_Флора_комбо_решение ===
КОЩЕЙ: Эти ягоды — как сорняк. Их нужно не вырывать, а лишить силы. Постепенно, чтобы не вызвать взрыв.
ФЛОРА: Давай я выращу «цветы-поглотители» из семян лунного лотоса. Они впитают яд, как губка.
Кощей кивает и проводит сложный жест руками, создавая над ягодами тёмную сферу, замедляющую время.
КОЩЕЙ: У тебя будет десять минут, дриада. Я стабилизирую магический фон.
Флора закрывает глаза, и из её рук вырастают серебристые цветы. Они обвивают ягоды, поглощая их магическое свечение. Цветы темнеют, а ягоды становятся бледно-голубыми.
ФЛОРА: Готово! Магия ягод теперь в цветах... они просто уснут до весны. А ягоды остались целыми.#Р
КОЩЕЙ: Они лишены магии и совершенно безвредны!#Р
~ улика = улика + 2
ФОЛИАНТ ПО СВЯЗИ: Не забудьте захватить пору ягодок для анализа!#Р
-> Кухня_После_Нейтрализации      
        
 === Кощей_Дровосек_комбо_решение ===
КОЩЕЙ: Лучший способ сохранить улику — в буквальном смысле остановить для нее время. Заморозить в момент наибольшей активности.#З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Согласен. Предлагаю криомагическую капсулу с последующим спектральным анализом без размораживания.#З
Кощей произносит короткое заклинание, и ягоды покрываются инеем, застывая в воздухе. Вокруг них образуется мерцающая сфера замедленного времени.
КОЩЕЙ: Температура: абсолютный ноль в магическом эквиваленте. Теперь твоя очередь, железный человек.
Дровосек аккуратно помещает замороженные ягоды в прозрачный контейнер, который сразу же заполняется голубым светом. На внутренних стенках появляются голографические схемы.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Образец сохранён. Начинаю дистанционный анализ... Обнаружены уникальные магические метки-подписи. Они идентичны тем, что находили на устройствах «Громовых Шестерён». 
КОЩЕЙ: Теперь я знаю, как убрать негативный эффект. Ягоды станут безопасными!
~ улика = улика + 2
ФОЛИАНТ ПО СВЯЗИ:  Холодный расчёт и ледяная магия! Я восхищен!#Р
-> Кухня_После_Нейтрализации

===Кощей_Иван_комбо_решение===
КОЩЕЙ: (холодно) Зачем что-то выпытывать? Просто заберём ягоды и проанализируем. Быстро и эффективно.#У
ИВАН ЦАРЕВИЧ: Согласен. Иногда прямой путь — самый короткий. Я возьму образец. А ты отвлеки внимание великана.#У
КОЩЕЙ: Я тебе что, клоун? Не мешай, я перенесу ягоды с помощью телекинеза!#У
ИВАН ЦАРЕВИЧ: Не верю я твоей магии. Смотри, как надо.#У
Иван пытается незаметно взять несколько ягод со стола, но Кощей в этот момент создаёт заклинание телекинеза. Миска с я годами приподнимается в воздух и... врезавшись в лоб Царевича с грохотом падает на пол.
ГРУМБЕЛЬ: (оборачивается) ЭЙ! ТЫ ЧТО, ЯГОДЫ ВОРУЕШЬ?!#З
Великан хватает гигантскую метлу и начинает размахивать ею, как мечом. Иван и Кощей едва успевают увернуться от сметающих ударов.
ИВАН ЦАРЕВИЧ: (отскакивая) Всё испортил... Великий чародей и волшебник!
КОЩЕЙ: (сквозь зубы, уклоняясь от очередного взмаха) В следующий раз работай тише, следопыт!
Пытаясь поймать непрошенных гостей, великан опрокидывает котел. 
#Location: Kuhnia_katastrofa
        Суп с оглушительным бульканьем взрывается, разбрызгивая ядовито-фиолетовую похлёбку по всей пещере. Грумбель в ярости начинает крушить всё вокруг, но внезапно над пещерой сходится настоящий ураган. Молнии бьют в землю, сметая деревья, а град размером с голову человека засыпает окрестности. Великан забивается в дальний угол своей разрушенной кухни, бормоча проклятия, а вы еле успеваете телепортироваться в офис.
        ~ катастрофа = true
        -> Кухня_Итоги

=== Дровосек_Флора_комбо_решение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Предлагаю термальную утилизацию. Высокая температура нейтрализует магические связи. 800 градусов по Цельсию будет достаточно.
ФЛОРА: Нет-нет! Нельзя жечь ягоды! Они живые, их нужно успокоить! Дайте мне поговорить с ними! #З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Не вижу логической составляющей.
Дровосек направляет тепловой луч на мешок. Флора в панике пытается вырастить защитные лианы, чтобы закрыть ягоды.
ФЛОРА: Я не дам тебе их спалить! Они страдают!#З
ГРУМБЕЛЬ: (оборачиваясь) ЧТО ВЫ ДЕЛАЕТЕ В МОЕЙ КУХНЕ?!#З
Пытаясь поймать непрошенных гостей, великан опрокидывает котел. 
#Location: Kuhnia_katastrofa
       Котел с оглушительным бульканьем взрывается, разбрызгивая ядовито-фиолетовую похлёбку по всей пещере. Грумбель в ярости начинает крушить всё вокруг, но внезапно над пещерой сходится настоящий ураган. Молнии бьют в землю, сметая деревья, а град размером с голову человека засыпает окрестности. Великан забивается в дальний угол своей разрушенной кухни, бормоча проклятия, а вы еле успеваете телепортироваться в офис.
        ~ катастрофа = true
        -> Кухня_Итоги

=== Кощей_Василиса_комбо_решение ===
ВАСИЛИСА: Давай попробуем просто убедить великана?#З
КОЩЕЙ: Без наглядности ничего не выйдет. Надо показать ему последствия его действий#З
Василиса мягко подводит Грумбеля к центру пещеры, где Кощей создаёт магический экран
ВАСИЛИСА: Грумбель, посмотри, что происходит снаружи.#П
КОЩЕЙ: (произнося заклинание) Speculum veritatis...
На экране появляются образы: разрушенные дома, перепуганные животные, люди, прячущиеся от града.
ГРУМБЕЛЬ:  Это... из-за меня?#П
ВАСИЛИСА: Из-за того, кто тебя обманул. Ты хотел создать шедевр, а тебе подсунули оружие. #П
КОЩЕЙ: И оружие это имеет конкретного владельца. Смотри.#З
Кощей добавляет в образы едва заметные символы — молот и шестерню, мелькающие в каплях дождя, в узорах облаков.
ГРУМБЕЛЬ: (сжимая кулаки) Они... использовали мою кухню. Моё искусство.#У
ВАСИЛИСА: Но ты можешь это остановить. Прямо сейчас.
Грумбель молча подходит к мешку с ягодами и протягивает его Василисе. 
~ улика = улика + 2
ФОЛИАНТ ПО СВЯЗИ:   Идеальный баланс! Я восхищен!#Р
-> Кухня_После_Нейтрализации


=== Кухня_После_Нейтрализации ===
#Location: Kuhnia
Буря прекращается. Грумбель, хоть и недовольный, возвращается к приготовлению нормального супа. Над пещерой снова светит солнце.
-> Кухня_Итоги

=== Кухня_Итоги ===
#Location: Ofis
~ всего_улик = всего_улик + улика
ФОЛИАНТ: Передаю итоговый отчет.

{ катастрофа:
ФОЛИАНТ: Босс... это полный провал. Грумбель в ярости разрушил свою кухню, а его ядовитый суп спровоцировал климатическую катастрофу местного масштаба. Окрестные деревни затоплены, мосты снесены. "Громовые Шестерни" превратили великана в орудие массового разрушения.#П
}

{ улика >=3:
ФОЛИАНТ: Великолепно! Мы не только остановили бурю, но и получили прямые вещественные доказательства и свидетельские показания против «Громовых Шестерен». Они не просто создают хаос - они манипулируют, подставляя других.#Р
}

{ улика == 2  && катастрофа==false:
ФОЛИАНТ: Буря прекращена. Мы установили связь с гильдией, но доказательств недостаточно для серьезного обвинения.
}

{ улика <=1  && катастрофа==false:
ФОЛИАНТ: Погода нормализовалась, но великан так и не понял, что им манипулировали. «Громовые Шестерни» остаются в тени.
}

КОГНИТИВНЫЙ КРИСТАЛЛ: Найдено улик в этом деле: {улика}. Всего улик: {всего_улик}
~катастрофа=false
ФОЛИАНТ: Четвертое дело закрыто. Теперь мы знаем, что наши противники действуют не только напрямую, но и через подставных лиц. Это новая, опасная тактика.

ФОЛИАНТ: А у нас новый сигнал - на главном рынке столицы что-то опять случилось!

+ [Перебрасывай нас на рынок!] -> РЫНОК_ШЕПОТОВ
+ [Что случилось на рынке?] -> Инфо_Рынок_Шепотов

=== Инфо_Рынок_Шепотов ===
КОГНИТИВНЫЙ КРИСТАЛЛ: На главной рыночной площади столицы происходит нечто странное. Люди теряют голоса — не могут говорить громче шепота. Никаких видимых причин, только легкий звон и запах карамели в воздухе.

-> РЫНОК_ШЕПОТОВ

=== РЫНОК_ШЕПОТОВ ===
#Location: Market_anomalia

Вы на главной рыночной площади столицы. Еще вчера шумный и пестрый базар теперь напоминает призрачный город. Воздух, обычно дрожащий от криков торговцев, споров покупателей и детского смеха, теперь неподвижен и тяжел.

Люди озираются, бросают тревожные взгляды по сторонам, словно ищут кого-то. Некоторые потирают горло, будто пытаясь нащупать пропажу. Никаких щелчков механизмов или вспышек магии — лишь лёгкий, едва уловимый звон, будто бы звенят крошечные невидимые колокольчики. В воздухе явственно ощущается знакомый, но неуместный сейчас запах леденцовой карамели.

ФОЛИАНТ ПО СВЯЗИ: (голос из коммуникатора звучит приглушенно, будто и он боится нарушить тишину) Команда, вы на месте. Это... жутковато. Люди не оглохли. Они не онемели. Они просто разучились говорить громко. Прошу вас, будьте осторожны. То, что крадет голоса, может украсть и ваши.

~ улика = 0
~ попытка = 0

// Начало первого этапа
-> Этап1_Рынок_Обнаружение
=== Этап1_Рынок_Обнаружение ===
ТЫ: С чего начнем? Нужно понять логику этого безмолвия.

+ { дровосек == true} [Спросить Железного Дровосека] -> Дровосек_рынок_предложение
+ { дриада == true} [Спросить Флору] -> Флора_рынок_предложение
+ { кощей == true} [Спросить Кощея] -> Кощей_рынок_предложение
+ { иван == true} [Спросить Ивана Царевича] -> Иван_рынок_предложение
+ { кот == true} [Спросить Кота Ученого] -> Кот_рынок_предложение
+ { василиса == true} [Спросить Василису] -> Василиса_рынок_предложение

+{ дровосек == true && кот == true } [Отправить Дровосека и Кота работать вместе] -> Дровосек_Кот_комбо_обнаружение2
+{ кощей == true && дриада == true } [Отправить Кощея и Флору работать вместе] -> Кощей_Флора_комбо_обнаружение2
+{ василиса == true && иван == true } [Отправить Василису и Ивана работать вместе] -> Иван_Василиса_комбо_обнаружение2
+{ кощей == true && иван == true } [Отправить Кощея и Ивана работать вместе] -> Кощей_Иван_комбо_обнаружение2
+{ иван == true && дриада == true } [Отправить Ивана и Флору работать вместе] -> Флора_Иван_комбо_обнаружение2
+{ кощей == true && дровосек == true } [Отправить Кощея и Дровосека работать вместе] -> Кощей_Дровосек_комбо_обнаружение2
+{ кощей == true && кот == true } [Отправить Кощея и Кота работать вместе] -> Кот_Кощей_обнаружение2
+{ кот == true && василиса == true } [Отправить Кота и Василису работать вместе] -> Кот_Василиса_обнаружение2
+{ дровосек == true && дриада == true } [Отправить Дровосека и Флору работать вместе] -> Дровосек_Флора_обнаружение2
+{ кот == true && иван == true } [Отправить Кота и Ивана работать вместе] -> Иван_Кот_рынок_обнаружение2

=== Иван_Кот_рынок_обнаружение2 ===
ИВАН ЦАРЕВИЧ: (прищурившись) Торговец в синем плаще кажется мне подозрительным. У него глаза бегают. И правую руку в кармане держит. Там что-то есть. Я подойду под видом покупателя, надавлю на него, посмотрю реакцию.#З
КОТ УЧЁНЫЙ: (заметно нервничая) Это неэтично и преждевременно! Сначала нужно проанализировать данные! Я нашёл упоминание о «Камне шепчущих обетов» — для его активации нужны особые условия! Надо проверить фазу луны..#У
ИВАН ЦАРЕВИЧ: Фазу луны он в кармане не прячет. Я действую.
Иван принимая вид простого прохожего, и неспешно направляется к лотку. Его движения естественны, взгляд рассеян. Он почти у цели.
КОТ УЧЁНЫЙ: Стойте! А вдруг это не «Камень шепчущих обетов», а «Амулет молчаливого согласия»? Для нейтрализации первого нужен святой источник, а для второго — зола драконьего зуба! Совершенно разные ритуалы!
Резкий крик заставляет всех вздрогнуть. Торговец в синем плаще оглядывается на шум, и его взгляд встречается с внимательным, оценивающим взглядом Ивана. В глазах торговца — мгновенная паника. Он резко хватает свою сумку и, не раздумывая, ныряет в толпу, используя суматоху.
ИВАН ЦАРЕВИЧ: (сдавленно, Ко́ту) Поздравляю. Ты его спугнул. Я даже не рассмотрел, что у него в руке.#П
ФОЛИАНТ ПО СВЯЗИ: Подозреваемый скрылся, улик нет. Надо начинать сначала.#П
~ попытка = 1
~ улика = 0
-> Этап1_Рынок_Обнаружение

=== Дровосек_Флора_обнаружение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Для локализации источника аномального акустического поля требуется равномерная сеть датчиков. Я установлю 12 миниатюрных сейсмодатчиков по периметру площади. Они зафиксируют малейшие вибрации, и мы построим точную карту распространения звукоподавляющих волн.#З
ФЛОРА: Как практично!... А я с помощью растений закреплю их! #Р
ЖЕЛЕЗНЫЙ ДРОВОСЕК: В этом нет необходимости.#У
ФЛОРА: Но я же хочу помочь!#Р
Дровосек расставляет датчики. Флора, следуя за ним, шепчет слова роста растений. Из-под датчиков мгновенно пробивается густая трава и мелкие цветы, которые, разрастаясь, приподнимают и перекашивают точные приборы.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: (раздражённо) Прекратите! Биомасса изменяет уровень и угол наклона датчиков на 3-17 миллиметров! Показания становятся бесполезными! Коэффициент погрешности зашкаливает!
На экране Дровосека вместо чёткой карты — хаотичные помехи. 
~ попытка = 1
-> Этап1_Рынок_Обнаружение


=== Кощей_Иван_комбо_обнаружение2 ===

ИВАН ЦАРЕВИЧ: Слушай, Кощей. Ты же маг. Сделай какое-нибудь яркое представление на площади. Фейерверк, иллюзию, что угодно. Пока все смотрят на тебя, я незаметно прощупаю, у кого в карманах или под прилавком есть неизвестный артефакт.#З

КОЩЕЙ: Развлекать толпу? Я вам что, циркач?.. Ладно, попробую...#У

Кощей встаёт в центре площади и щёлкает пальцами. Из воздуха рождается огненная птица, которая с криком взмывает в небо и рассыпается на сотни искр. Люди, и без того напуганные, взвизгивают от ужаса. 
ИВАН ЦАРЕВИЧ:(пытается пробиться сквозь мечущуюся толпу) Чёрт! Все носятся как угорелые! Я не могу ни к кому подойти незаметно!#П
КОЩЕЙ: Я же вам не клоун...#У
~ попытка = 1
-> Этап1_Рынок_Обнаружение

=== Флора_Иван_комбо_обнаружение2 ===

ИВАН ЦАРЕВИЧ: У меня есть идея. Видишь тех городских голубей? Они везде летают и на них не обращают внимания. Прикрепим миниатюрный видеокристалл к птице, и она облетит всю площадь. Мы увидим всё сверху и вычислим источник.#З
ФЛОРА: (восторженно) О, какая прекрасная мысль! Но голуби... они такие серые и грустные. Давай сделаем это красиво! Я создам не просто пернатого свидетеля, а настоящее чудо!#Р
Прежде чем Иван успевает возразить, Флора подзывает маленького воробья, сидящего на карнизе. Она дует на него, осыпая блестящей пыльцой. Воробей начинает светиться, его перья переливаются, и через мгновение в воздух взмывает огромная, сияющая неземным светом Жар-Птица.
ИВАН ЦАРЕВИЧ: Флора... Мы же должны были наблюдать скрытно! #П
ФЛОРА: Но... она же такая красивая! Я хотела немного порадовать людей, им так страшно... И воробей теперь счастлив...#П
Видеокристалл, прикреплённый к бывшему воробью, передаёт великолепные кадры полёта над ошарашенной толпой, но ни одного полезного.
ФОЛИАНТ ПО СВЯЗИ: Босс, у нас есть запись того, как полсотни человек пялятся на Жар-Птицу. Красиво, но бесполезно. #П
~ попытка = 1
-> Этап1_Рынок_Обнаружение

=== Кот_Василиса_обнаружение2 ===
 // Комбинация Василиса + Кот
        ВАСИЛИСА: Я расспрошу людей, а ты, Котик, ищи в архивах подтверждение их слов.
        КОТ УЧЕНЫЙ: Хорошо. Записывай детали.
        Василиса беседует с людьми, Кот сверяется с текстами.
        ВАСИЛИСА: Все говорят о "сделке" — торговец предлагал удачу за тишину. И каждый раз что-то шептал.
        КОТ УЧЕНЫЙ: Да, это классическая схема Камня Тихой Сделки! В летописях сказано, что артефакт нужно активировать, шепча определенные слова!#З
        ВАСИЛИСА: И этот торговец... он явно не сам придумал эту схему. Кто-то его научил.#З
        КОТ УЧЕНЫЙ: Судя по модификациям артефакта — это работа «Громовых Шестерен». Они усовершенствовали древнюю технологию.
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ: Отличная совместная работа#Р
}
~ попытка = 1
-> Рынок_После_Обнаружения

=== Кот_Кощей_обнаружение2 ===
        // Комбинация Кот + Кощей
        КОТ УЧЕНЫЙ: Я подниму архивы, а Кощей пусть определит источник магии.
        КОЩЕЙ: Договорились. Сравним данные.
        Кот зарывается в свитки, а Кощей анализирует магическое поле.
        КОТ УЧЕНЫЙ: Нашел! Камень Тихой Сделки — артефакт, который заключает негласный договор: жертва соглашается на тишину в обмен на мимолетную удачу!#З
        КОЩЕЙ: Согласен, это энергетический вампиризм. Но не живой. Артефакт. Судя по всему, это вон тот торговец сладостями.#Р
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ: Отличная совместная работа#Р
}
~ попытка = 1
-> Рынок_После_Обнаружения

==Иван_Василиса_комбо_обнаружение2 ===
        ИВАН ЦАРЕВИЧ: Я прослежу за перемещениями людей, а ты, Василиса, поговори с жертвами, узнай детали.#З
        ВАСИЛИСА: Хорошо, я расспрошу тех, кто еще может шептать.#З
        Иван наблюдает за движением толпы, а Василиса беседует с пострадавшими.
        ИВАН ЦАРЕВИЧ: Все следы ведут к тому торговцу в синем плаще. Смотрите — у него в руках что-то тяжелое, вроде камень.
        ВАСИЛИСА: Люди говорят, что он предлагал им "удачу" в обмен на тишину. И каждый раз что-то шептал, когда брал плату.
        ИВАН ЦАРЕВИЧ: Видишь, как он нервничает? Судорожно сжимает камень. Это точно он.#З
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ: Отлично, мы нашли злоумышленника.#Р
}
~ попытка = 1
-> Рынок_После_Обнаружения

=== Кощей_Дровосек_комбо_обнаружение2 ===
КОЩЕЙ: Я буду искать магический след, а ты, Железный, фиксировать параметры.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Принято. Могу создать  детектор для записи магических частот.
КОЩЕЙ: Действуй, Железный#Р
Кощей проводит руками, и в воздухе появляются едва видимые светящиеся полупрозрачные нити. Они поднимаются от каждого безмолвного человека и тянутся к одному из торговцев сладостями. Дровосек наводит своё устройство на этот энергетический узел.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Наблюдаю структурированную систему. Каждый голос - отдельный канал с уникальным ID.#З
КОЩЕЙ: Все ясно. Энергетический вампир... но не живой. Чувствуется след работы артефакта.#Р
 ЖЕЛЕЗНЫЙ ДРОВОСЕК: Подтверждаю. Работает мощный артефакт. Содержит микрочипы и усилители. Источник - торговец сладостями.#Р
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ: Отлично, мы получили новые данные!#Р
}
~ попытка = 1
-> Рынок_После_Обнаружения

=== Кощей_Флора_комбо_обнаружение2 ===
КОЩЕЙ: Дриада, попробуй почувствовать искажения в природной магии. А я просканирую техномагический след.#З
ФЛОРА: Хорошо... Дай мне сосредоточиться...
Флора закрывает глаза, Кощей проводит руками в воздухе.
        ФЛОРА: Ой! Я чувствую холодное пятно. Там, где стоит вон тот торговец. Вокруг него пустота — даже насекомые облетают стороной!#П
        КОЩЕЙ: Энергетический вампиризм. Но не живой. Артефакт. И этот артефакт явно техномагического происхождения. 
ФЛОРА: Точно, торговец что-то сжимает в руках. Похожее на камень.#З
{
    - попытка == 0:
    ~ улика = улика + 1
    ФОЛИАНТ ПО СВЯЗИ: Отличная совместная работа, команда!#Р
}
~ попытка = 1
-> Рынок_После_Обнаружения

=== Дровосек_Кот_комбо_обнаружение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Начинаю сканирование акустических аномалий. Профессор, сопоставьте с известными вам случаями.
КОТ УЧЕНЫЙ: Уже ищу! В "Анналах акустической магии" есть описание подобного..."Безмолвный резонанс", вызываемый артефактом, который поглощает звуковые волны определенной частоты!#У
Дровосек сканирует, Кот листает свитки.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Обнаружено: устройство генерирует поле на частоте 17000 Гц, блокирующее голосовые связки. 
КОТ УЧЕНЫЙ: Артефакт - предмет в руках вон того торговца сладостями.
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ: Мы знаем и принцип работы, и модификацию!#Р
}
~ попытка = 1
-> Рынок_После_Обнаружения



=== Дровосек_рынок_предложение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Попытаюсь провести акустический анализ среды. Возможно, здесь действует подавляющий резонанс, который физически глушит голосовые связки.#У
+ [Действуй!] -> Дровосек_рынок_действие
+ [Послушаем других] -> Этап1_Рынок_Обнаружение

=== Дровосек_рынок_действие ===
        Железный Дровосек направляет сенсоры на толпу.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Акустический анализ не выявляет аномалий. Голосовые связки людей физически здоровы. Мои методы здесь неприменимы.#П
        ~ попытка = 1
        -> Этап1_Рынок_Обнаружение

=== Флора_рынок_предложение ===
ФЛОРА: Я попробую поговорить с местными растениями, что они знают... Может, пыль на листьях или дрожь корней подскажет причину неестественной тишины.#У
+ [Действуй!] -> Флора_рынок_действие
+ [Послушаем других] -> Этап1_Рынок_Обнаружение

=== Флора_рынок_действие ===
        Флора прикладывает ладони к стволу старого дерева на краю рынка, закрывая глаза.
        ФЛОРА: Они... они не понимают. Для деревьев тишина - это мир. Они чувствуют страх людей, но не понимают его.#П
        ~ попытка = 1
        -> Этап1_Рынок_Обнаружение

=== Кощей_рынок_предложение ===
КОЩЕЙ: Я могу провести диагностику на предмет вампирических чар. Если что-то высасывает силу, я это почую.#У
+ [Действуй!] -> Кощей_рынок_действие
+ [Послушаем других] -> Этап1_Рынок_Обнаружение

=== Кощей_рынок_действие ===
        Кощей проводит рукой по воздуху, и его пальцы на мгновение оставляют в пространстве бледный светящийся след.
        КОЩЕЙ: Похоже это действительно энергетический вампир... но не живой. Чувствуется след работы артефакта. Следы ведут вон к тому торговцу сладостями. Но у нас нет прямых доказательств.
~ попытка = 1
-> Рынок_После_Обнаружения

=== Иван_рынок_предложение ===
ИВАН ЦАРЕВИЧ: Уверен, это дело рук вора, а вор оставляет следы. Я прослежу, к кому подходили все эти люди до того, как лишились голоса. Злоумышленник должен быть среди них.#З
+ [Действуй!] -> Иван_рынок_действие
+ [Послушаем других] -> Этап1_Рынок_Обнаружение

=== Иван_рынок_действие ===
        Иван, как тень, скользит между притихшими людьми, его глаза выхватывают детали. Через несколько минут он возвращается.
        ИВАН ЦАРЕВИЧ: Вот он. Торговец сладостями в синем плаще. Все, с кем я говорил, покупали у него леденцы. И каждый раз он что-то шептал им на ухо, когда брал плату. Смотрите - та женщина только что купила сладости, а теперь она схватилась за горло.#З
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Установленный торговец-посредник - явная улика! Я заношу в базу.
        }
~ попытка = 1
-> Рынок_После_Обнаружения

=== Кот_рынок_предложение ===
КОТ УЧЕНЫЙ: Кража голоса - магия не новая! Я поищу в архивах аналогичные случаи. Если мы поймем, как это делалось в прошлом, мы найдем и нынешний инструмент!#З
+ [Действуй!] -> Кот_рынок_действие
+ [Послушаем других] -> Этап1_Рынок_Обнаружение

=== Кот_рынок_действие ===
        Кот зарывается носом в парящий свиток, бормоча себе под нос.
        КОТ УЧЕНЫЙ: Так-так-так, Камень Тихой Сделки... описан в Манускриптах Магического Шантажа эпохи Третьей Империи! Артефакт заключает негласный договор - жертва соглашается на тишину в обмен на мимолетную удачу или исполнение мелкого желания! Все звуки должны быть заключены в артефакт. С виду он выглядит как обычный камень. Только холодный.
        ФОЛИАНТ ПО СВЯЗИ: Тогда обратите внимание вон того торговца! У него в руках, что-то тяжелое.
        {
            - попытка == 0:
                ~ улика = улика + 1
        }
~ попытка = 1
-> Рынок_После_Обнаружения

=== Василиса_рынок_предложение ===
ВАСИЛИСА: Страх - плохой советчик, но хороший свидетель. Я поговорю с людьми, попробую найти общее в их историях. Может, все они ели одно и то же, или трогали один и тот же предмет...#У
+ [Действуй!] -> Василиса_рынок_действие
+ [Послушаем других] -> Этап1_Рынок_Обнаружение

=== Василиса_рынок_действие ===
        Василиса подходит к группе перешептывающихся людей. После нескольких минут тихого разговора она возвращается.
        ВАСИЛИСА: Некий торговец предлагал людям: "Хочешь, чтобы спор решился в твою пользу? Просто съешь пирожное". "Хочешь удачной покупки? Возьми конфетку и не благодари". Возможно, это вон тот торговец сладостями. Он держится подозрительно и явно нервничает.
~ попытка = 1
-> Рынок_После_Обнаружения

=== Рынок_После_Обнаружения ===
ФОЛИАНТ ПО СВЯЗИ: Значит, люди, сами того не понимая, заключали какую-то сделку. Мошенник - скорее всего вон тот торговец с камнем в руке. Ваша задача - вернуть людям их голоса. И постарайтесь сохранить артефакт - он нам нужен как доказательство.
-> Этап2_Рынок_Решение

=== Этап2_Рынок_Решение ===
ТЫ: Нужно аннулировать сделку. Как поступим?

+ { дровосек == true} [Спросить Железного Дровосека] -> Дровосек_рынок_предложение2
+ { дриада == true} [Спросить Флору] -> Флора_рынок_предложение2
+ { кощей == true} [Спросить Кощея] -> Кощей_рынок_предложение2
+ { иван == true} [Спросить Ивана Царевича] -> Иван_рынок_предложение2
+ { кот == true} [Спросить Кота Ученого] -> Кот_рынок_предложение2
+ { василиса == true} [Спросить Василису] -> Василиса_рынок_предложение2

+{ кощей == true && дриада == true } [Отправить Кощея и Флору работать вместе] -> Кощей_Флора_комбо_решение2
+{ кот == true && дриада == true } [Отправить Кота и Флору работать вместе] -> Кот_Флора_рынок_решение2
+{ кощей == true && дровосек == true } [Отправить Кощея и Дровосека работать вместе] -> Кощей_Дровосек_комбо_решение2
+{ кощей == true && кот == true } [Отправить Кощея и Кота работать вместе] -> Кощей_Кот_комбо_решение2
+{ василиса == true && дровосек == true } [Отправить Василису и Дровосека работать вместе] -> Василиса_Дровосек_рынок_решение2
+{ кощей == true && иван == true } [Отправить Кощея и Ивана работать вместе] -> Кощей_Иван_комбо_решение2
+{ дровосек == true && дриада == true } [Отправить Дровосека и Флору работать вместе] -> Дровосек_Флора_комбо_решение2
+{ кощей == true && василиса == true } [Отправить Кощея и Василису работать вместе] -> Василиса_Кощей_рынок_решение2
+{ кот == true && василиса == true } [Отправить Кота и Василису работать вместе] -> Василиса_Кот_рынок_решение2

=== Василиса_Кот_рынок_решение2 ===
ВАСИЛИСА: Я верю, что торговец не злодей, его обманули. Надо постараться убедить его отдать артефакт.#З
ВАСИЛИСА: (мягко, торговцу) Гильдия Громовых Шестерен тебя использовала, как и многих до тебя. Хочешь знать, чем это для них закончилось?#У
Кот Учёный тут же разворачивает потрёпанный свиток, поправляя очки.
КОТ УЧЁНЫЙ: Согласно «Хроникам городских махинаций», в 1247 году гильдия «Молчаливые Тени» применяла идентичные артефакты. Посредников после выполнения задачи объявляли виновными во всём. Все они закончили дни в темнице.#У
ТОРГОВЕЦ: Но… что мне делать?
КОТ УЧЁНЫЙ: Механизм прост! Любая сделка, заключенная через этот артефакт, держится на добровольном согласии. 
ВАСИЛИСА: Достаточно добровольно от неё отказаться и вернуть камень – и все связи разорвутся. Произнеси: «Я отрекаюсь от этой сделки и возвращаю то, что взял».
Торговец, дрожащими руками, протягивает камень Василисе и повторяет слова.
Камень на его ладони меркнет, и от него во все стороны расходятся тихие, звенящие волны. На площади один за другим люди начинают кашлять, прочищать горло, а затем – говорить.
КОТ УЧЁНЫЙ: Работает! Сделки аннулированы! Артефакт деактивирован!#Р
ВАСИЛИСА : (торговцу) Теперь помоги нам описать тех, кто тебя нанял. Это твой шанс всё исправить.
ФОЛИАНТ ПО СВЯЗИ: Команда сработала безупречно! #Р
~ улика = улика + 2
-> Рынок_После_Нейтрализации

=== Василиса_Кощей_рынок_решение2 ===
ВАСИЛИСА: Я поговорю с торговцем, постараюсь убедить его отдать камень.
КОЩЕЙ: Сентиментально и ненадёжно. Я могу принудительно разорвать связи, но это будет болезненно. Возможно, смертельно для слабых.
ВАСИЛИСА: Дай мне сначала поговорить с ним. 
Кощей молча кивает, наблюдая.
ВАСИЛИСА: (обращаясь к торговцу) Посмотри, что происходит с людьми. Они напуганы, они страдают. Уверена, ты не хотел этого! Что обещали тебе Громовые Шестерни?
ТОРГОВЕЦ: (дрожа) Они обещали мне дать денег... Я не хотел...
КОЩЕЙ: Не хотел? Ты просто жалкий и жадный мошенник! Тебе нет пощады!
Он проводит рукой, и тёмная аура окутывает камень в руках торговца.
Камень трескается, и из него вырывается тихий вихрь шёпотов. Люди падают на колени, крича от боли. Но голоса возвращаются — медленно, но верно. Торговец роняет осколки и бросается бежать.
ВАСИЛИСА: Ты смог сохранить артефакт для анализа?
КОЩЕЙ: Нет. Энергетическая структура разрушена. Но я зафиксировал след заклинания — оно имеет техномагическую природу. Стиль «Громовых Шестерён».
ФОЛИАНТ ПО СВЯЗИ: Задача выполнена. Голоса возвращены, источник угрозы устранён, но материальная улика утрачена. 
~ улика = улика + 1
-> Рынок_После_Нейтрализации


=== Кот_Флора_рынок_решение2 ===
КОТ УЧЕНЫЙ: Я нашёл! В «Манускриптах Расторжения» описан именно такой случай. Нужно заставить камень отдать голоса — для этого требуется натуральный катализатор, живой и чистый!#З
ФЛОРА: Я могу вырастить «цветы правды» — они расцветают только от честных слов. Если люди скажут, что отказываются от сделки, цветы вберут ложную магию камня и нейтрализуют её!#З
КОТ УЧЕНЫЙ: Да! Я знаю заклинание, которое усилит связь между цветами и камнем — оно из «Травника эльфийских мудрецов»!
Флора рассыпает семена. Кот читает древний стих. Семена мгновенно прорастают в серебристые цветы с полупрозрачными лепестками.
ФЛОРА: (обращаясь к людям) Скажите просто: «Я не согласен». Шёпотом!
Люди начинают шептать. Каждый лепесток цветка начинает светиться, и из камня вырываются тонкие звуковые волны. Камень постепенно тускнеет.
КОТ УЧЕНЫЙ: Работает! Магическая схема артефакта перезаписывается цветочной пыльцой — теперь она хранит не голоса, а память о нарушенной сделке!#З
Камень раскалывается с тихим звоном. Из него выпадает маленькая металлическая шестерёнка с клеймом «Громовые Шестерни».
ФОЛИАНТ ПО СВЯЗИ: Комбинация магии природы и исторических знаний сработала! Голоса возвращены, улика найдена, но механизм работы артефакта остался неисследованным.
~ улика = улика + 2
-> Рынок_После_Нейтрализации

=== Василиса_Дровосек_рынок_решение2 ===

ВАСИЛИСА: Я уверена, этот торговец не понимает, что делает. Я хочу поговорить с ним.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Это риск. Любое резкое движение — и может активироваться цепная реакция. Нужна стабилизация.
ВАСИЛИСА: Ты займись этим. (подходит к торговцу) Посмотри, что происходит... Люди напуганы, они теряют голоса... Гильдия обманула тебя.
ТОРГОВЕЦ: (дрожит) Человек в плаще обещал мне золото… я не знал, что так будет…
ВАСИЛИСА: Ты можешь всё исправить. Просто отпусти камень — медленно, в мои руки. Мы его обезвредим, и никто не пострадает.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Готовлю нейтрализующее поле.
Торговец, кивая, разжимает пальцы. Василиса аккуратно принимает камень и помещает его в силовую сферу, созданную Дровосеком.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Контейнер герметичен. Энергетические связи начали рваться… плавно. Голоса возвращаются.
По площади прокатывается волна облегчённых возгласов, смешков, криков. Камень в сфере темнеет.
ВАСИЛИСА: Всё. Артефакт обезврежен, люди в безопасности. Но торговец ничего не знает о заказчиках — только про «человека в плаще».
ФОЛИАНТ ПО СВЯЗИ: Задача выполнена. Голоса возвращены, улика сохранена. 
~ улика = улика + 1
-> Рынок_После_Нейтрализации

=== Кощей_Флора_комбо_решение2 ===

КОЩЕЙ: Я извлеку энергию, которую артефакт украл. Но это опасная субстанция.#У
ФЛОРА: А обездвижу злодея, чтобы он не успел активировать артефакт.#З
Кощей поднимает руки, и воздух наполняется магией. Флора шепчет заклинание и из земли вытягиваются лианы, опутывающие торговца по рукам и ногам.
Энергия возвращается к людям как тихое эхо - сначала шёпот, потом нормальная речь.

КОЩЕЙ: Контракты аннулированы. Энергия возвращена. И у нас есть полный набор отпечатков для отслеживания.#Р
ФЛОРА: И никто не пострадал!#Р
ФОЛИАНТ ПО СВЯЗИ: И злодея задержали! Блестящая работа! #Р
~ улика = улика + 2
-> Рынок_После_Нейтрализации

=== Дровосек_Флора_комбо_решение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Я создам сервисного дроида-курьера. Он подойдёт к торговцу с запросом на «доставку партии карамели». В процессе обсуждения условий дроид незаметно просканирует артефакт и запишет все данные, после чего уйдёт под предлогом уточнения адреса. Риск минимален.#З
ФЛОРА: (с искренним участием) Бедняжка! Он будет таким механическим и неживым. Как ему доверят? Я могу вдохнуть в его схему немного жизни! Чтобы он вызывал доверие!#У
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Это не предусмотрено конструкцией. Его убедительность — в чётких алгоритмах и правдоподобной легенде.
Но Флора уже подошла к металлическому дроиду и положила на его холодный корпус ладонь. По магическому контуру побежали зелёные искры. Голос дроида  стал тёплым и задушевным, а  «глазах»-индикаторах появилось добродушное выражение.
ДРОИД: (идя к торговцу, почти с пафосом) О, уважаемый торговец! Ваши дивные сладости творят чудеса! Моя бабушка очень нуждается в удаче! Не могли бы вы...
Торговец с радостью вступает в диалог. Дроид сканирует артефакт. Но артефакт, чувствуя перед собой не просто механизм, а нечто, наделённое магией, срабатывает.
#Location: Market_KATASTРОФА
Камень вспыхивает ослепляющим светом и гаснет. Тишина становится абсолютной, тягучей и физически давящей. Люди замирают на месте, их глаза стекленеют. Рынок превращается в жуткую живую картину: окаменевшие фигуры, неподвижные, с открытыми ртами, в которых больше не родится ни звука.
ФОЛИАНТ ПО СВЯЗИ(голос еле слышен сквозь наступившую немоту): К-катастрофа... Аварийная активация... Тишина стала необратимой... #П
~ катастрофа = true
-> Рынок_Итоги

=== Кощей_Иван_комбо_решение2===
ИВАН ЦАРЕВИЧ: Кощей, ты выглядишь солидно. Подойди к торговцу как важный, но капризный клиент. Начни торговаться, придирайся к карамелькам, отвлеки его. А я в это время вытащу эту штуковину у него из-под плаща.#З
КОЩЕЙ (брезгливо): Общаться с уличным торговцем?.. Я просто потребую показать товар.#У
Кощей надменно подходит к лотку.
КОЩЕЙ: Эй, торговец. Это что за дешёвая крашеная патока? В моих кладовых такие сладости выбрасывают. Дай-ка посмотреть на твой «секретный ингредиент», если он, конечно, существует.#У
ТОРГОВЕЦ: (начинает нервничать) Простите, господин, это просто карамель...

КОЩЕЙ: (раздражённо, повышая голос) Не просто! Я чувствую здесь магию! Грубую, топорную, дешёвую магию! Жалкий шарлатан!#У
Торговец в панике хватает камень и со всей силы швыряет его на землю, чтобы уничтожить улики.
#Location: Market_KATASTРОФА
Камень вспыхивает ослепляющим светом и гаснет. Тишина становится абсолютной, тягучей и физически давящей. Люди замирают на месте, их глаза стекленеют. Рынок превращается в жуткую живую картину: окаменевшие фигуры, неподвижные, с открытыми ртами, в которых больше не родится ни звука.
ФОЛИАНТ ПО СВЯЗИ (голос еле слышен сквозь наступившую немоту): К-катастрофа... Аварийная активация... Тишина стала необратимой... #П
~ катастрофа = true
-> Рынок_Итоги

===Кощей_Кот_комбо_решение2===
 // Комбинация Кощей + Кот
        КОЩЕЙ: Я разорву связи, а ты, Кот, найди способ сохранить информацию о контрактах. Для доказательств.#З
        КОТ УЧЕНЫЙ: Уже ищу заклинание архивации... Вот! Глава шестая, "Сохранение следов магических сделок"!#З
        Кощей проводит ритуал, а Кот архивирует информацию. Камень трескается, но из него вылетают призрачные светящиеся свитки — копии всех заключенных сделок.
        КОЩЕЙ: Готово. Доказательства сохранены.#Р
        КОТ УЧЕНЫЙ: И артефакт достаточно цел для анализа технологий врага.#Р
        ~ улика = улика + 2
         ФОЛИАНТ ПО СВЯЗИ: Прекрасное решение!#Р
         -> Рынок_После_Нейтрализации

===Кощей_Дровосек_комбо_решение2===
// Комбинация Кощей + Дровосек
        КОЩЕЙ: Я начну разрывать магические связи, а ты, Дровосек, стабилизируй артефакт, чтобы он не взорвался.#З
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Создаю силовое поле для сдерживания энергии.#З
        Кощей щелкает пальцами. Торговец вскрикивает и роняет камень. Черные молнии рвут невидимые нити, но силовое поле Дровосека сдерживает разрушительную энергию.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Артефакт стабилизирован. Магическая схема повреждена, но сохранилась для изучения.
        КОЩЕЙ: Контракты аннулированы. Голоса возвращаются. И мы получили образец технологии «Громовых Шестерен».
        ~ улика = улика + 2
        ФОЛИАНТ ПО СВЯЗИ: Прекрасное решение!#Р
        -> Рынок_После_Нейтрализации

===Дровосек_Кот_комбо_решение2===
// Комбинация Дровосек + Кот
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Создаю усилитель. Профессор, подскажите безопасную частоту, чтобы не повредить артефакт.
        КОТ УЧЕНЫЙ: В "Трактате о звуковых артефактах" сказано: нужно использовать частоту 432 Гц — она гармонична и не разрушает магические структуры!#У
        Дровосек настраивает устройство на указанную частоту. Устройство издает низкий гул. Усилитель работает. Голоса постепенно возвращается, но торговец успевает скрыться в толпе. 
        КОТ УЧЕНЫЙ: И я успел записать магическую схему артефакта до его деактивации!#Р
        ~ улика = улика + 2
               -> Рынок_После_Нейтрализации

=== Дровосек_рынок_предложение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Попробую создать резонансный усилитель голосовых связок. Он физически усилит колебания на 500%, позволяя преодолеть магическое шумоподавление.#У
+ [Действуй!] -> Дровосек_рынок_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Рынок_Решение

=== Дровосек_рынок_нейтрализация ===
        Устройство Дровосека издаёт низкий гул. Внезапно камень в руках торговца начинает пульсировать красным светом.
        // КАТАСТРОФА
        ФОЛИАНТ ПО СВЯЗИ: Вон артефакт! Мы нашли его!! Но он перешел в режим максимальной защиты!
#Location: Market_KATASTРОФА
        Камень вспыхивает ослепляющим светом и гаснет. Тишина становится абсолютной, тягучей и физически давящей. Люди замирают на месте, их глаза стекленеют. Рынок превращается в жуткую живую картину: окаменевшие фигуры, неподвижные, с открытыми ртами, в которых больше не родится ни звука.
        Торговец с камнем в панике убегает.
        ~ катастрофа = true
        -> Рынок_Итоги

=== Флора_рынок_предложение2 ===
ФЛОРА: Эта тишина пахнет страхом! Я наполню воздух пыльцой солнечных лютиков - они несут в себе энергию чистого радостного возгласа! Когда люди вдохнут её, их лёгкие сами захотят издать счастливый крик, и это разорвёт искусственные оковы! Природа всегда найдёт способ выразить себя!#Р
+ [Действуй!] -> Флора_рынок_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Рынок_Решение

=== Флора_рынок_нейтрализация ===
        Пыльца Флоры вызывает у людей лишь тихое, беззвучное чихание, от которого у них слезятся глаза. Чары остаются непоколебимыми.
        ФЛОРА: Почему не работает? Это же чистая радость...#П
        ФОЛИАНТ ПО СВЯЗИ: Босс, эффект нулевой. Теперь люди не только не могут говорить, но и постоянно чихают. Их страдания только усилились.#У
        -> Рынок_Итоги


=== Кощей_рынок_предложение2 ===
КОЩЕЙ: Я проведу ритуал принудительного расторжения магических контрактов. Это болезненно для артефакта и, возможно, для его носителя, но это единственный способ разорвать все связи разом.#З
+ [Действуй!] -> Кощей_рынок_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Рынок_Решение

=== Кощей_рынок_нейтрализация ===
        Кощей с презрением щелкает пальцами. Торговец, держащий камень, вдруг вскрикивает и роняет его.
        Камень зависает в воздухе, и от него во все стороны бьют черные молнии, будто рвутся невидимые нити.
        КОЩЕЙ: Контракты аннулированы. Артефакт более не функционирует.#Р
        По рынку прокатывается волна - сначала тихого удивления, потом сдавленного смеха, а затем и полнозвучного крика: "Я МОГУ ГОВОРИТЬ!"
        ФОЛИАНТ ПО СВЯЗИ: Нейтрализованное ядро артефакта - бесценная улика!
        ~ улика = улика + 1

-> Рынок_После_Нейтрализации

=== Иван_рынок_предложение2 ===
ИВАН ЦАРЕВИЧ: Я просто уничтожу камень. Нет артефакта - некому поддерживать чары. Голоса должны вернуться сами.#У
+ [Действуй!] -> Иван_рынок_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Рынок_Решение

=== Иван_рынок_нейтрализация ===
        Иван резким движением выхватывает камень из рук торговца и с силой швыряет его оземь. Тот разбивается вдребезги.
        Эффект немоты спадает не мгновенно, а в течение нескольких минут, вызывая замешательство, но в итоге голоса возвращаются. Торговец, пользуясь суматохой, сбегает.
        ФОЛИАНТ ПО СВЯЗИ: Проблема решена, но улика уничтожена, а торговец скрылся.
-> Рынок_После_Нейтрализации

=== Кот_рынок_предложение2 ===
КОТ УЧЕНЫЙ: Найду контрзаклинание из "Манускриптов Расторжения". Оно заставит камни извергнуть поглощённые голоса. Безопасно, но потребует времени...#У
+ [Действуй!] -> Кот_рынок_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Рынок_Решение

=== Кот_рынок_нейтрализация ===
        Кот, бормоча заклинание, заставляет осколки камня светиться и испускать тихий, мелодичный звон. Голоса возвращаются, но путаются - некоторые люди начинают говорить чужими голосами, что вызывает временный хаос. Только через полчаса все приходит в норму. Пользуясь суетой, торговец успевает скрыться.
        КОТ УЧЕНЫЙ: М-да, побочный эффект. В древних текстах об этом не упоминалось... Кажется, голоса немного перемешались.#П
        ФОЛИАНТ ПО СВЯЗИ:  Артефакт разрушен, улик нет, и полчаса на рынке царил хаос, когда все говорили голосами соседей. Но хоть голоса вернулись...
        -> Рынок_После_Нейтрализации

=== Василиса_рынок_предложение2 ===
ВАСИЛИСА: Я спою старую колыбельную - ту, что знает каждый с детства. Если я смогу заставить их вспомнить мелодию и подпеть мне, их собственные голоса пробьются сквозь чары.#З
+ [Действуй!] -> Василиса_рынок_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Рынок_Решение

=== Василиса_рынок_нейтрализация ===
        Василиса встает у колодца и начинает петь старую колыбельную. Её голос плывёт над притихшим рынком. Это простая, знакомая каждому с детства колыбельная. Сначала в ответ - тишина. Но вот старушка у лавки шевелит губами. Ребёнок подхватывает припев. К ней присоединяется ещё один голос, ещё один... Шёпот набирает силу, превращаясь в хор, а затем - в мощное пение, сотрясающее воздух. Камень в руках торговца трескается и рассыпается в пыль.
        ФОЛИАНТ ПО СВЯЗИ: Невероятно! Артефакт не выдержал напора! Мы получили запись этого момента - это бесценная магическая улика!#Р
        ~ улика = улика + 1
-> Рынок_После_Нейтрализации

=== Рынок_После_Нейтрализации ===
#Location: Market
На рынке медленно, но верно воцаряется нормальный, здоровый шум. Кто-то плачет от радости, обнимая соседа, кто-то ругает торговца, который уже скрылся, кто-то просто смеется, с наслаждением слушая собственный голос.
-> Рынок_Итоги

=== Рынок_Итоги ===
#Location: Ofis
~ всего_улик = всего_улик + улика
ФОЛИАНТ: Передаю итоговый отчет.

{ катастрофа:
ФОЛИАНТ: Босс... тишина победила. Рынка больше не существует - теперь это музей под открытым небом, где застыли живые статуи. "Громовые Шестерни" провели полевые испытания нового оружия... и мы позволили им это сделать. Хуже того - тишина продолжает расползаться по городу.
}

{ улика >=3  && катастрофа==false:
ФОЛИАНТ: Блестяще! Мы не только вернули городу голос, но и получили бесценные данные. Мы понимаем принцип действия их нового оружия и имеем материальные доказательства. Мы можем подготовиться. Это наша первая по-настоящему стратегическая победа.
}

{ улика == 2  && катастрофа==false:
ФОЛИАНТ: Голоса возвращены, паника прекращена. Мы установили связь с тактикой врага, но у нас недостаточно улик, чтобы предъявить их Совету. Шестерни остаются в тени.
}

{ улика <=1  && катастрофа==false:
ФОЛИАНТ: Немота отступила, но Громовые Шестерни остались безнаказанными. Они получили все данные своих полевых испытаний. В следующий раз их оружие будет совершеннее.
}

Найдено улик в этом деле: {улика}
Всего улик: {всего_улик}
~катастрофа=false
ФОЛИАНТ: Босс, пятое дело закрыто. Пришло время сложить все части пазла. Все улики, все свидетельства. Противостояние с Громовыми Шестернями неизбежно, и теперь мы идем на нее, зная врага в лицо.

+ [Вернуться на базу для подведения итогов] -> ФИНАЛ_СЦЕНА


===ФИНАЛ_СЦЕНА===











->все








===все===


-> END


VAR кощей = false
VAR кот  = false
VAR иван = false
VAR василиса = false
VAR дровосек = false
VAR дриада = false
VAR кол_арсенал = 0
VAR команда = ""
//VAR техническая_улика = 0
//VAR магическая_улика = 0
VAR всего_улик = 0
VAR катастрофа = false
VAR улика = 0
VAR попытка = 0
VAR пройдено = 0

-> merlin

=== merlin ===
#Location: Kabinet_Merlina
Кабинет Верховного Мага Мерлина в Башне Вечных Сумерек. Комната выглядит так, будто по ней прошелся магический торнадо. Горы свитков нависают над странными артефактами, а в камине пляшет синий огонь. На одной из полок толстая книга в кожаном переплете тихонько скребется и вздыхает, требуя, чтобы ее, наконец-то, почитали... или хотя бы сдули пыль с обложки.
Великий волшебник нервно ходит по кабинету. Он явно чем-то взволнован!
МЕРЛИН: Это невообразимо! Наш мир сошел с ума! Все эти странные случаи... Ты ведь в курсе последних новостей?#З
ТЫ:
+ [Да, конечно! На курсе все говорят об этом!] ->dalee2
+ [Э-э... нет... Я был занят экзаменами...] ->case
===case===
МЕРЛИН: Магия становится ненадёжной. Артефакты тускнеют, заклинания выдыхаются. Даже древние существа слабеют — драконьи крылья тяжелеют, а лешие теряются в собственных лесах. Что-то высасывает силу из самого мира. 
 ->dalee2
===dalee2===
МЕРЛИН: Боюсь, что за этими странностями кроется общая причина! 
МЕРЛИН: Я создаю новый отдел. Назовем его... "Агентство Сказочных Аномалий"! 
МЕРЛИН: Я за тобой давно наблюдаю. На курсе ты был возможно не самым сильным, но, черт побери, самым сообразительным! Поэтому возглавить новое агентство должен именно ты!
ТЫ:
        + [Но есть же более опытные маги!]
            МЕРЛИН: Да, конечно! Старик Хоттабыч мог бы решить эти проблемы одним волоском со своей бороды! Но нам нужна причина, а она, я уверен, техномагическая. 
            МЕРЛИН: Ты ведь знаешь, какой вечный спор ведут маги и инженеры: одни презирают волшебство как "детские игрушки", другие страшатся обычной розетки пуще разбушевавшегося дракона.
 		МЕРЛИН: Одни говорят: "Магия должна быть только у избранных, иначе хаос!" Другие кричат: "Долой магическую аристократию! Да здравствует техника для всех!" Но мало кто пытается совместить одно и другое...
            МЕРЛИН: Я помню, как на первом курсе ты разгадал историю с заколдованным котлом. Все искали сложное проклятие, а ты нашел... засор в сливе и забытый носок.
             ТЫ: Э-э-э... ну да... было дело... пахло еще так...
            -> accept_mission

        + [Агентство? Это звучит... официально. А каков мой бюджет?]
                МЕРЛИН: (закатывает глаза) Ах, молодежь... Сначала задумываются о бюджете, а уж потом – о приключении и славе!
            МЕРЛИН: Ладно. У тебя будет кабинет в Старом Городе. Очень... уютный. То есть, очень маленький. И... мой старый летающий ковер. Он иногда чихает и сбрасывает седоков, но в резвости ему не откажешь!
             ТЫ: Что ж, тогда я берусь.
        -> accept_mission

        + [(С энтузиазмом) Вы правы, все эти случаи связаны! Я берусь!]
            МЕРЛИН: Так и знал, что не ошибся в тебе! В твоих глазах я вижу здоровое любопытство к хаосу!#Р
             -> accept_mission
             

=== accept_mission ===
МЕРЛИН: Но одному тебе не справиться. Эти аномалии – как гидра: отрубишь одну голову, вырастут две. Тебе нужна команда. 
Мерлин протягивает тебе сверкающий свиток.

МЕРЛИН: Я подготовил список. Шесть незаурядных личностей. Испытанные агенты и настоящие специалисты. Тебе нужно выбрать троих.
ТЫ:
+ [Почему только троих? Чем больше, тем лучше!]
    МЕРЛИН: (ворчливо) Может, тебе еще и мою бороду в придачу? На каждого такого «уникума» требуется куча магической энергии, кофе и терпения. Наш бюджет не резиновый...
    МЕРЛИН: Меньше вопросов – они уже ждут в приемной.
    -> приемная

+ [Расскажите о них подробнее. На что мне смотреть?]
    МЕРЛИН: Смотри не на силу, а на сочетаемость, как в хорошем зелье. Бросишь в котел несовместимые ингредиенты – и получишь взрыв вместо эликсира.
    МЕРЛИН: Тебе нужна команда, способная заткнуть все дыры в этой тонущей лодке. Инженер, маг, следопыт... комбинации могут быть разными.
    МЕРЛИН:Меньше вопросов – они уже ждут в приемной.
    -> приемная

=== приемная ===
#Location: Ofis
Ты выходишь из кабинета Мерлина в небольшой кабинет твоего нового Агенства, держа под мышкой шесть светящихся папок.
->ff

===ff===
#Location: Ofis
//{кол_арсенал}
 {кол_арсенал > 2 :->ffff |->q}
 =q
ТЫ:
+{дровосек == false  } [ЖЕЛЕЗНЫЙ ДРОВОСЕК]
 ->жд
+{дриада == false  } [ФЛОРА ЗЕЛЕНОЛИСТ, дриада]
 ->д
+{иван == false  }[ИВАН ЦАРЕВИЧ]
->и
+{кот == false  }[КОТ УЧЕНЫЙ]
->ку
+{кощей == false  }[КОЩЕЙ БЕССМЕРТНЫЙ]
->кб
+{василиса == false  }[ВАСИЛИСА ПРЕМУДРАЯ] 
->вп

===жд===
 Железный Дровосек входит по точно выверенной кратчайшей траектории. Его металлические суставы издают тихие щелчки.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Система готова к интервью. Мои навыки включают проектирование, починку и создание механизмов любой сложности. Коэффициент полезного действия – 94.7%. ->sob
=sob
+[Посмотреть досье] -> d_k
+[Задать вопросы] 
#Location: Ofis
-> a

=d_k
#Location: Delo_ZH_D
Ты открываешь папку с досье
+[Задать вопросы] 
#Location: Ofis
-> a
=a
//#Location:Ofis
       +[Как вы относитесь к магии?]
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: (металлически щелкает) Магия – это недокументированный и часто нестабильный API. На нее нельзя положиться. Технология – вот чистый, откомментированный код. Она приносит порядок. #У
 ->a
       +[Что для вас самое важное в работе?]
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Эффективность. Точность. Предсказуемость результата. Беспорядок и хаос – это ошибка 404 в протоколе бытия.
 ->a
       +[Как вы справляетесь с неожиданными проблемами?]
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Моя логика не допускает понятия «неожиданность». Алгоритм прост: анализ → план → действие. #З
    ->a
        +[Вы можете работать в команде?]
    ЖЕЛЕЗНЫЙ ДРОВОСЕК: (щелчок) Безусловно. Я воспринимаю команду как распределённую вычислительную сеть. Каждый участник — отдельный модуль с уникальным интерфейсом. Маг — модуль обработки аномальных данных, следопыт — датчик внешней среды. Главное — чтобы их ПО было совместимо с моим и не содержало вирусов эмоциональной нестабильности.
->a
        +[закончить собеседование]
#Location:Ofis
  ->lk
  
  =lk
   *[принять в команду]
   ~дровосек = true
   ~кол_арсенал = кол_арсенал+1
   ~команда=команда+"Железного Дровосека, "
   ->ff
   +[попросить подождать]
   ->ff

===д===
Флора не входит, а вплывает в комнату, принося аромат свежескошенной травы и цветущей липы. 
ФЛОРА: Здравствуйте, родной! Я Флора. Я слышу, как поют растения и чувствую, когда животным больно. Я постараюсь успокоить этот прекрасный, но встревоженный мир.#Р
->соб
=соб
+[Посмотреть досье] -> d_f
+[Задать вопросы] 
#Location: Ofis
-> щ

=d_f
#Location: Delo_Flora
Ты открываешь папку с досье
+[Задать вопросы] 
#Location: Ofis
-> щ
=щ
       +[Что вы думаете о технологиях?]
       ФЛОРА: (слегка грустно, гладя цветок на своем платье) О, эти железные штуки... Они как тяжёлые сапоги на нежной траве... Но если они помогают и не слишком дымят – почему бы и нет? #У
 ->щ
       +[Как вы улаживаете конфликты?]
ФЛОРА: О, нужно дать гневу увянуть, как осеннему листу, и взрастить понимание, как первый весенний побег. У каждого в душе есть тихий родничок доброты. 
->щ
       +[Ваше отношение к магии?]
        ФЛОРА: (зажмуривается от восторга) Магия – это дыхание мира! Она повсюду – в каждом шелесте листочка, в каждой капле росы!#Р
->щ
        +[Вы можете найти общий язык с теми, кто не чувствует природу?]
        ФЛОРА: (мягко улыбается) Я стараюсь показать им красоту живого. Я вижу жизнь даже в мерном ритме шестерёнок. Создателям машин я могу показать, как лоза оплетает опору — не ломая, а поддерживая. А с магами... о, мы говорим на одном языке, только их магия — из книг, а моя — от земли.
->щ
        +[закончить собеседование]
  ->ц
  
  =ц
   *[принять в команду]
   ~дриада = true
    ~кол_арсенал = кол_арсенал+1
    ~команда=команда+"Флору Зеленолист, "
   ->ff
   +[попросить подождать]
   ->ff
   
   
   ===и===
   Царевич входит уверенной походкой человека, который привык, чтобы кусты перед ним расступались. Смотрит прямо, будто оценивая, насколько прочный у тебя череп.
ИВАН ЦАРЕВИЧ: Иван. Если нужно найти, выследить или защитить – я ваш человек. Предпочитаю действие разговорам. 
->ц
=ц
+[Посмотреть досье] -> d_i
+[Задать вопросы] 
#Location: Ofis
-> о
=d_i
#Location: Delo_Ivan
Ты открываешь папку с досье
+[Задать вопросы] 
#Location: Ofis
->о
=о
       +[Как вы относитесь к магии?]
       ИВАН ЦАРЕВИЧ: (пожимает плечами) Скользкая штука — не пощупаешь, в ножны не спрячешь. Больше верю клинку: он не подведёт. Но если чары бьют по нашим врагам — пусть искрят на здоровье. В бою и волшебный щит пригодится.#У
 ->о
       +[ Что делаете, когда сталкиваетесь с неизвестным?]
       ИВАН ЦАРЕВИЧ: Как на охоте — не суйся в чащу сломя голову. Сначала выследи. Учуял угрозу — будь готов к прыжку зверя. Мудрость в том, чтобы отличить, когда нужно пустить стрелу, а когда — затаить дыхание в траве.
 ->о
       +[Ваша сильная сторона?]
        ИВАН ЦАРЕВИЧ: Я вижу то, что другие не замечают. След, взгляд, ложь в голосе... Мелочи решают всё. Ну и меткость, куда без нее. Я могу стрелой сбить муху с носа у Кощея.#З
->о
        +[Нужна ли вам поддержка в расследованиях?]
        ИВАН ЦАРЕВИЧ: (уверенно) Один в поле не воин. Пусть волшебник читает замыслы, а учёный ковыряется в механизмах. Я найду дорогу, а они скажут, куда она ведёт...
        ->о
        +[закончить собеседование]
  ->з
    =з
   *[принять в команду]
   ~иван = true
    ~кол_арсенал = кол_арсенал+1
    ~команда=команда+"Ивана Царевича, "
   ->ff
   +[попросить подождать]
   ->ff
   
      ===ку===
   Кот Ученый входит, спотыкаясь о собственную тень. Поправляет очки, съехавшие на кончик носа, и пытается пригладить взъерошенную шерсть.
КОТ УЧЕНЫЙ: Вот... да... собственно... Кот Ученый, к вашим услугам... м-да... Специализация – история, мифология, древние языки и забытые диалекты. Если нужно что-то вспомнить – обращайтесь...->к
=к
    +[Посмотреть досье] -> d_ku
+[Задать вопросы] 
#Location: Ofis
-> ъ

=d_ku
#Location: Delo_kot
Ты открываешь папку с досье
+[Задать вопросы] 
#Location: Ofis
->ъ
=ъ
       +[Ваши знания могут помочь в современных расследованиях?]
       КОТ УЧЕНЫЙ: О! Каждая современная проблема – это просто новая обложка на старой книге. Всегда можно найти похожий случай в древних текстах.#З
 ->ъ
       +[ Отношение к современным технологиям?]
       КОТ УЧЕНЫЙ: (морщится, как от сквозняка) Шумные эти ваши механизмы... Вечно гудят и мигают. Но... признаю, интересно понаблюдать, как они работают. Древние цивилизации тоже пытались их использовать... Вот, например, в «Трактате о Выдумках Забытых Цивилизаций» сказано... Впрочем, я, кажется, снова отвлекся...#У
 ->ъ
       +[Что для вас самое сложное в работе?]
        КОТ УЧЕНЫЙ: (задумчиво) Объяснить, что истина не терпит спешки. Мир стал слишком быстрым, он пролистывает страницы, не вчитываясь. А я пытаюсь научить его видеть не только заголовки, но и сноски, в которых, зачастую, и скрыта разгадка!
->ъ
        +[Как можно применить ваши знания на практике?]
        КОТ УЧЕНЫЙ: (поправляет очки) Одного знания, конечно, мало — нужен кто-то, кто сможет использовать его в деле. Скажем, инженер: я найду описание древнего механизма, а он поймёт, как его обезвредить. Или маг: я вспомню заклинание, а он его усилит. Знание — это карта, но идти по ней должны другие! #З
->ъ
        +[закончить собеседование]
  ->ю
  
  =ю
   *[принять в команду]
   ~кот = true
    ~кол_арсенал = кол_арсенал+1
    ~команда=команда+"Кота Ученого, "
   ->ff
   +[попросить подождать]
   ->ff
   
   ===кб===
   Кощей появляется бесшумно, материализовавшись из самой тени. Смотрит с таким выражением лица, будто только что прочел инструкцию к мирозданию и нашел в ней досадные опечатки.
КОЩЕЙ: Кощей Бессмертный. Магия, проклятия, все виды защитных заклинаний. Если вам нужны разговоры по душам, зовите Василису. А если результат – договоримся. Мое время бесценно, но за определенную плату я готов его вам подарить. ->я
=я
    +[Посмотреть досье] -> d_ka
+[Задать вопросы] 
#Location: Ofis
-> э

=d_ka
#Location: Delo_Koshey
Ты открываешь папку с досье
+[Задать вопросы] 
#Location: Ofis
->э
=э
       +[Почему вы хотите работать в команде?]
      КОЩЕЙ: (холодно усмехается) Надоело над златом чахнуть. Решил понаблюдать за... коллективной динамикой примитивных социальных групп. Считайте это антропологическим экспериментом. 
 ->э
       +[Ваше отношение к технологиям?]
       КОЩЕЙ: Грубые и шумные попытки имитировать магию. Колотушка вместо телекинеза. Но иногда... удивительно эффективно. Не признаю, но использую.#У
 ->э
       +[Что вы цените в коллегах?]
       КОЩЕЙ: Компетентность. Молчаливость. И чтобы не задавали глупых вопросов и не пытались со мной «подружиться». Взаимное уважение на расстоянии – идеальный формат.
->э

        +[Как вы относитесь к другим специалистам?]
        КОЩЕЙ: (холодно усмехается) Не терплю некомпетентность. Но ценю специалистов, которые знают своё дело. Технолог, который не боится магии, или ученый, который не путает даты — вот с кем можно работать. #З
->э
        +[закончить собеседование]
          ->а
  
  =а
   *[принять в команду]
   ~кощей = true
    ~кол_арсенал = кол_арсенал+1
    ~команда=команда+"Кощея Бессмерного, "
   ->ff
   +[попросить подождать]
   ->ff
   
   
      
   ===вп===
   Василиса Премудрая входит с неспешной грацией лебедя. Ее появление словно приглушает звуки в комнате и делает воздух чище. На ее лице — теплое, ободряющее выражение..
ВАСИЛИСА: Очень приятно, Василиса. Я умею находить общий язык почти со всеми. Даже с теми, у кого этого языка и нет. #Р
->в
=в
   +[Посмотреть досье] -> d_vas
+[Задать вопросы] 
#Location: Ofis
-> э

=d_vas
#Location: Delo_Vas
Ты открываешь папку с досье
+[Задать вопросы] 
#Location: Ofis
->э
=э
       +[Почему вы ушли из дворца?]
    ВАСИЛИСА: Дворцовые интриги и бесконечные балы мне так надоели! Я хочу приносить реальную пользу, а не просто украшать собой трон.
 ->э
       +[Ваша сильная сторона?]
     ВАСИЛИСА: Я вижу ситуацию с разных сторон. Технология, магия, природа, людские суеверия... Всё это — нити одного полотна. Вы согласны со мной? А еще я умею заваривать такой чай, ради которого даже Кощей согласился бы на светскую беседу.#З
 ->э
       +[Как вы относитесь к конфликтам в команде?]
    ВАСИЛИСА: Конфликт – это чаще всего непонимание. А любое непонимание можно преодолеть, если выслушать друг друга. #У
->э
     +[Вы умеете работать в команде?]
    ВАСИЛИСА: (спокойно) Главное — понять, что каждый смотрит на проблему со своей стороны. Техник видит механизм, маг — энергию, следопыт — следы. Когда эти взгляды складываются — получается полная картина. 
->э
        +[закончить собеседование]
  ->а

    =а
   *[принять в команду]
   ~василиса = true
    ~кол_арсенал = кол_арсенал+1
    ~команда=команда+"Василису Премудрую, "
   ->ff
   +[попросить подождать]
   ->ff
   
     ===ffff===
Поздравляем! Твоя команда набрана. Ты принял {команда}
Ты только что проводил последнего кандидата. В воздухе еще витает запах машинного масла от Дровосека, аромат цветов от Флоры и легкое чувство превосходства, оставленное Кощеем.

Дверь приоткрывается и входит молодой человек в помятой мантии мага-академика. Его глаза скрыты за толстыми линзами очков, а за спиной парят несколько пергаментных свитков и магический артефакт, похожий на хрустальный шар.

ФОЛИАНТ: Приветствую, босс. Меня прислал Мерлин. Я – Архивариус Фолиант. Можно просто Фол. Ваш техномаг и аналитик. #Р

*   [Техномаг? Редкий специалист.] -> choice_technomage
*   [Проходи скорее, Фол. У нас полно работы!] -> choice_welcome

=== choice_technomage ===
ФОЛИАНТ: Точно, босс. Соединять магию и технологию непросто. Приходится выслушивать: «Как? Ты используешь магию? Это же прошлый век! Нельзя доверять этим дедовским методам!» или «Как ты только терпишь все эти шумные железяки? Выбрось их все разом, и твоя комната станет намного чище!»
ФОЛИАНТ: А как вы считаете, босс, что круче — магия или технология?
ТЫ:
*   [Ну, это даже мудрецы в Большом Совете решить не могут!] -> fol_response_1
*   [Конечно технология, за ней будущее!] -> fol_response_2
*   [Магия рулит, она дает столько возможностей!] -> fol_response_3
*   [Ну, я за разумный компромисс.] -> fol_response_4

=== fol_response_1 ===
ФОЛИАНТ: Это точно, босс! #Р
-> fol_enters

=== fol_response_2 ===
ФОЛИАНТ: Человек с разумным взглядом на прогресс! Хотя мои свитки иногда обижаются на такие заявления.
-> fol_enters

=== fol_response_3 ===
ФОЛИАНТ: Уважаю. Главное - не говорите этого моему паяльнику. Он очень ранимый.
-> fol_enters

=== fol_response_4 ===
ФОЛИАНТ: Мой любимый ответ! Зачем выбирать, если можно взять лучшее? Как этот магический кристалл, подключенный к облачному хранилищу.#З
-> fol_enters

=== choice_welcome ===
ФОЛИАНТ: Отлично! Я люблю, когда дело не стоит на месте. Прямо как заряженный кристалл.#Р
-> fol_enters

=== fol_enters ===

Фолиант щелкает пальцами. В воздухе появляются полупрозрачные синие экраны, между которыми текут строки магических кодов. Несколько свитков разворачиваются на столе, а хрустальный шар занимает место в центре композиции.
ФОЛИАНТ: Знакомы с нашими последними разработками, босс? Это – «Когнитивный Кристалл», четвертого поколения! 
ФОЛИАНТ: Магический интерфейс для сбора, анализа и систематизации данных. Улики, гипотезы, даже сплетни – все будет вноситься сюда. Впечатляет, верно? Магические технологии не стоят на месте.
В этот момент Когнитивный Кристалл издает оглушительный писк и начинает мерцать тревожным алым светом.
ФОЛИАНТ: Босс! Мы засекли первую аномалию!#У
Из Кристалла доносится искаженный, полный паники голос.
КОГНИТИВНЫЙ КРИСТАЛЛ: ...Кто-нибудь! Слышит кто-нибудь?! В Гроте... в Хрустальном Гроте Эхо... это невозможно вынести! Этот звук... он сверлит мозги, он сводит с ума! 
ФОЛИАНТ: Босс, сигнал поступил из Хрустального Грота Эхо. Уровень искажения зашкаливает! Возможно что-то техническое. Собирать команду? 
-> fol_enters2

===fol_enters2===
ТЫ:
*   [Давай, Фол, зови всех! Время работать.] -> call_team
*   [Что это за Хрустальный Грот? Давай сначала справку.] -> grotto_info

=== grotto_info ===
КОГНИТИВНЫЙ КРИСТАЛЛ: Хрустальный Грот Эхо. Находится глубоко в недрах горы. Добраться можно через потайной ход, известный горным духам, или наведенным порталом. Природная и магическая достопримечательность. Место силы. Горные элементали используют его для накопления первичной магической энергии.
-> fol_enters2 // Возвращаемся к моменту выбора после справки

=== call_team ===
Ты киваешь Фолианту. Похоже, ваше новое подразделение приступает к работе.
->grot

===grot===
// Начало сцены в Хрустальном Гроте
#Location: Grot_Anomalia
Вы внутри Хрустального Грота Эхо. Грот обычно ослепляет первозданной красотой, но сейчас он наполнен оглушительным, почти механическим визгом. Звук вызывает неестественную, синхронную вибрацию кристаллов.
Несколько мелких сталактитов уже лопнули, их осколки усеяли пол. Слышен слабый треск, похожий на короткое замыкание. На поверхности крупнейшего сталагмита виден геометрический узор из оплавленных точек, словно кто-то поработал лазером.

ФОЛИАНТ ПО СВЯЗИ: Вы на месте! Сенсоры кричат от перегрузки. Если мы не остановим это, грот может рухнуть!#У
~попытка=0

// Начало первого этапа
-> Этап1_Обнаружение

=== Этап1_Обнаружение ===
ТЫ: Надо найти источник шума. Ваши предложения??

+{ дровосек == true }[Спросить Железного Дровосека] -> Дровосек_предложение
+{ дриада == true }[Спросить Флору] -> Флора_предложение
+{ кощей == true }[Спросить Кощея] -> Кощей_предложение
+{ иван == true }[Спросить Ивана Царевича] -> Иван_предложение
+{ кот == true }[Спросить Кота Ученого] -> Кот_предложение
+{ василиса == true }[Спросить Василису] -> Василиса_предложение

=== Дровосек_предложение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Проведу акустическое сканирование. Построю 3D-карту резонансных полей для точного определения координатов источников.#З

+[Действуй!] -> Дровосек_действие
+[Послушаем, что скажут остальные] -> Этап1_Обнаружение

=== Дровосек_действие ===
{
    - кот == true:
        // КОМБИНАЦИЯ: Дровосек + Кот
        Железный Дровосек замирает на месте, издавая низкочастотный гул. Из его глазниц проецируются невидимые волны.
        
        КОТ УЧЕНЫЙ: (взволнованно) Погодите! Эти частотные паттерны... Я видел нечто подобное в "Анналах Древней Акустики"! Это "Звуковые Стражи" — устройства третьей эпохи!#З
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Подтверждаю. Сканирование выявило пять источников. Архитектура соответствует описанию Кота. Обнаружено клеймо: "Громовые Шестерни".#Р
        
        {попытка==0:
            ~улика=улика+2
            ФОЛИАНТ ПО СВЯЗИ: Идентификация и происхождение — превосходный результат!#Р
        }
    - else:
        // Только Дровосек
        Металлический великан замирает на месте, издавая низкочастотный гул. Из его глазниц проецируются невидимые волны.
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Сканирование завершено. Обнаружено пять источников акустической атаки. Расположение неслучайно, образует разрушительный резонансный контур. Координаты занесены. Это инженерная работа, а не природный феномен.#Р
        
        {попытка==0:
            ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ:  Пришлите мне все данные, я занесу их в базу.
        }
}
-> После_обнаружения

=== Кот_предложение ===
КОТ УЧЕНЫЙ: Давайте поищем в летописях. Кажется, в «Каталоге Акустических Чудес» есть описание подобных случаев...#У

+[Действуй!] -> Кот_действие
+[Послушаем, что скажут остальные] -> Этап1_Обнаружение

=== Кот_действие ===
{
    - дровосек == true:
        // КОМБИНАЦИЯ: Кот + Дровосек
        КОТ УЧЕНЫЙ: Так-так-так, "Звуковые Стражи" — устройства для нарушения акустической гармонии! Они должны крепиться к звуковым отражателям!#У
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Подтверждаю. На сталгмитах обнаружены крепления. Сканирую структуру...Сохраняю для анализа.
        
        {попытка==0:
            ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ: Идеально! Фиксирую данные!#Р
        }
    - else:
        // Только Кот
        Кот в панике ныряет в груду парящих свитков.
        КОТ УЧЕНЫЙ: Минуточку, минуточку!.. Где же... Вот! «Звуковые Стражи» — устройства для нарушения акустической гармонии! Они должны крепиться к звуковым отражателям! Смотрите, на тех сталагмитах что-то есть!
        ФОЛИАНТ ПО СВЯЗИ: Хорошее начало, но нужны технические детали...#У
      -> После_обнаружения
}
-> После_обнаружения

=== Кощей_предложение ===
КОЩЕЙ: Проведу спектральный анализ магических искажений. Эта техномагия оставляет такие же грубые следы, как сапог тролля на чистом полу.#З

+[Действуй!] -> Кощей_действие
+[Послушаем, что скажут остальные] -> Этап1_Обнаружение

=== Кощей_действие ===
{
    - дриада == true:
        // КОМБИНАЦИЯ: Кощей + Флора
        Щелкнув пальцами, Кощей создает в воздухе бледно-зеленоватый светящийся узор.
        
        ФЛОРА: (вздрагивая) Ой! Я чувствую... это не просто магия. Это что-то чужеродное, что враждует с природой кристаллов!#У
        
        КОЩЕЙ: Точно. Источники помех. Пять точек. Их энергия конфликтует с естественной магией грота. По следам можно определить производителя.#Р
        
        {попытка==0:
            ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ: Конфликт энергий — важная улика! Записываю.
        }
    - else:
        // Только Кощей
        Щелкнув пальцами, Кощей создает в воздухе бледно-зеленоватый светящийся узел.
        
        КОЩЕЙ: Как я и предполагал. Техномагические источники, расположенные в нескольких точках грота. Пять штук. Примитивно, но эффективно.#Р
        
        {попытка==0:
            ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ:  Отлично. Соберите все данные, я занесу их в базу.#Р
        }
        }
        
-> После_обнаружения

=== Флора_предложение ===
ФЛОРА: Я попробую понять боль кристаллов. Если прислушаться к их вибрациям, они сами покажут, где находятся инородные предметы...Они ведь немножко живые, правда?#У

+[Действуй!] -> Флора_действие
+[Послушаем, что скажут остальные] -> Этап1_Обнаружение

=== Флора_действие ===
{
    - кощей == true:
        // КОМБИНАЦИЯ: Флора + Кощей
        Флора со слезами на глазах касается ближайшего кристалла, но сразу отскакивает, словно от удара током.
        
        ФЛОРА: Ай! Их боль... она повсюду! я чувствую, как чужеродная энергия разрывает их изнутри!#П
        
        КОЩЕЙ: (хладнокровно) Да. Пять точек разрыва. Техномагия. Подожди, дриада, я проанализирую структуру... Вон там, в основании сталагмитов.
		ФЛОРА: Да-да! Чужая энергия идет оттуда!        
        {попытка==0:
            ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ:  Чувствительность природы и магический анализ — это мощно!#Р
                    }
    - else:
        // Только Флора
        Дриада со слезами на глазах касается ближайшего кристалла, но сразу отскакивает, словно от удара током.
                ФЛОРА: Ай! Их боль... она повсюду! Я не могу выделить отдельный источник! Это техногенная угроза, я ничего не могу сделать...#П
       		~попытка=1
         -> Этап1_Обнаружение
           
}
            -> После_обнаружения


=== Иван_предложение ===
ИВАН ЦАРЕВИЧ: Я предлагаю осмотреть все внимательнее. Возможно, удастся что-то заметить - неестественный блеск, отпечатки, следы вмешательства.#У

+[Действуй!] -> Иван_действие
+[Послушаем, что скажут остальные] -> Этап1_Обнаружение

=== Иван_действие ===
{
    - василиса == true:
        // КОМБИНАЦИЯ: Иван + Василиса
        Царевич, щурясь от боли в ушах, медленно обходит грот по периметру. Внезапно он замирает и указывает на один из крупных сталагмитов.
        
        ИВАН ЦАРЕВИЧ: Вот! Видите? Маленькая металлическая штуковина. И там еще одна. Похоже, они расставлены по всему гроту.
        
        ВАСИЛИСА: Дай-ка я спрошу у духов, что они об этом знают... (закрывает глаза) Они говорят, что устройства установлены недавно... Приходили два невысоких человека в плащах...
        
        {попытка==0:
            ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ:  Отлично! Заношу улику в базу...
        }
    - else:
        // Только Иван
        Царевич, щурясь от боли в ушах, медленно обходит грот по периметру. Внезапно он замирает и указывает на один из крупных сталагмитов.
        
        ИВАН ЦАРЕВИЧ: Вот! Видите? Маленькая металлическая штуковина. И там еще одна. Похоже, они расставлены по всему гроту. Источник обнаружен, но принцип ее работы мне непонятен.
        // Без улики
}
-> После_обнаружения

=== Василиса_предложение ===
ВАСИЛИСА: Предлагаю поговорить с духом пещеры. Если он не слишком расстроен, то, возможно, подскажет, где находится источник проблем.#У

+[Действуй!] -> Василиса_действие
+[Послушаем, что скажут остальные] -> Этап1_Обнаружение

=== Василиса_действие ===
{
    - кот == true:
        // КОМБИНАЦИЯ: Василиса + Кот
        Василиса закрывает глаза, пытаясь послать успокаивающий импульс.
        
        ВАСИЛИСА: Дух пещеры слишком напуган, но... он показывает образы... устройства, которые издают звук...#У
        
        КОТ УЧЕНЫЙ: О! Это напоминает описание в "Хрониках подземных эхо"... Устройства, нарушающие акустическую гармонию! Их устанавливали древние инженеры для разрушения пещер!#З
        
        ВАСИЛИСА: Да, и дух говорит, что установщики были в плащах с эмблемой молота и шестерни.
        
        {попытка==0:
            ~улика=улика+1
		ФОЛИАНТ ПО СВЯЗИ: Это уже улика!           
        }
    - else:
        // Только Василиса
        Василиса закрывает глаза, пытаясь послать успокаивающий импульс.
        
        ВАСИЛИСА: Увы! Дух пещеры слишком напуган. Его голос заглушен этим визгом. К сожалению, он не может мне помочь.#П
		~попытка=1
         -> Этап1_Обнаружение
              }  
        -> После_обнаружения

=== После_обнаружения ===
ФОЛИАНТ ПО СВЯЗИ:  Источники найдены! Теперь главное - обезвредить их, не превратив грот в груду битого хрусталя.

-> Этап2_Решение

=== Этап2_Решение ===
 ТЫ:  Кто возьмется?

+{ дровосек == true}[Спросить Железного Дровосека] -> Дровосек_предложение2
+{ дриада == true}[Спросить Флору] -> Флора_предложение2
+{ кощей == true}[Спросить Кощея] -> Кощей_предложение2
+{ иван == true}[Спросить Ивана Царевича] -> Иван_предложение2
+{ кот == true}[Спросить Кота Ученого] -> Кот_предложение2
+{ василиса == true}[Спросить Василису] -> Василиса_предложение2

=== Дровосек_предложение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Мое предложение: точечные электромагнитные импульсы. Они не повредят хрупкие кристаллы, переведут устройства в спящий режим и сохранят их для последующего анализа. Это наиболее эффективный и предсказуемый метод.#З

+[Действуй!] -> Дровосек_нейтрализация
+[Послушаем другие варианты] -> Этап2_Решение

=== Дровосек_нейтрализация ===
{
    - кот == true:
        // КОМБИНАЦИЯ: Дровосек + Кот
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Посылаю точечные импульсы...
        
        КОТ УЧЕНЫЙ: В архивах есть указание на уязвимость таких устройств к определенной частоте!#З
        
        Дровосек использует подсказку Кота. Устройства отключаются идеально.
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Деактивация успешна. Устройства в идеальном состоянии. Получены полные данные.#Р
        
        КОТ УЧЕНЫЙ: Я узнаю клеймо гильдии "Громовые Шестерни". Это их эмблема.#Р
        ФОЛИАНТ ПО СВЯЗИ:  Отлично! Фиксирую данные! #Р
        ~улика=улика+2
    - else:
        // Только Дровосек
        Дровосек наводит указательный палец на одно из устройств. Раздается тихий щелчок, и визг от этого источника мгновенно прекращается. Он методично повторяет процедуру для остальных четырех.
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Задача выполнена. Устройства деактивированы, целы и готовы к анализу. На корпусе обнаружено клеймо: «Громовые Шестерни».#Р
        
        ФОЛИАНТ ПО СВЯЗИ:  Я что-то слышал об этой гильдии! Явная улика!#Р
        
        ~улика=улика+1
}
-> После_нейтрализации

=== Кот_предложение2 ===
КОТ УЧЕНЫЙ: Я только что нашёл! В «Манускрипте Молчания» описан именно этот случай! Специальное заклинание «Прерывание алгоритма»! Оно не ломает устройства, а обманывает их, переводя в спящий режим. Все данные останутся в сохранности. Методика проверенная временем!#Р
+[Действуй!] -> Кот_нейтрализация
+[Послушаем другие варианты] -> Этап2_Решение

=== Кот_нейтрализация ===
{
    - кощей == true:
        // КОМБИНАЦИЯ: Кот + Кощей
        КОТ УЧЕНЫЙ: Произношу заклинание "Прерывание алгоритма"...
         КОЩЕЙ: Позволь мне усилить его магией тьмы, чтобы оно прошло через любую защиту.
         Совместное заклинание работает безупречно. Устройства отключаются, сохраняя все данные.
          КОЩЕЙ: Готово. Я добавил отслеживающий след на каждое устройство.#Р
          КОТ УЧЕНЫЙ: И данные полностью сохранены! Мы можем проанализировать их!#Р
        ФОЛИАНТ ПО СВЯЗИ:  То, что нужно!
        ~улика=улика+2
    - else:
        // Только Кот
        Кот, зажмурившись, бормочет сложное заклинание. Устройства одно за другим издают тихий щелчок и гаснут, как уснувшие светлячки.
        КОТ УЧЕНЫЙ: Получилось! Заклинание «Прерывание алгоритма» сработало! Устройства полностью обезврежены и готовы для изучения.#Р
        ФОЛИАНТ ПО СВЯЗИ:  Спасибо, профессор. Это улика!#Р
      
        ~улика=улика+1
}
-> После_нейтрализации

=== Флора_предложение2 ===
ФЛОРА: Эти железные штучки так больно впились в бедные кристаллики! Давайте я попрошу корни и лианы аккуратно опутать их и вытащить. Природа сделает это бережн, но... некоторые кристаллы могут треснут#У

+[Действуй!] -> Флора_нейтрализация
+[Послушаем другие варианты] -> Этап2_Решение

=== Флора_нейтрализация ===
{
    - дровосек == true:
        // КОМБИНАЦИЯ: Флора + Дровосек
        ФЛОРА: Я попрошу растения аккуратно обвить устройства...
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: (Рисует чертеж на камне) Предлагаю направить рост корней по такой траектории, чтобы минимизировать повреждения.
        Флора и Дровосек работают вместе. Лианы осторожно обвивают устройства и вытаскивают их, почти не повреждая кристаллы.
        ФЛОРА: Готово! Устройства целы, и кристаллики почти не пострадали!#Р
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Устройства сохранены для анализа. Вижу клеймо.
		ФОЛИАНТ ПО СВЯЗИ:  Заношу в базу данных! #Р
        ~улика=улика+1
    - else:
        // Только Флора
        Флора что-то напевает, и по стенам грота начинают стремительно ползти лианы и корни. Они плотно опутывают устройства и вырывают их. Несколько кристаллов при этом получают небольшие сколы. Визг прекращается.
        ФЛОРА: Готово! Правда, бедные кристаллики немного пострадали... А эти железки, кажется, сломались...
}
-> После_нейтрализации

=== Кощей_предложение2 ===
КОЩЕЙ: Эта техномаговая безвкусица оскорбляет взгляд. Я применю целевое разлагающее заклятье. Чисто и эффективно. Оно уничтожит устройства, но сохранит кристаллы невредимыми.#У

+[Действуй!] -> Кощей_нейтрализация
+[Послушаем другие варианты] -> Этап2_Решение

=== Кощей_нейтрализация ===
{
    - кот == true:
        // КОМБИНАЦИЯ: Кощей + Кот
        КОЩЕЙ: Я растворю эти устройства...
         КОТ УЧЕНЫЙ: Погодите! Я нашел заклинание, которое скопирует магическую схему перед уничтожением!#З
         Кот быстро шепчет заклинание. Кощей добавляет свое. Устройства исчезают, но в воздухе остаются светящиеся схемы.
         КОЩЕЙ: Хм... неожиданно полезно. Теперь у нас есть схемы для изучения.#Р
        ФОЛИАНТ ПО СВЯЗИ:  То что нужно!#Р
            ~улика=улика+1
    - else:
        // Только Кощей
        Кощей проводит рукой, и устройства один за другим растворяются в клубах черной дымки. Визг прекращается.
        КОЩЕЙ: Дело сделано.
        ФОЛИАНТ ПО СВЯЗИ:  Босс, я потерял энергетические следы на устройствах! Они полностью уничтожены!#П
}
-> После_нейтрализации

=== Иван_предложение2 ===
ИВАН ЦАРЕВИЧ: Давайте я просто собью устройства стрелами. Быстро и надежно, хотя устройства, возможно, повредятся.#У

+[Действуй!] -> Иван_нейтрализация
+[Послушаем другие варианты] -> Этап2_Решение

=== Иван_нейтрализация ===
{
    - дровосек == true:
        // КОМБИНАЦИЯ: Иван + Дровосек
        ИВАН ЦАРЕВИЧ: Я собью их стрелами!
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: (Рисует чертеж на камне) Рассчитаем траекторию полета стрелы, чтобы устройства упали на мягкую поверхность.
        Иван стреляет. Устройства падают на заранее подготовленные мягкие моховые подушки.
        ИВАН ЦАРЕВИЧ: Все сбиты!#Р
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Устройства повреждены, но основные компоненты целы. Можно анализировать.
        ФОЛИАНТ ПО СВЯЗИ:  Тащите их на базу, проведем полный анализ!
        ~улика=улика+1
    - else:
        // Только Иван
        Иван быстрыми и точными выстрелами сбивает все пять устройств. Они со звоном разбиваются о каменный пол.
        ИВАН ЦАРЕВИЧ: Враг повержен! От этих штуковин теперь одни осколки.
        ФОЛИАНТ ПО СВЯЗИ: И от улик тоже...#П
}
-> После_нейтрализации

=== Василиса_предложение2 ===
ВАСИЛИСА:  А что, если попробовать поговорить с устройствами? Всё в этом мире имеет каплю сознания. Если объяснить, какой вред они причиняют, может, они сами остановятся? Стоит попробовать?#У

+[Действуй!] -> Василиса_нейтрализация
+[Послушаем другие варианты] -> Этап2_Решение

=== Василиса_нейтрализация ===
  // Только Василиса (катастрофа)
        ВАСИЛИСА: Уважаемые источники, пожалуйста, поймите, вы причиняете вред этим прекрасным созданиям...#У
      Её слова тонут в оглушительном визге. Устройства начинают мигать в тревожном ритме.
        ФОЛИАНТ ПО СВЯЗИ:  (панически) Босс, что вы делаете?! Уровень звукового давления зашкаливает! Магический резонанс нарастает!#З
#Location: Grot_KATASTROFA        
       Монотонный вой перерастает в оглушительный грохот. Гигантские сталактиты один за другим откалываются от потолка и с гулким треском обрушиваются вниз, разбиваясь на миллионы сверкающих осколков. Потолок и стены начинают покрываться трещинами, грот, веками хранивший своё эхо, исчезает под тоннами битого хрусталя и камня. Вы еле-еле успеваете спастись и возвращаетесь на базу.
        
        ~катастрофа = true
    -> Итоги

=== После_нейтрализации ===
#Location: Grot
Визг смолк. В гроте воцаряется блаженная, звенящая тишина.
+[Осмотреть устройство] ->Osmotr1
+[Вернуться на базу] ->Итоги

===Osmotr1===
#Location: Osmotr1
ФОЛИАНТ ПО СВЯЗИ: Зафиксировал данные. Устройство предназначено для преобразования акустических колебаний в электрический ток. Принцип работы — магнитно-резонансный.
-> Итоги

=== Итоги ===
#Location: Ofis
~всего_улик=всего_улик+улика
ФОЛИАНТ:Передаю итоговый отчет.

{ катастрофа:
ФОЛИАНТ: Мы не справились, босс. Гроту нанесён серьёзный ущерб. Потребуются месяцы, если не годы, магической реставрации. Эх... Нам нужно быть осторожнее. Не каждый меч годится для резьбы по хрусталю. Урок, как говорится, выучен. Дорогой ценой.#П
}

{улика>=3:
ФОЛИАНТ: Босс, это блестящий результат! Мы не только спасли грот, но и получили четкие зацепки. У нас есть образец устройства и данные о том, кто его создал и устанавливал. Цепочка ведет к гильдии «Громовые Шестерни».#Р
}

{ улика ==2 && катастрофа==false:
ФОЛИАНТ: Грот спасен, и это главное. Босс, мы получили интересную информацию. Похоже, улики ведут к гильдии «Громовые Шестерни». Но пока еще доказательств недостаточно.
}

{улика<=1 && катастрофа==false:
ФОЛИАНТ: Мы потушили пожар, но не нашли поджигателя. Грот цел, но виновные пока неизвестны... Нам необходимы улики!#У
}

{ дровосек == true && дриада == true:
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Нужен более рациональный и логичный подход. Эмоциональность снижает эффективность принимаемых решений на 38%.#У
ФЛОРА: (презрительно) Бездушная железяка!#З
}
{ дриада == true && иван == true:
ФЛОРА: Это было так ужасно! Так ужасно! Я чуть не закричала от ужаса! #П
ИВАН ЦАРЕВИЧ: Флора, я бы тебя с собой в разведку не взял. Ты слишком шумная!#З
}

КОГНИТИВНЫЙ КРИСТАЛЛ: Всего в базу данных занесено улик: {всего_улик} 
~катастрофа=false


ФОЛИАНТ: Первое дело закрыто. Поздравляю, босс. А у нас новая проблема: Нужно спасти облачные корабли в небесной верфи. Там какая-то ржавчина появилась... Откуда на облаках ржавчина?#У
ТЫ:
+[Перебрасывай нас туда немедленно!] -> НЕБЕСНАЯ_ВЕРФЬ
+[Небесная верфь? Это еще что такое?] 
КОГНИТИВНЫЙ КРИСТАЛЛ: Небесная Верфь — это мастерская воздушных духов-альвов, парящая высоко в небе на облачном плато. Альвы  вдыхают жизнь в облака, придавая им изящные формы фрегатов и каравелл. Они даже считают свои корабли живыми существами. Каждый такой корабль связан с создавшим его альвом незримой магической нитью. 
 -> НЕБЕСНАЯ_ВЕРФЬ

=== НЕБЕСНАЯ_ВЕРФЬ ===
~улика=0
~попытка=0
#Location: Verf_Anomalia
Вы стоите на парящем в небе плато — Небесной Верфи.

Величественные облачные корабли, обычно парящие с невесомой грацией, теперь медленно опускаются на землю. Ржавая пыль разъедает их эфемерные борта. Резкие порывы ветра вздымают железную пыль и создают хаотичные вихри и непредсказуемые потоки. 
Стайки ветряных фей мечутся над плато, встревоженно перекрикиваясь. Духи-альвы бесцельно бродят по палубам корабля, в их голосах чувствуется растерянность. Они ничем не могут помочь своим творениям.

ФОЛИАНТ ПО СВЯЗИ:  Босс, сенсоры показывают аномальную концентрацию частиц небесного железа в атмосфере. Если корабли упадут — это будет катастрофа. Нужно найти источник этой железной пыли!

-> Этап2_Решение1

=== Этап2_Решение1 ===
ТЫ: Команда, какие будут предложения?
+ { дровосек == true} [Спросить Железного Дровосека]->Дровосек_предложение3
+ { дриада == true} [Спросить Флору]->Флора_предложение3
+ { кощей == true} [Спросить Кощея]->Кощей_предложение3
+ { иван == true} [Спросить Ивана Царевича]->Иван_предложение3
+ { кот == true} [Спросить Кота Ученого]->Кот_предложение3
+ { василиса == true} [Спросить Василису]->Василиса_предложение3

=== Дровосек_предложение3 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Попытаюсь проанализировать состав и плотность пыли. Это позволит вычислить эпицентр её распространения по градиенту плотности.#У
+ [Действуй!]->Дровосек_действие1
+ [Послушаем другие варианты]->Этап2_Решение1

=== Дровосек_действие1 ===
        {  иван == true || василиса == true:
        // КОМБИНАЦИЯ: Дровосек с помощником
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Начинаю анализ градиента плотности...
        
        {
            - иван == true:
                ИВАН ЦАРЕВИЧ: Я заметил, что на северной стороне плато пыль лежит толще. Может, там искать?
            - василиса == true:
                ВАСИЛИСА: Альвы говорят, что ветер обычно дует с севера по утрам. Возможно, это поможет?
        }
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Корректирую алгоритм с учётом дополнительных данных. Источник определён: Пик Грифона, северо-восточный склон. Анализ показывает техногенное происхождение пыли.
        
        {попытка==0:
           ~улика=улика+1  
            ФОЛИАНТ ПО СВЯЗИ:  Командный анализ дал результат!
        }
    - else:
        // Только Дровосек
        ЖЕЛЕЗНЫЙ ДРОВОСЕК:Ветер перемешивает пыль, снижая эффективность анализа на 75,7 %. Логика тут не эффективна. Фиксирую аномальную концентрацию частиц на севере и северо-востоке. Рекомендую провести дополнительные замеры в этом направлении.
}
~попытка=1
->Этап2_после_обнаружения

=== Флора_предложение3 ===
ФЛОРА: Это же экологическая катастрофа! Я поговорю с ветром и ветряными феями. Они видят все и наверняка знают, откуда дует этот «железный» ветер.#З

+ [Действуй!]->Флора_действие1
+ [Послушаем другие варианты]->Этап2_Решение1

=== Флора_действие1 ===
{
    - василиса == true || кощей == true:
        // КОМБИНАЦИЯ: Флора с Василисой или Кощеем
        Дриада закрывает глаза, подставляя лицо ветру. Ее волосы колышутся в такт невидимым ритмам.
        
        {
            - василиса == true:
                ВАСИЛИСА: Можно я помогу? (обращаясь к ветряным феям) Не бойтесь, ответьте нам, мы друзья, мы хотим помочь...
                ФЛОРА: Они показывают... железного паука на Пике Грифона! И говорят... у него на боку клеймо молота и шестерни!#З
            - кощей == true:
                КОЩЕЙ: Позволь мне усилить вашу связь с помощью магии....#З
                ФЛОРА: Спасибо! Теперь я вижу гораздо чётче... Механизм в виде паука на Пике Грифона! Пыль идет оттуда!#Р
        }
        
        {попытка==0:
           ~улика=улика+2 
            ФОЛИАНТ ПО СВЯЗИ:  Отличная командная работа! Детализированная информация!#Р
        }
    - else:
        // Только Флора (как в оригинале)
        Дриада закрывает глаза, подставляя лицо ветру. Ее волосы колышутся в такт невидимым ритмам.
        ФЛОРА: Ветер с севера... он несет пыль и холодную злобу! Ветряные феи показывают мне железного паука на Пике Грифона, он плюется этой ржавой дымкой!
           {попытка==0:
           ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ:  Отлично! Я поищу в базе похожие механизмы!
        }
}
~попытка=1
->Этап2_после_обнаружения

=== Кощей_предложение3 ===
КОЩЕЙ: Проведу отслеживание по магическому шлейфу. Эта пыль не природная, а значит, несет на себе отпечаток создателя.#З

+ [Действуй!]->кощей_действие1
+ [Послушаем другие варианты]->Этап2_Решение1

=== кощей_действие1 ===
{
    - дриада == true || василиса == true:
        // КОМБИНАЦИЯ: Кощей с Флорой или Василисой
        Кощей проводит рукой, и частицы пыли в воздухе начинают светиться зловещим фиолетовым светом.
        
        {
            - дриада == true:
                ФЛОРА: (вздрагивая) Эта магия губит растения. На Пике Грифона уже весь склон безжизненный!..#У
                КОЩЕЙ: Именно. Шлейф как раз и ведет на север. И судя по характерным следам... это работа "Громовых Шестерней".#З
            - василиса == true:
                ВАСИЛИСА: Магия оставляет эмоциональный след... в ней есть гнев, но и страх тоже. Кто-то боится быть обнаруженным.#У
                КОЩЕЙ: Интересное наблюдение. Шлейф ведет к Пику Грифона. И да, в магии есть нотки страха. Значит, преступник не уверен в себе.#З
        }
                {попытка==0:
           ~улика=улика+2  
            ФОЛИАНТ ПО СВЯЗИ:  Анализ с дополнительным контекстом — ценная улика!#Р
        }
    - else:
        // Только Кощей (как в оригинале)
        Кощей проводит рукой, и частицы пыли в воздухе начинают светиться зловещим фиолетовым светом, выстраиваясь в четкую линию, ведущую на север.
        
        КОЩЕЙ: Как я и думал. Грубая работа. Шлейф ведет прямиком к одинокой скале. Пик Грифона, кажется. Никакой маскировки. Самоуверенные недоучки.#Р
        {попытка==0:
           ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ:  Это улика, заношу ее в базу.
        }
}
~попытка=1
->Этап2_после_обнаружения

=== Иван_предложение3 ===
ИВАН ЦАРЕВИЧ: Я обойду край плато в поисках следов. Для установки устройства нужен был доступ, а значит, точно должны остаться следы.#У

+ [Действуй!]->иван_действие1
+ [Послушаем другие варианты]->Этап2_Решение1

=== иван_действие1 ===
{
    - дровосек == true || дриада == true:
        // КОМБИНАЦИЯ: Иван с Дровосеком или Флорой
        Иван Царевич внимательно изучает край плато.
        
        {
	            - дриада == true:
                ФЛОРА: Иван, обрати внимание на примятые растения. Кто-то недавно прошел по этой тропе с тяжелой ношей.#З
                ИВАН ЦАРЕВИЧ: Точно, вот следы грубой обуви. И металлические опилки... Ведут на север.#З
                        }
            - дровосек == true:
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Изучаю найденные следы... Металлическая стружка имеет уникальный сплав. Анализирую...
                ИВАН ЦАРЕВИЧ: И еще царапины от чего-то тяжелого. Смотри — ведут на север, к Пику Грифона.#З
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Подтверждаю. По составу стружки — производство "Громовых Шестерней".#З
        
        {попытка==0:
           ~улика=улика+1 
           ФОЛИАНТ ПО СВЯЗИ: Команда нашла убедительные улики!#Р 
                   }
    - else:
        // Только Иван
        Иван Царевич внимательно изучает край плато.
        ИВАН ЦАРЕВИЧ: К сожалению, большинство следов засыпано металлической пылью. Но сюда точно поднимались. Недавно. Видны следы грубой обуви и... царапины от чего-то тяжелого и металлического. Источник должен быть где-то на Пике Грифона.#У
}
~попытка=1
->Этап2_после_обнаружения

=== Кот_предложение3 ===
КОТ УЧЕНЫЙ: Предлагаю свериться с манускриптами по атмосферной магии. Возможно там есть описания аналогичных случаев.#У

+ [Действуй!]->кот_действие1
+ [Послушаем другие варианты]->Этап2_Решение1

=== кот_действие1 ===
{
    - дровосек == true:
        // КОМБИНАЦИЯ: Кот с Дровосеком
        КОТ УЧЕНЫЙ: Так, м-да... «Трактат о летающих островах»...#У
           {
            - дровосек == true:
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Профессор, предлагаю совместить исторические данные с анализом состава пыли...
                КОТ УЧЕНЫЙ: Да, да! Вот! "Металлические туманы третьей эпохи, создаваемые устройствами горных кузнецов"... Описание симптомов совпадает!#З
                   }
        
        {попытка==0:
           ~улика=улика+1
            ФОЛИАНТ ПО СВЯЗИ: Совместный поиск дал результат!#Р
        }
    - else:
        // Только Кот
        КОТ УЧЕНЫЙ: Так, м-да, «Трактат о летающих островах»... нет, не то. «Песни небесных мореходов»... э-э-э... Здесь слишком много теорий и ничего конкретного!.. Редкий случай, когда древние летописи бесполезны... Нужно мнение другого специалиста...#П
       ~попытка=1
->Этап2_Решение1
}
       ->Этап2_после_обнаружения

=== Василиса_предложение3 ===
ВАСИЛИСА: Я вежливо расспрошу альвов. Они могли заметить что-то необычное в последние дни.#У

+ [Действуй!]->василиса_действие1
+ [Послушаем другие варианты]->Этап2_Решение1

=== василиса_действие1 ===
{
    - дриада == true || иван == true:
        // КОМБИНАЦИЯ: Василиса с Флорой или Иваном
        Василиса подходит к группе напуганных альвов.
        
        {
            - дриада == true:
                ФЛОРА: Я с тобой, Василиса. Я помогу успокоить их... #З
                ВАСИЛИСА: Они говорят, что видели вспышки на Пике Грифона три дня назад. 
                ФЛОРА: Ветер принёс пыль с севера.
            - иван == true:
                ИВАН ЦАРЕВИЧ: Я помогу задать точные вопросы. Когда именно? В какое время? С какой стороны пика?#З
                ВАСИЛИСА: Они точно говорят: северо-восточный склон, на закате. И слышали странный гул.#З
                ИВАН ЦАРЕВИЧ: Значит, устройство расположена на Пике Грифона.
        }
        
        {попытка==0:
           ~улика=улика+1 
		ФОЛИАНТ ПО СВЯЗИ:  Отлично, заношу улики в базу.#Р     
}
    - else:
        // Только Василиса
        Василиса подходит к группе напуганных альвов. После нескольких минут тихого разговора один из них указывает дрожащей рукой на север.
         ВАСИЛИСА: Альвы видели странные вспышки на Пике Грифона ровно три дня назад. Тогда все и началось.
}
~попытка=1
->Этап2_после_обнаружения

=== Этап2_после_обнаружения ===
ФОЛИАНТ ПО СВЯЗИ:  Источник на Пике Грифона! Желательно его обезвредить и очистить воздух. Ну, и про улики не забывайте!

->Этап2_Решение2

=== Этап2_Решение2 ===
ТЫ: Команда! Ваши предложения?
+ { дровосек == true} [Спросить Железного Дровосека]->Дровосек_предложение4
+ { дриада == true} [Спросить Флору]->Флора_предложение4
+ { кощей == true} [Спросить Кощея]->Кощей_предложение4
+ { иван == true} [Спросить Ивана Царевича]->Иван_предложение4
+ { кот == true} [Спросить Кота Ученого]->Кот_предложение4
+ { василиса == true} [Спросить Василису]->Василиса_предложение4

=== Дровосек_предложение4 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Предлагаю установить сеть магнитных фильтров и ветровых турбин. Это позволит деактивировать источник пыли и собрать выброшенные частицы для анализа.#З
+ [Действуй!]->Дровосек_нейтрализация2
+ [Послушаем другие варианты]->Этап2_Решение2

=== Дровосек_нейтрализация2 ===
{
    - кот == true || иван == true:
        // КОМБИНАЦИЯ: Дровосек с Котом или Иваном
        Железный Дровосек устанавливает несколько устройств по краю плато.
        
        {
            - кот == true:
                КОТ УЧЕНЫЙ: В архивах есть схема подобных распылителей! У них есть резервный источник питания...#З
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Учитываю. Корректирую параметры импульса для одновременного отключения основной и резервной систем.
            - иван == true:
                ИВАН ЦАРЕВИЧ: Я могу подняться на пик и визуально подтвердить отключение. И если что — вручную отключу.#З
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Хорошо. Координирую действие. Мои сенсоры будут отслеживать ваше продвижение.
        }
        
               ЖЕЛЕЗНЫЙ ДРОВОСЕК: Источник деактивирован. Пыль собрана. Мы получили данные о системе защиты устройства.
        
        ФОЛИАНТ ПО СВЯЗИ:  Отличная командная работа! #Р
        
       ~улика=улика+2 
    - else:
        // Только Дровосек 
        Железный Дровосек устанавливает несколько устройств по краю плато. С щелчком они активируются, создавая невидимую сеть. Железная пыль начинает притягиваться к ним, а на Пике Грифона видна вспышка короткого замыкания.
        
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Источник деактивирован. Пыль собрана. Анализ показывает уникальный сплав, патентованный гильдией «Громовые Шестерни».#Р
        
        ФОЛИАНТ ПО СВЯЗИ:  Отличная работа! Заношу в базу данных.
       ~улика=улика+1
}
->После_нейтрализации2

=== Флора_предложение4 ===
ФЛОРА: Эта ржавая дымка душит небо! Я призову чистый, омывающий ливень. Он будет нежным, как роса, и смоет всю эту гадость.#З

+ [Действуй!]->Флора_нейтрализация2
+ [Послушаем другие варианты]->Этап2_Решение2

=== Флора_нейтрализация2 ===
{
    - василиса == true || кощей == true:
        // КОМБИНАЦИЯ: Флора с Василисой или Кощеем
        {
            - василиса == true:
                ВАСИЛИСА: Альвы, помогите направить дождь так, чтобы он очистил, но не повредил корабли!
                      - кощей == true:
                КОЩЕЙ: А я пока, пожалуй, займусь отключением распылителя...
                    }
        
        Флора воздевает руки к небу, и с ясного неба обрушивается теплый ливень, мягко омывающий борта кораблей.
        ФЛОРА: Вот так-то лучше! Чистота! Давайте возьмем пробы воды, воздуха и грунта для анализа этой гадости.#Р
        ФОЛИАНТ ПО СВЯЗИ: Согласен, это может дать дополнительную информацию. Отличная командная работа!
           ~улика=улика+2  
    - else:
        // Только Флора
        Флора воздевает руки, и с ясного неба обрушивается теплый, сильный ливень. Он смывает пыль с кораблей, и те сразу вздымаются выше, расправляя туманные паруса.
                ФЛОРА: Вот так-то лучше! Чистота! Давайте возьмем пробы воды, воздуха и грунта для анализа этой гадости..#Р
       ФОЛИАНТ ПО СВЯЗИ:  Согласен, это может дать дополнительную информацию.
               ~улика=улика+1
}
->После_нейтрализации2

=== Кощей_предложение4 ===
КОЩЕЙ: Я могу наложить локальную область анти-магии. Внутренняя магическая схема распылителя сотрётся. Пыль перестанет поступать и осядет вниз.#У
+ [Действуй!]->Кощей_нейтрализация2
+ [Послушаем другие варианты]->Этап2_Решение2

=== Кощей_нейтрализация2 ===
{
    - дриада == true || василиса == true:
        // КОМБИНАЦИЯ: Кощей с Флорой или Василисой
        {
            - дриада == true:
                ФЛОРА: Я постараюсь смягчить воздействие на природу...#З
                КОЩЕЙ: Хорошо. Тогда я сфокусирую заклинание только на устройстве.
            - василиса == true:
                ВАСИЛИСА: Кощей, если ты сотрёшь магическую схему, сможешь ли ты сохранить её копию для изучения?#З
                КОЩЕЙ: ...Интересная мысль... Да, пожалуй, смогу.
        }
        
        Кощей щелкает пальцами в сторону пика. Раздается глухой хлопок, и распылитель замирает.
        КОЩЕЙ: Готово. Магическая схема стёрта, но я сохранил её отпечаток. Пыль перестала поступать.#Р
        ФОЛИАНТ ПО СВЯЗИ: Отпечаток схемы? Это же готовая улика! Теперь мы узнаем принцип работы.#Р        
       ~улика=улика+1
    - else:
        // Только Кощей 
        Кощей щелкает пальцами в сторону пика. Раздается глухой хлопок, и распылитель замирает. Пыль перестает поступать, но та, что висела в воздухе, тяжело оседает на плато, покрывая его ржавым налетом.
        ФОЛИАНТ ПО СВЯЗИ: Придется теперь альвам делать генеральную уборку.#У
}
->После_нейтрализации2

=== Иван_предложение4 ===
ИВАН ЦАРЕВИЧ: Вижу конструкцию - простую, но с хитрым креплением. Подобраться можно только по скалам. Я заберусь и аккуратно отключу его. Никаких взрывов, устройство останется целым. Иногда лучший ключ - не магия, а верный инструмент и ловкие руки.#З

+ [Действуй!]->Иван_нейтрализация2
+ [Послушаем другие варианты]->Этап2_Решение2

=== Иван_нейтрализация2 ===
{
    - дровосек == true || василиса == true:
        // КОМБИНАЦИЯ: Иван с Дровосеком или Василисой
        Иван, проявляя чудеса ловкости, карабкается по скалам на Пик Грифона.
        {
            - дровосек == true:
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: (по связи) Иван, рекомендую сначала отключить синий провод, затем красный. Иначе сработает защита.#З
                ИВАН ЦАРЕВИЧ: Понял. Синий... красный... Готово.
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Подтверждаю деактивацию. Устройство цело, логи сохранены.
            - василиса == true && дровосек == false:
                ВАСИЛИСА: (по связи) Иван, альвы говорят, что с восточной стороны есть более безопасный путь.
                ИВАН ЦАРЕВИЧ: Вижу, спасибо! ... Готово. Устройство отключено без повреждений.
                ВАСИЛИСА: Еще альвы видели, как устройство устанавливали двое людей в плащах с эмблемой шестерни.
				ИВАН ЦАРЕВИЧ: Вижу следы сапог. Альвы правы, злодеи поднимались здесь.
        }
        
        ФОЛИАНТ ПО СВЯЗИ:  Мастерская командная работа!#Р
        
       ~улика=улика+2 
    - else:
        // Только Иван 
        Иван, проявляя чудеса ловкости, карабкается по скалам к Пику Грифона. Он находит распылитель, перекусывает провода и откручивает несколько винтов в нужной последовательности. Устройство затихает.
        ИВАН ЦАРЕВИЧ: Готово. Механика - она простая, главное понять, какой проводок дернуть.#Р
        ФОЛИАНТ ПО СВЯЗИ:  Мастерская работа! Тащи механизм на базу, изучим и занесем в базу данных!
           ~улика=улика+1
}
->После_нейтрализации2

=== Кот_предложение4 ===
КОТ УЧЕНЫЙ: Кажется, нашёл... В «Трактате о тяжёлом небе» описан ритуал «Утяжеления Воздуха»! Если воздух станет плотнее, пыль не сможет в нём держаться и осядет! Правда, тут не сказано, что будет с самими облачными конструкциями... Теоретически это должно сработать!#У

+ [Действуй!]->Кот_нейтрализация2
+ [Послушаем другие варианты]->Этап2_Решение2

=== Кот_нейтрализация2 ===
{
    - кощей == true || дровосек == true:
        // КОМБИНАЦИЯ: Кот с Кощеем или Дровосеком (избегает катастрофы)
        КОТ УЧЕНЫЙ: Начинаю ритуал...
        
        {
            - кощей == true:
                КОЩЕЙ: (резко) Стоп! Твой ритуал не учитывает магическую природу облаков! Дай я скорректирую...#З
                Кощей быстро произносит модифицированное заклинание. Воздух уплотняется, облака остаются стабильными. Пыль постепенно оседает.
				КОТ УЧЕНЫЙ: Ох, спасибо... Кажется мое заклинание чуть не привело к катастрофе...#П
					->После_нейтрализации2
            - дровосек == true:
                ЖЕЛЕЗНЫЙ ДРОВОСЕК: Профессор, мои расчёты показывают, что при уплотнении воздуха на 15% облачные структуры потеряют целостность. Безопасный предел — 7%.#З
                КОТ УЧЕНЫЙ: Ох, точно! Спасибо!..  Постараюсь быть осторожнее...
                   ->После_нейтрализации2
        }
          
          - else:
        // Только Кот 
        Кот бормочет заклинание, над верфью возникает небольшой вихрь, который поднимает еще больше пыли и заставляет альвов в ужасе разбегаться. Воздух становится тяжелым, а корабли еще быстрее опускаются на дно ущелья.
        #Location: Verf_KATASTROFA
        // КАТАСТРОФА
        Облачные корабли один за другим теряют форму, превращаясь в бесформенные клочья тумана. Альвы, отчаянно цепляющиеся за свои творения, вместе с ними падают в бездну. Через несколько минут от величественной верфи не остаётся ничего, кроме ржавой пыли, медленно оседающей на опустевшем плато под одинокий вой ветра.
         КОТ УЧЕНЫЙ: Ох... кажется, что-то пошло не так...#П
        ФОЛИАНТ ПО СВЯЗИ:  Быстрее возвращайтесь на базу! Дышать этой пылью вредно для здоровья!#З
         ~катастрофа = true
       ->Итоги2
}

=== Василиса_предложение4 ===
ВАСИЛИСА: Не нужно ничего отключать и рисковать! Главное — спасти корабли. Давайте позовём альвов, создадим живую цепь и аккуратно перенесём повреждённые суда в безопасное место. Это даст нам время спокойно обдумать, как обезвредить устройство.#У
+ [Действуй!]->Василиса_нейтрализация2
+ [Послушаем другие варианты]->Этап2_Решение2

=== Василиса_нейтрализация2 ===
          Альвы, под руководством Василисы, начинают действовать. Они успевают перенести несколько кораблей. Однако процесс идёт слишком медленно, и не все корабли удаётся спасти.
        ФОЛИАНТ ПО СВЯЗИ: Босс, бльшинство кораблей эвакуировано. Это неплохой результат. Но теперь здесь техномаги из столицы — они займутся источником и наверняка перекроют нам доступ к уликам. Возвращаемся на базу?
->После_нейтрализации2

=== После_нейтрализации2 ===
#Location: Verf
Воздух на Верфи снова стал чистым и прозрачным. Облачные корабли медленно, но верно набирают высоту, их краски вновь заиграли на солнце. Успокоенные альвы возобновляют свою работу.
+[Осмотреть устройство] ->Osmotr2
+[Вернуться на базу] ->Итоги2

===Osmotr2===
#Location: Osmotr2
ФОЛИАНТ ПО СВЯЗИ: Свиток содержит данные о попытке сбора атмосферной магии. Интересная конструкция — магнитные ловушки для облачных частиц.
-> Итоги2

=== Итоги2 ===
~всего_улик=всего_улик+улика
#Location: Ofis
ФОЛИАНТ:Передаю итоговый отчет.

{ катастрофа:
ФОЛИАНТ: Мы почти потеряли Небесную Верфь, босс. Для ее восстановления потребуются дополнительные ресурсы и месяцы работы лучших облакопрядов. Урок ясен, как неба после грозы: нам нужно тщательнее подбирать специалистов для каждой задачи. Молоток - не универсальный инструмент.#П
}

{улика>=3 :
ФОЛИАНТ: Великолепно! Мы не только остановили экологическую катастрофу, но и получили неопровержимые доказательства! След "Громовых Шестерней" становится все горячее!#Р
}

{ улика==2  && катастрофа==false:
ФОЛИАНТ: Верфь спасена. Мы получили важную информацию, но ее пока недостаточно для полной картины. Расследование продолжается.
}

{улика<=1 && катастрофа==false:
ФОЛИАНТ: Мы почти не получили новых зацепок! Противник остался в тени. Нам нужно быть удачливее.#П
}

{ кощей == true && иван == true:
ИВАН ЦАРЕВИЧ: Мы слишком часто используем магию. Иногда нужен простой подход.#У
КОЩЕЙ: (презрительно) Простота — удел примитивных умов.#З
}

{ кощей == true && дровосек == true:
КОЩЕЙ: (презрительно) Технология должна служить магам. Тогда и результат будет! #З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: (щелкает) Имею диаметрально противоположное мнение.#У
ФОЛИАНТ:  Не спорьте, у каждого свои методы! Но согласитесь, иногда вы вполне эффективно работаете вместе!#Р
}

КОГНИТИВНЫЙ КРИСТАЛЛ: В этом деле обнаружено улик: {улика}. Всего улик в базе данных: {всего_улик}
~катастрофа=false
ФОЛИАНТ: Поздравляю, босс, второе дело завершено. Но у нас новый экстренный вызов! Проблема в затонувшей библиотеке! Медлить нельзя, срочная переброска!#У
ТЫ:
+[Перебрасывай нас туда немедленно!]
-> ЗАТОНУВШАЯ_БИБЛИОТЕКА
+[Затонувшая библиотека? Это еще что такое?]
КОГНИТИВНЫЙ КРИСТАЛЛ: Затонувшая библиотека — тысячелетний архив эльфов, покоящийся на дне Серебряного озера. Её светящиеся стены хранят тысячи магических манускриптов, запечатанных в пузыри замедленного времени. Здесь содержатся все забытые языки, песни драконов, рецепты зелий из времён, когда текли медовые реки, зеркальные гримуары, показывающие не прошлое, а возможное будущее.
->  ЗАТОНУВШАЯ_БИБЛИОТЕКА

=== ЗАТОНУВШАЯ_БИБЛИОТЕКА ===
#Location: Bibl_anomalia
Вы находитесь в затопленной эльфийской библиотеке, скрытой на дне озера. Дышать здесь можно только с помощью магии. Бесценные манускрипты и свитки плавают в толще воды, заключенные в пузыри воздуха. Но с ними творится нечто ужасное: чернила на пергаментах расплываются, буквы тают, как сахар, и исчезают в воде.
В углу все заросло какими-то странными водорослями. Они разрастаются прямо на глазах с невообразимой скоростью. Дух-хранитель библиотеки в отчаянии мечется между стеллажами, хватая то один, то другой рассыпающийся свиток.

ФОЛИАНТ ПО СВЯЗИ: Это катастрофа! Каждая секунда на счету.

~ улика = 0
~ попытка = 0

// Начало первого этапа
-> Этап1_Библиотека_Обнаружение

=== Этап1_Библиотека_Обнаружение ===
ТЫ: Что происходит? Как можно найти причину?

+ { дровосек == true} [Спросить Железного Дровосека] -> Дровосек_библиотека_предложение
+ { дриада == true} [Спросить Флору] -> Флора_библиотека_предложение
+ { кощей == true} [Спросить Кощея] -> Кощей_библиотека_предложение
+ { иван == true} [Спросить Ивана Царевича] -> Иван_библиотека_предложение
+ { кот == true} [Спросить Кота Ученого] -> Кот_библиотека_предложение
+ { василиса == true} [Спросить Василису] -> Василиса_библиотека_предложение

=== Дровосек_библиотека_предложение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Я проанализирую целостность энергетического поля, найду в нём самые слабые места.#У
+ [Действуй!] -> Дровосек_библиотека_действие
+ [Послушаем, что скажут остальные] -> Этап1_Библиотека_Обнаружение

=== Дровосек_библиотека_действие ===
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Аномалия обнаружена. Защитное поле равномерно деградирует из-за внешнего воздействия. Источник у вентиляционной шахты. Природа воздействия не установлена.
~ попытка = 1
-> Библиотека_После_Обнаружения

=== Флора_библиотека_предложение ===
ФЛОРА: Могу попробовать вырастить чувствительные водоросли. Они покажут, где магия больше всего искажена — там и искать причину.#У
+ [Действуй!] -> Флора_библиотека_действие
+ [Послушаем, что скажут остальные] -> Этап1_Библиотека_Обнаружение

=== Флора_библиотека_действие ===
{
    - кощей == true:
        // Комбинация Флора + Кощей
        ФЛОРА: Я начну выращивать водоросли-индикаторы, Кощей, проследишь за магической реакцией?
        КОЩЕЙ: Договорились. Только не переборщи с удобрением, дриада.
        Флора выпускает споры, а Кощей направляет на них магический поток.
        ФЛОРА: Водоросли показывают, что реагент идет из вентиляционной шахты. #З
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Команда, вы великолепны! Мы нашли источник и природу реагента!#Р
        }
        ~ попытка = 1
        -> Библиотека_После_Обнаружения
    - else:
        Флора выпускает в воду споры водорослей. Они быстро прорастают, но начинают буйно разрастаться по всей библиотеке, опутывая свитки и еще быстрее уничтожая их.
        ФЛОРА:  Ой... Кажется, реагент подействовал как удобрение! Всё стало только хуже...#П
        ~ попытка = 1
        -> Этап1_Библиотека_Обнаружение
}

=== Кощей_библиотека_предложение ===
КОЩЕЙ: Проведу общий анализ магического вырождения. Определю, что за сила разъедает заклятья.#У
+ [Действуй!] -> Кощей_библиотека_действие
+ [Послушаем, что скажут остальные] -> Этап1_Библиотека_Обнаружение

=== Кощей_библиотека_действие ===
{
    - кот == true:
        // Комбинация Кощей + Кот
        Кощей проводит рукой, и вода в центральном зале начинает светиться.
        КОТ УЧЕНЫЙ: О, это же следы реагента «Чернильная смерть»! Я читал о нем! Он был изобретен гильдией «Громовые Шестерни» для уничтожения магических текстов!#З
        КОЩЕЙ: Точно. И следы ведут к вентиляционной шахте. #Р
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Идеально! Мы знаем реагент и виновника!#Р
        }
    - else:
        Кощей проводит рукой, и вода в центральном зале начинает светиться зловещим фиолетовым светом.
        КОЩЕЙ: Энергия распада исходит из вентиляционной шахты. Грубая техномагическая атака.
}
~ попытка = 1
-> Библиотека_После_Обнаружения

=== Иван_библиотека_предложение ===
ИВАН ЦАРЕВИЧ: Осмотрю стены на предмет трещин и пробоин. Если нарушена целостность — значит, зараза просочилась извне.#У
+ [Действуй!] -> Иван_библиотека_действие
+ [Послушаем, что скажут остальные] -> Этап1_Библиотека_Обнаружение

=== Иван_библиотека_действие ===
{
    - василиса == true:
        // Комбинация Иван + Василиса
        ВАСИЛИСА: Иван, хранитель говорит, что видел, как кто-то возился у вентиляционной решетки в восточном углу.#З
        ИВАН ЦАРЕВИЧ: Понял. Иду смотреть.
        Иван идет к указанному месту и находит следы взлома решетки и капли неизвестной жидкости.
        ИВАН ЦАРЕВИЧ: Здесь явно проникали. И есть капли реагента. Мы можем взять образец.#Р
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Образец реагента и следы взлома — отличные улики!#Р
        }
        ~ попытка = 1
        -> Библиотека_После_Обнаружения
    - else:
        Иван тщательно простукивает стены, но не находит повреждений.
        ИВАН ЦАРЕВИЧ: Кладка цела. Яд просочился как-то иначе... Мой метод это не обнаруживает.#П
        ~ попытка = 1
        -> Этап1_Библиотека_Обнаружение
}

=== Кот_библиотека_предложение ===
КОТ УЧЕНЫЙ: Проведу диагностику защитных печатей библиотеки. Они явно повреждены. Нужно найти, какое именно заклятье было нарушено первым.#З
+ [Действуй!] -> Кот_библиотека_действие
+ [Послушаем, что скажут остальные] -> Этап1_Библиотека_Обнаружение

=== Кот_библиотека_действие ===
        Кот лихорадочно листает парящий перед ним каталог печатей.
        КОТ УЧЕНЫЙ: Нашел! Печать Целостности Знаний нарушена! Источник реагента - вентиляционная шахта древней системы охлаждения!#З
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Отличная работа! Заношу улику в базу данных!#Р
        }
~ попытка = 1
-> Библиотека_После_Обнаружения

=== Василиса_библиотека_предложение ===
ВАСИЛИСА: Попробую поговорить с Хранителем библиотеки. Духи места всегда знают больше, чем кажется.#З
+ [Действуй!] -> Василиса_библиотека_действие
+ [Послушаем, что скажут остальные] -> Этап1_Библиотека_Обнаружение

=== Василиса_библиотека_действие ===
{
    - кот == true:
        // Комбинация Василиса + Кот
        ВАСИЛИСА: Хранитель говорит, что сюда приходил странный посеитель. Черноволосый невысокий человек средних лет. В больших очках, с растрепанными волосами... Он что-то влил в вентиляционную шахту!
        КОТ УЧЕНЫЙ: (шепчет) Спроси про его одежду, может,на ней был герб или знак.
        Василиса задает наводящие вопросы, и хранитель описывает эмблему на плаще: скрещенные молот и шестерня.
        КОТ УЧЕНЫЙ: Это же символ гильдии «Громовые Шестерни»! В летописях упоминается, что они разрабатывали реагенты для уничтожения магических текстов!#З
        {
            - попытка == 0:
                ~ улика = улика + 2
                ФОЛИАНТ ПО СВЯЗИ:  Потрясающе! Мы имеем прямое указание на гильдию и знаем их методы!#Р
        }
    - else:
        Василиса тихо беседует с духом Хранителем. Разговор продолжается довольно долго.
        ВАСИЛИСА: Хранитель рассказал мне о необычном посетителе. Черноволосый невысокий человек средних лет. В больших очках, с растрепанными волосами... Он что-то влил в вентиляционную шахту!
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ:  Описание злоумышленника - отличная зацепка!
        }
}
~ попытка = 1
-> Библиотека_После_Обнаружения

=== Библиотека_После_Обнаружения ===
ФОЛИАНТ ПО СВЯЗИ: Причина ясна! Теперь нужно остановить распад, пока не погибли все свитки!
-> Этап2_Библиотека_Решение

=== Этап2_Библиотека_Решение ===
ТЫ: Команда, предлагайте решение!

+ { дровосек == true} [Спросить Железного Дровосека] -> Дровосек_библиотека_предложение2
+ { дриада == true} [Спросить Флору] -> Флора_библиотека_предложение2
+ { кощей == true} [Спросить Кощея] -> Кощей_библиотека_предложение2
+ { иван == true} [Спросить Ивана Царевича] -> Иван_библиотека_предложение2
+ { кот == true} [Спросить Кота Ученого] -> Кот_библиотека_предложение2
+ { василиса == true} [Спросить Василису] -> Василиса_библиотека_предложение2

=== Дровосек_библиотека_предложение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Предлагаю изолировать зону герметичным куполом.. Это не восстановит поврежденное, но сохранит до 60% текстов.#У
+ [Действуй!] -> Дровосек_библиотека_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Библиотека_Решение

=== Дровосек_библиотека_нейтрализация ===
{
    - кот == true:
        // Комбинация Дровосек + Кот
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Создаю герметичный купол. Профессор, определите самые ценные свитки для срочного копирования.
        КОТ УЧЕНЫЙ: Уже делаю! Вот эти десять нужно спасти в первую очередь!#З
       Дровосек и Кот работают слаженно: купол останавливает распад, а Кот успевает скопировать самые ценные манускрипты.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Уцелело 80% текстов. Устройство-источник изолировано, для его изучения потребуется время.#Р
        КОТ УЧЕНЫЙ: Я сохранил копии самых ценных манускриптов! #Р
		 ~ улика = улика + 1
   			ФОЛИАНТ ПО СВЯЗИ: Принято!
           - else:
        Железный Дровосек создает силовое поле, изолирующее центральный зал. Распад останавливается.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Уцелело 65% текстов. Это даже выше предварительных расчетов. Источник изолирован в куполе, изучение потребует времени.
}
-> Библиотека_После_Нейтрализации

=== Флора_библиотека_предложение2 ===
ФЛОРА: Вода пытается очиститься сама, но у неё не хватает сил! Давайте поможем ей - я выращу био-фильтр из глубинного мха. Он впитает всю эту скверну, как губка!#У
+ [Действуй!] -> Флора_библиотека_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Библиотека_Решение

=== Флора_библиотека_нейтрализация ===
{
    - кощей == true:
        // Комбинация Флора + Кощей
       ФЛОРА: Я выращу био-фильтр, но боюсь, что он выйдет из-под контроля. #У
	КОЩЕЙ: Я поставлю магический барьер, чтобы ограничить его рост. Начинай. #З
	Флора выпускает споры под контролем Кощея. Мох впитывает реагент, не повреждая свитки.
	ФЛОРА: Получилось! Теперь реагент можно собрать для анализа. #Р
	КОЩЕЙ: Уверен, эта жалкаяя поделка - работа «Громовых Шестерён». #Р
        ~ улика = улика + 1
		ФОЛИАНТ ПО СВЯЗИ:  Отлично сработали! #Р
        -> Библиотека_После_Нейтрализации
    - дровосек == true:
        // Комбинация Флора + Дровосек
        ФЛОРА: Я выращу мох, который впитает реагент, но он может повредить свитки.#У
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Я создам микро-силовые поля вокруг каждого свитка, чтобы защитить их.#З
        Дровосек устанавливает защиту, а Флора выращивает мох. 
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Реагент собран. Анализ показывает состав, характерный для гильдии «Громовые Шестерни».
        ~ улика = улика + 1
		ФОЛИАНТ ПО СВЯЗИ: Отлично сработали! #Р
        -> Библиотека_После_Нейтрализации
    - else:
        // Только Флора - катастрофа
        Флора выпускает в воду споры магического мха. На мгновение кажется, что это сработало - но затем под влиянием реагента мох начинает расти с ужасающей скоростью. Нежные ростки за секунды превращаются в толстые слизистые побеги, которые обволакивают свитки мертвой хваткой.
#Location: Bibl_KATASTROFA
        Вода внезапно вспенивается и чернеет. Дух-хранитель издаёт последний беззвучный крик и тает, словно капля в грязной воде. Стены библиотеки покрываются мерзкой чёрной плесенью, пожирающей последние фрески. От веками хранимых знаний остаётся лишь мутная, зловонная жижа, в которой плавают обломки полок.
ФОЛИАНТ ПО СВЯЗИ:  (в панике) Что это, босс?!? Что происходит? #З
        ~ катастрофа = true
        -> Библиотека_Итоги
}

=== Кощей_библиотека_предложение2 ===
КОЩЕЙ: Я инвертирую формулу реагента и он выпадет в безвредный осадок. Мы и свитки спасём, и улики получим.#З
+ [Действуй!] -> Кощей_библиотека_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Библиотека_Решение

=== Кощей_библиотека_нейтрализация ===
{
    - кот == true:
        // Комбинация Кощей + Кот
        КОЩЕЙ: Я нейтрализую реагент, а ты, Кот, найди, как его разложить без остатка. #З
		КОТ УЧЕНЫЙ: Уже ищу! Вот: «Чернильная смерть» разлагается заклинанием обратного отсчёта, но тут нужна точная формула.
		КОЩЕЙ: Формула у меня. Начинаем.
		Кощей и Кот произносят совместное заклинание, и реагент разлагается без осадка.
		КОТ УЧЕНЫЙ: Я нашёл упоминание — этот реагент использовался «Громовыми Шестернями» в 1235 году для уничтожения библиотек соперников. #Р
        ~ улика = улика + 2
		ФОЛИАНТ ПО СВЯЗИ:  Вот что значит командная работа! #Р
    - else:
        Кощей произносит короткую, режущую слух формулу. Вода в центральном зале мутнеет, со дна поднимается облако бурого осадка, но распад текстов мгновенно прекращается.
        КОЩЕЙ: Готово. Реагент нейтрализован.
        ФОЛИАНТ ПО СВЯЗИ:  Отлично! Теперь у вас есть время чтобы тщательно собрать следы и улики!
        ~ улика = улика + 1
}
-> Библиотека_После_Нейтрализации

=== Иван_библиотека_предложение2 ===
ИВАН ЦАРЕВИЧ: Вижу сосуд с реагентом - источник заражения. Проберусь в шахту и помещу его в герметичный контейнер. Риск оправдан: остановим заразу и получим улику#З
+ [Действуй!] -> Иван_библиотека_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Библиотека_Решение

=== Иван_библиотека_нейтрализация ===
{
    - василиса == true:
        // Комбинация Иван + Василиса
        ИВАН ЦАРЕВИЧ: Я ныряю за сосудом.
        ВАСИЛИСА: Я попрошу хранителя помочь тебе, он укажет точное местоположение и защитит от остаточного реагента.#З
        Иван, с помощью хранителя, быстро находит сосуд и помещает в контейнер.
        ИВАН ЦАРЕВИЧ: Сосуд в целости. На нем есть гравировка: «Собственность гильдии «Громовые Шестерни».#Р
        ВАСИЛИСА: Хранитель говорит, что видел, кто оставил сосуд. Мы можем создать портрет злоумышленника.#Р
        ~ улика = улика + 2
		ФОЛИАНТ ПО СВЯЗИ:  Здорово, мы все ближе к цели!#Р
    - дровосек == true:
        // Комбинация Иван + Дровосек
        ИВАН ЦАРЕВИЧ: Я достану сосуд, а ты, Дровосек, подготовь контейнер.#З
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Контейнер готов. Проведу предварительный анализ на месте.#З
        Иван достает сосуд, Дровосек немедленно помещает его в контейнер и сканирует.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Сосуд изготовлен из сплава, патентованного гильдией «Громовые Шестерни». Реагент внутри соответствует их формуле.#Р
        ~ улика = улика + 2
		ФОЛИАНТ ПО СВЯЗИ:  Здорово, мы все ближе к цели!#Р
    - else:
        Иван, задержав дыхание, ныряет в эпицентр заражения. Ловкостью и силой он достает небольшой кристаллический стержень - сосуд с реагентом. Распад прекращается.
        ИВАН ЦАРЕВИЧ: Готово. Реагент в контейнере и готов к изучению.#Р
        ФОЛИАНТ ПО СВЯЗИ:  Здорово! Возвращайтесь скорее, мне не терпится провести химический анализ!
        ~ улика = улика + 1
}
-> Библиотека_После_Нейтрализации

=== Кот_библиотека_предложение2 ===
КОТ УЧЕНЫЙ: Применю заклинание мгновенного копирования! Успею сохранить десяток ценнейших текстов до их гибели. Знания будут спасены, пусть даже артефакты исчезнут.#У
+ [Действуй!] -> Кот_библиотека_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Библиотека_Решение

=== Кот_библиотека_нейтрализация ===
{
    - василиса == true && кощей == false:
        // Комбинация Кот + Василиса
        КОТ УЧЕНЫЙ: Я начну копирование, но мне нужно время.#П
        ВАСИЛИСА: Я попрошу духов библиотеки помочь тебе. Они могут замедлить распад на время.#З
        Василиса призывает духов, и они стабилизируют магическое поле вокруг свитков. 
        КОТ УЧЕНЫЙ: Удалось спасти больше половины текстов! #Р
    - кощей == true:
        // Комбинация Кот + Кощей
        КОТ УЧЕНЫЙ: Я копирую, но реагент разрушает слишком быстро.#П
			Кощей создаёт временной пузырь, и Кот успевает скопировать практически все манускрипты.
		КОТ УЧЕНЫЙ: Тексты спасены! И в одном из свитков нашёл формулу реагента и способ его нейтрализации. #Р
		ФОЛИАНТ ПО СВЯЗИ:  Отлично!
        ~ улика = улика + 1
    - else:
        Кот успевает создать магические копии важнейших манускриптов, прежде чем оригиналы бесследно растворяются.
		КОТ УЧЕНЫЙ: Знания сохранены, но артефакты утрачены навсегда... Удалось спасти около трети текстов.
}
-> Библиотека_После_Нейтрализации

=== Василиса_библиотека_предложение2 ===
ВАСИЛИСА: Мы теряем драгоценное время на споры! Давайте привлечём гномов для эвакуации свитков в сухие боксы, а альвов-писцов — для экстренного копирования. Это системный подход, который спасёт максимум знаний. #У
+ [Действуй!] -> Василиса_библиотека_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Библиотека_Решение

=== Василиса_библиотека_нейтрализация ===
{
    - дровосек == true:
        // Комбинация Василиса + Дровосек
        ВАСИЛИСА: Я организую рабочих, но нужно быстро перенести свитки.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Я соберу транспортные платформы и защитные коконы для каждого свитка.#З
        Рабочие, обеспеченные устройствами Дровосека, быстро переносят манускрипты. Удаётся спасти 80% архива.
    	ФОЛИАНТ ПО СВЯЗИ: Неплохая работа, команда!
        ~ улика = улика + 1
    - else:
        С присущим ей дипломатическим тактом Василиса начинает организовывать рабочих-гномов и альвов-писцов, терпеливо объясняя каждому их роль в спасении свитков. Но пока она улаживает споры, пока альвы церемонно распределяют обязанности, а гномы с дотошной неторопливостью проверяют герметичность боксов, магический реагент продолжает свою разрушительную работу.
        За эти долгие минуты десятки бесценных манускриптов успевают полностью раствориться в воде.
        ВАСИЛИСА: Наконец-то! Начинаем операцию по спасению!..
...........
		ВАСИЛИСА: У нас получилось спасти около половины манускриптов.
}
-> Библиотека_После_Нейтрализации

=== Библиотека_После_Нейтрализации ===
#Location: Bibl
Вода в библиотеке постепенно светлеет. Распад прекратился, и уцелевшие свитки медленно опускаются на свои полки. Дух-хранитель успокаивается, его полупрозрачная фигура становится чуть ярче.
+[Осмотреть остатки реагента] ->Osmotr3
+[Вернуться на базу] ->Библиотека_Итоги

===Osmotr3===
#Location: Osmotr3
ФОЛИАНТ ПО СВЯЗИ: Химический состав указывает на растворитель для магических чернил. Похоже, пытались что-то оцифровать, а не просто уничтожить.
-> Библиотека_Итоги

=== Библиотека_Итоги ===
#Location: Ofis
~ всего_улик = всего_улик + улика
ФОЛИАНТ: Передаю итоговый отчет.

{ катастрофа:
ФОЛИАНТ: Босс... это катастрофа. Столько знаний пропало! Боюсь, с нас еще спросят на Совете...#П
}

{ улика>=3:
ФОЛИАНТ: Фантастика! Мы не только остановили уничтожение знаний, но и добыли вещественные доказательства. Все указывает на гильдию "Громовые Шестерни" и Мастера Грома - главу гильдии!#Р
}

{ улика == 2 && катастрофа==false:
ФОЛИАНТ: Библиотека спасена, и это главное. Мы получили важный фрагмент головоломки, но полной картины все еще нет. Возможно, здесь замешан Мастер Гром, глава гильдии Громовых Шестерней
}

{ улика<=1 && катастрофа==false:
ФОЛИАНТ: Знания, к счастью, частично сохранены. Но материальные доказательства утеряны. Наш противник почти не оставляет следов.#У
}
{ кот == true && иван == true:
КОТ УЧЕНЫЙ: Если бы у меня было больше времени на изучение архивов, я бы уже распутал это дело! #П
ИВАН ЦАРЕВИЧ: Но времени-то у нас и нет! Иногда нужно действовать, а не бумажки перекладывать!#З
КОТ УЧЕНЫЙ: Вам этого не понять, молодой человек! #У
}

КОГНИТИВНЫЙ КРИСТАЛЛ: Найдено улик в этом деле: {улика}. Всего улик: {всего_улик}
~катастрофа=false

// Выбор_после_третьей_локации
ФОЛИАНТ: (хлопая ладонью по голографическому экрану) Босс, мы собрали {всего_улик} улик. По статье 7 Устава Аномального Следствия, этого уже достаточно для предъявления официального обвинения гильдии "Громовые Шестерни" и лично их гильдмастеру Грому.
ФОЛИАНТ: Но... улики пока что разрозненные. Мы знаем КТО, но не совсем понимаем ЗАЧЕМ. Их истинные цели могут быть глубже.

ТЫ:
+ [У нас достаточно улик. Пора предъявить обвинение!] -> Предъявить_обвинение_после_3
+ [Нужно продолжить расследование. Хочу понять их истинные цели!] -> Продолжить_после_3

=== Предъявить_обвинение_после_3 ===
ФОЛИАНТ: (кивает) Как скажете, босс. Давайте составим обвинительный акт и передадим в Большой Совет.
-> ФИНАЛ_СЦЕНА

===Продолжить_после_3===
~ пройдено=4
ФОЛИАНТ: Готовы к новым приключениям? На кухне Великана Грумбеля происходит что-то неладное. Туда и отправимся дальше!
ТЫ:
+ [Перебрасывай нас к Великану!] -> КУХНЯ_ВЕЛИКАНА
+ [Кухня Великана? Расскажи подробнее.] -> Инфо_Кухня_Великана

=== Инфо_Кухня_Великана ===
КОГНИТИВНЫЙ КРИСТАЛЛ: Великан Грумбель, знаменитый шеф-повар, известен своими кулинарными экспериментами. Автор "Монструозной Поваренной Книги" и сборника рецептов "Здоровое питание троллей". Его последний рецепт вызывает климатические аномалии.
-> КУХНЯ_ВЕЛИКАНА

=== КУХНЯ_ВЕЛИКАНА ===
#Location: Kuhnia_anomalia
Вы перенеслись в огромную, дымящуюся кухню в пещере великана по имени Грумбель. Горшки размером с дома кипят на кострах, с потолка свисают гирлянды из перцев чихач-дракон и луковиц плакса-великан. На столе валяется пергамент с рецептом, исписанный убористыми письменами.
Сам Грумбель, в заляпанном фартуке, бормочет себе под нос и с энтузиазмом помешивает в главном котле что-то ядовито-фиолетовое. Над пещерой бушует ад: бьют молнии, ветер сносит крыши, а с неба падает град размером с кулак. Источник бури - испарения от котла.

ФОЛИАНТ ПО СВЯЗИ:  Босс, Грумбель снова за своё! Будьте аккуратны. Сейчас он в прекрасном настроении, но характер у него взрывной!

~ улика = 0
~ попытка = 0
Перед тобой — твоя команда. Можно выслушать каждого или сразу назначить группу для совместной работы. Командная работа может принести больше улик... Если, конечно, группа подобрана правильно! 
// Начало первого этапа
-> Этап1_Кухня_Обнаружение

=== Этап1_Кухня_Обнаружение ===
ТЫ:
+ { иван == true} [Спросить Ивана Царевича] -> Иван_кухня_предложение
+ { дриада == true} [Спросить Флору] -> Флора_кухня_предложение
+ { кот == true} [Спросить Кота Ученого] -> Кот_кухня_предложение
+ { василиса == true} [Спросить Василису] -> Василиса_кухня_предложение
+ { дровосек == true} [Спросить Железного Дровосека] -> Дровосек_кухня_предложение
+ { кощей == true} [Спросить Кощея] -> Кощей_кухня_предложение

+{ иван == true && василиса == true } [Отправить Ивана и Василису работать вместе] -> Иван_Василиса_комбо_действие
+{ кощей == true && иван == true } [Отправить Кощея и Ивана работать вместе] -> Кощей_Иван_комбо_действие
+{ кощей == true && дриада == true } [Отправить Кощея и Флору работать вместе] -> Кощей_Флора_комбо_действие
+{ кощей == true && дровосек == true } [Отправить Кощея и Дровосека работать вместе] -> Дровосек_Кощей_кухня_комбо_обнаружение
+{ дровосек == true && дриада == true } [Отправить Дровосека и Флору работать вместе] -> Дровосек_Флора_комбо_действие
+{ кот == true && василиса == true } [Отправить Кота и Василису работать вместе] -> Кот_Василиса_комбо_действие
+{ василиса == true && дриада == true } [Отправить Флору и Василису работать вместе] -> Василиса_Флора_комбо_обнаружение
+{ кощей == true && василиса == true } [Отправить Кощея и Василису работать вместе] -> Василиса_Кощей_кухня_обнаружение
+{ иван == true && кот == true } [Отправить Кота и Ивана работать вместе] ->Иван_Кот_комбо_действие 
+{ дриада == true && кот == true } [Отправить Кота и Флору работать вместе] ->Флора_Кот_кухня_комбо_обнаружение

=== Василиса_Кощей_кухня_обнаружение ===
ВАСИЛИСА: (подходя с дружелюбной улыбкой) Грумбель, это пахнет… волшебно! Ты не подскажешь, откуда у тебя рецепт?#Р
КОЩЕЙ: Не трать время на светские беседы. Искомый ингредиент прямо на столе.#У
Он указывает пальцем на мешок с синими ягодами. От его жеста ягоды вспыхивают зловещим фиолетовым светом, выдавая магическую природу.
ГРУМБЕЛЬ: (насупливается) Эй, ты! Не порть товар своими фокусами! И не перебивай даму!#У
ВАСИЛИСА: Грумбель, не сердись!Мой друг восхищён редкостью твоих ягод. Их наверное сложно найти?
ГРУМБЕЛЬ: Маленький человечек дал… на пробу. А теперь не мешайте, у меня бульон убегает!#У
Кощей презрительно фыркает и отворачивается. Возможность расспросить великана упущена.
-> Кухня_После_Обнаружения

=== Дровосек_Кощей_кухня_комбо_обнаружение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Для анализа необходимы пробы пара, почвы и супа. Проведу химико-магическую спектрометрию на месте. #З
КОЩЕЙ: (с усмешкой) Ты собираешься измерять хаос? Позволь мне сделать это проще.#З
Кощей щёлкает пальцами. Над кипящим котлом возникает бледная, мерцающая сфера, втягивающая в себя клубы пара. Внутри проявляются искажённые символы.
КОЩЕЙ: Видишь? Грубая техномагическая примесь. Источник — те синие ягоды. Но кто их произвёл, не ясно.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Мой анализ подтверждает аномальный состав ягод
ГРУМБЕЛЬ: Эй! Что вы там колдуете? Не портите атмосферу! А то я вас половником выгоню!#У
ФОЛИАНТ ПО СВЯЗИ: Мы установили техногенную природу ягод. Великан раздражён, рекомендую вам отступить. 
{
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Угроза установлена, но великан теперь не даст образцов. Без вещественных доказательств все это только теория.
                }
~ попытка = 1
-> Кухня_После_Обнаружения

=== Флора_Кот_кухня_комбо_обнаружение ===
ФЛОРА: Котик, обрати внимание на те ягодки. С ними что-то не так... Они выросли в темноте, без солнца, без ласкового дождика... их корни ни разу не касались земли! #П
КОТ УЧЕНЫЙ: М-да... действительно, структура слишком правильная для природного плода... Посмотрю в "Каталоге запрещённой алхимии", раздел "Растения искусственные"... #У
Грумбель перестаёт мешать суп и смотрит на них с подозрением.
ГРУМБЕЛЬ: Эй, вы что там шепчетесь? Не трогайте мои ягодки! Они для супа!
ФЛОРА: Грумбель, они не настоящие! Они сделаны, чтобы причинять вред!#У
КОТ УЧЕНЫЙ: Нашёл! "Грозоягода синтетическая"... Боже мой, это же метеорологическое оружие, замаскированное под пищевой продукт!#З
Грумбель хмурится, его огромная рука накрывает мешок с ягодами.
ГРУМБЕЛЬ: Какое ещё оружие? Это просто ягодки для вкуса и гро-о-ома! Человечек сказал!#У
ФЛОРА: Он обманул тебя! Принюхайся — они даже пахнут ржавым металлом!#З
ГРУМБЕЛЬ: (раздражённо) Хватит! Вы со своими книжками и цветочками меня сбиваете! Я шеф! Я знаю, что делаю! Убирайтесь с моей кухни!#У
ФОЛИАНТ ПО СВЯЗИ: Вы определили угрозу, но рассердили великана. Он теперь ни за что не отдаст образцы и не признает ошибку. Без образцов ваши выводы — лишь теория.
{
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Вы определили угрозу, но напугали великана. Он теперь ни за что не отдаст образцы. Без вещественных доказательств наша улика — лишь теория.
                }
~ попытка = 1
-> Кухня_После_Обнаружения

=== Иван_кухня_предложение ===
ИВАН ЦАРЕВИЧ: Я прокрадусь, поищу следы чужаков. Прослежу, откуда Грумбель берет добавки и подслушаю, что он бормочет. #З
+ [Действуй!] -> Иван_кухня_действие
+ [Послушаем других] -> Этап1_Кухня_Обнаружение

=== Иван_кухня_действие ===
Иван, как тень, скользит между гигантскими кухонными принадлежностями, замирает у стола, где лежат мешки с ингредиентами.

ИВАН ЦАРЕВИЧ: (возвращаясь) Мешок с синими ягодами. На нём вышит знак — скрещенные молот и шестерня. Грумбель бормочет: "Спасибо тому маленькому человечку. И верно, ягодки грозности добавляют..."
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Превосходный результат! Мешок с клеймом "Громовые Шестерни" - явная улика!#Р
        }
~ попытка = 1
-> Кухня_После_Обнаружения

=== Флора_кухня_предложение ===
ФЛОРА: Я хочу посмотреть, какие травы Грумбель использует. Скорее всего одна из них чужеродная, не из здешних лесов.#З
+ [Действуй!] -> Флора_кухня_действие
+ [Послушаем других] -> Этап1_Кухня_Обнаружение

=== Флора_кухня_действие ===
        Флора закрывает глаза, вдыхая ароматы кухни.
        ФЛОРА: Вот он! Источник хаоса! Те синие ягоды... Такие у нас не растут! Их росли не в лесу, а в лаборатории!
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: След магической селекции на ягодах - отличная улика!#Р
        }
~ попытка = 1
-> Кухня_После_Обнаружения

=== Кот_кухня_предложение ===
КОТ УЧЕНЫЙ: Проведу исторический анализ рецептов великанов. Узнаю, использовали ли они раньше такие мощные ингредиенты.#У
+ [Действуй!] -> Кот_кухня_действие
+ [Послушаем других] -> Этап1_Кухня_Обнаружение

=== Кот_кухня_действие ===
        Кот листает парящие свитки, которые он  умудрился развернуть даже в этой дымящейся кухне.

        КОТ УЧЕНЫЙ: Так, м-да... в "Кулинарных хрониках" подобные рецепты не описаны. Это новшество. И весьма опасное. Похоже, дело вон в тех ягодах... Я нигде не нашел их описания...
        ~ попытка = 1
-> Кухня_После_Обнаружения

=== Василиса_кухня_предложение ===
ВАСИЛИСА: Я осторожно расспрошу Грумбеля о рецепте. Он доверчив, если подойти правильно. Главное — не спугнуть его и не задеть гордость шефа.#У
+ [Действуй!] -> Василиса_кухня_действие
+ [Послушаем других] -> Этап1_Кухня_Обнаружение

=== Василиса_кухня_действие ===
        Василиса подходит к великану с дружелюбной улыбкой.
        ВАСИЛИСА: Грумбель, это пахнет просто божественно! Кто тебе подсказал этот необычный рецепт?#Р
        ГРУМБЕЛЬ: Какой-то маленький человечек! Дал попробовать - ягодка как ягодка, а грому-то сколько! Я опять стану победителем в "Битве шефов"! Третий раз подряд! #Р
		ГРУМБЕЛЬ: А теперь отойди, не мешай. Или ты хочешь выведать мои секреты?#У
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ: Отлично, заношу данные в базу#Р
}
~ попытка = 1
-> Кухня_После_Обнаружения

=== Дровосек_кухня_предложение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Попробую провести спектральный анализ паров из котла. Вычислю химический состав.#У
+ [Действуй!] -> Дровосек_кухня_действие
+ [Послушаем других] -> Этап1_Кухня_Обнаружение

=== Дровосек_кухня_действие ===
        Железный Дровосек проводит анализ паров.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: В продукте питания выявлены аномальные соединения с магическим катализатором. Источник проблемы - синие ягоды, но их происхождение не установлено..
~ попытка = 1
-> Кухня_После_Обнаружения

=== Кощей_кухня_предложение ===
КОЩЕЙ: Просто просканирую варево диагностическим заклятьем. Это может показать нестабильный элемент.#У
+ [Действуй!] -> Кощей_кухня_действие
+ [Послушаем других] -> Этап1_Кухня_Обнаружение

=== Кощей_кухня_действие ===
        Кощей проводит быстрый магический анализ.
        КОЩЕЙ: Зелье перенасыщено магией бури. Источник - вон те синие ягоды. Но откуда они взялись мне неизвестно, я не разбираюсь в ботанике.
~ попытка = 1
-> Кухня_После_Обнаружения

// ========== КОМБИНАЦИОННЫЕ ДЕЙСТВИЯ (без предварительного опроса) ==========
=== Иван_Василиса_комбо_действие ===
Ты киваешь Ивану и Василисе. Они обмениваются понимающими взглядами и начинают действовать синхронно.
ВАСИЛИСА: (подходя с улыбкой) Грумбель, какой аромат! Ты — гений. Кто же подсказал тебе такой ингредиент?#Р
Пока Василиса отвлекает великана, Иван, как тень, скользит вдоль стола. Его глаза выхватывают деталь: помятый пергамент, торчащий из кармана фартука.
ГРУМБЕЛЬ: (бормочет, помешивая) А, человечек один... Маленький, в очках. Говорит, «попробуй, сила грома в каждой ягодке!» 
ИВАН ЦАРЕВИЧ: (шепотом, показывая бумажку) Босс, смотрите. На бумаге — печать. Молот и шестерня. И подпись: «Поставка №45-Г для эксперимента ГК».#З
ВАСИЛИСА: (продолжая мягко) А он не сказал, как его зовут, этого человечка?#Р
ГРУМБЕЛЬ: Не-а... Но бумажку оставил! Где же она... Куда-то задевалась...
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ:  Блестяще! Дипломатия плюс ловкость рук. Теперь у нас материальная улика с печатью!#Р
}

~ попытка = 1
-> Кухня_После_Обнаружения

=== Дровосек_Кот_комбо_действие ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Спектральный анализ обнаружил концентрацию ионизированных магических частиц возле мешка с синими ягодами.#З
КОТ УЧЕНЫЙ: (лихорадочно листая свиток) Погодите! Я видел подобные показатели в «Трактате о Запрещённых Алхимических Усилителях» Третьей эпохи! Это искусственный гибрид «Грозовника» и «Снежной ягоды» для управления погодой!#З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Сопоставляю данные. Соответствует заводским методикам гильдии «Громовые Шестерни». Вероятность совпадения: 96.3%. Обнаружены микрочипы для контроля магического выброса.
{
    - попытка == 0:
    ~ улика = улика + 1
    ФОЛИАНТ ПО СВЯЗИ: Идеально! Неоспоримая улика! Теперь мы знаем и состав, и производителя!#Р
}
~ попытка = 1
-> Кухня_После_Обнаружения

=== Кощей_Флора_комбо_действие ===
Кощей проводит рукой  в воздухе. Его пальцы оставляют в воздухе мерцающий тёмно-фиолетовый след.
КОЩЕЙ: Дриада, обрати внимание на те синие ягоды. Мне кажется, это не природная магия. Чувствуется металлический привкус... 
ФЛОРА: (протягивая ладонь к ягодам) Ой! И правда! Они... они неживые! В них нет солнечного света! Их вырастили в железных чанах, а не в земле!#З 
КОЩЕЙ: Точно. Это лабораторный продукт. И судя по резонансу магии тьмы... кто-то очень хотел, чтобы они вызывали именно хаос. 
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ:Теперь мы знаем метод изготовления ягод — это важная улика!#Р
}

~ попытка = 1
-> Кухня_После_Обнаружения

=== Кот_Василиса_комбо_действие ===
Василиса и Кот Ученый быстро договариваются о чем-то. Кот торжественно разворачивает древний свиток, а Василиса с улыбкой приближается к Грумбелю.
ВАСИЛИСА: Грумбель, дорогой, Совет Мудрецов высоко ценит твоё искусство!.. #Р
ГРУМБЕЛЬ: (надуваясь от гордости) Ну да... мне даже грамоту давали! Висит там, на стене!
КОТ УЧЕНЫЙ: (важно) Согласно Статье 12 Устава «О Технике Безопасности в великанской кулинарной традиции», любая попытка использования запрещенных ингредиентов карается лишением права на участие в "Битве шефов" на 10 лет!
ГРУМБЕЛЬ: Ого! Неужели на 10 лет?!?#П
ВАСИЛИСА: Ты не виноват, Грумбель. Скажи, кто-то подсунул тебе этот рецепт, верно? Кто-то хотел опозорить твоё имя!
Грумбель хмурится, его огромное лицо выражает сначала недоумение, потом обиду, а потом — растущее негодование.
ГРУМБЕЛЬ: Значит... Это... противозаконно? Меня... обманули?#П
Василиса кивает с сочувствием. Великан начинает рыться в ящике и достаёт помятую визитку.
ГРУМБЕЛЬ: Вот, он её оставил... «Лаборатория Громовых Шестерён. Инновации в магии и кулинарии. Мастер Гром».#П
{
    - попытка == 0:
    ~ улика = улика + 3
    ФОЛИАНТ ПО СВЯЗИ: Вот это удача! Визитка с именем — прямое доказательство и указание на главу гильдии!#Р
}
~ попытка = 1
-> Кухня_После_Обнаружения

=== Кощей_Иван_комбо_действие ===
Ты отправляешь Ивана и Кощея работать вместе. Иван начинает методично осматривать помещение, а Кощей приступает к магическому сканированию.
ИВАН ЦАРЕВИЧ: (шепчет) Я начну с осмотра пола и стен. Если здесь был кто-то посторонний, остались следы.#У
КОЩЕЙ: (пренебрежительно) Следы? Зачем нам пыль и грязь, когда можно увидеть энергетический отпечаток? Дай место.#У
Иван пытается рассмотреть что-то на полу, но магическое свечение Кощея создаёт блики и тени, искажая видимость. Кощей не может сконцентрироваться — движения Ивана создаёт помехи в энергетическом поле.
ИВАН ЦАРЕВИЧ: (раздражённо) Если бы ты не светил этой фиолетовой аурой, я бы уже нашёл улики!
КОЩЕЙ: (холодно) Если бы ты не топтался, как слон, я бы уже прочитал энергетический след доставщика.
ГРУМБЕЛЬ: (оборачивается, услышав спор) Чего шумите? Мешаете творческому процессу! Вас конкуренты прислали, чтобы рецепт выведать? А ну прочь отсюда, а то половником запущу!#У
ФОЛИАНТ ПО СВЯЗИ: Похоже, ничего выяснить не удалось...#У
~ попытка = 1
-> Этап1_Кухня_Обнаружение

=== Дровосек_Флора_комбо_действие ===
ФЛОРА: Дайте-ка я прикоснусь к стенам пещеры... Камень помнит всё. Он расскажет, что изменилось в последние дни.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Нелогично. Камень не обладает памятью. Я проведу анализ тепловых следов и акустических отпечатков.
Дровосек прикладывает металлическую ладонь к противоположной стене. Из неё исходят звуковые волны, которые отражаются и возвращаются обратно.
ФЛОРА: (отскакивая) Ай! Ты пугаешь духов камня! Они теперь затаились и не будут говорить со мной!#З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Духи камня — метафорическое понятие. Моя методика основана на физике. #З
ФЛОРА:Бездушная железяка! Ты всех распугиваешь своими вибрациями!#З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Мои вибрации находятся в безопасном диапазоне. Твое восприятие субъективно и ненадёжно.#З
ГРУМБЕЛЬ: (оборачивается, услышав спор) Чего шумите? Мешаете творческому процессу! Вас конкуренты прислали, чтобы рецепт выведать? А ну прочь отсюда, а то половником запущу!#У
ФОЛИАНТ ПО СВЯЗИ: Похоже, ничего выяснить не удалось...#У
~ попытка = 1
-> Этап1_Кухня_Обнаружение

=== Василиса_Флора_комбо_обнаружение ===
ВАСИЛИСА: Давайте подойдём системно. Флора, ты чувствуешь что-то необычное в атмосфере пещеры?#З
ФЛОРА: Да... Воздух тяжёлый, наполнен тревогой... что-то искусственное внесло дисбаланс.#П
Флора закрывает глаза, а Василиса начинает мягко расспрашивать Грумбеля, наблюдая за его реакцией.
ВАСИЛИСА: Грумбель, когда ты заметил, что погода стала реагировать на твой суп?
ФЛОРА: (не открывая глаз) Там, где он берёт специи есть холодное пятно.Это не природный холод...
Грумбель показывает на мешок с синими ягодами.
ГРУМБЕЛЬ: С тех пор, как начал эти ягодки добавлять. Человечек один дал, говорит: "Для остроты ощущений!"
ВАСИЛИСА: (осторожно) А этот человечек не говорил, откуда ягоды?
ГРУМБЕЛЬ: Не знаю. Он сказал, суп громкий будет. #П
ФЛОРА: Они не из нашего леса. Их растили без солнца, без земли... в металлическом ящике.
{
    - попытка == 0:
    ~ улика = улика + 3
   ФОЛИАНТ ПО СВЯЗИ:   Отлично! Командная работа дала результат!#Р
}
->Кухня_После_Обнаружения

===Иван_Кот_комбо_действие===
ИВАН ЦАРЕВИЧ: Я быстро осмотрю помещение. Мне не нужна помощь.#У
КОТ УЧЕНЫЙ: Нет, сначала нужно составить план исследования! Сверимся с классификациями аномалий!..#У
Иван начинает быстро и методично осматривать кухню, открывая крышки, заглядывая в углы. Кот пытается остановить его, размахивая свитками.
КОТ УЧЕНЫЙ: Погодите! Не трогайте ничего, мы не изучили возможные риски! В "Трактате об Осмотрительности" сказано...
ИВАН ЦАРЕВИЧ: Трактаты подождут. Я уже нашёл странное: вот эти ягоды светятся в темноте.#П
КОТ УЧЕНЫЙ: Светятся? Дайте-ка посмотреть в "Каталоге Светящихся Растений"...#У
Пока Кот листает свитки, Иван берёт одну ягоду, чтобы рассмотреть её ближе. 
КОТ УЧЕНЫЙ: (кричит) Не трогайте ничего руками, молодой человек! Что за варварские методы!
ГРУМБЕЛЬ:(замечает их) ЭЙ! КТО РАЗРЕШАЛ ТРОГАТЬ МОИ ИНГРЕДИЕНТЫ? ВОН ОТСЮДА! А ТО ПОЛОВНИКОМ ЗАПУЩУ!#У
Грумбель теперь настороже, спокойно исследовать кухню не получится. Приходится отступить.
ФОЛИАНТ ПО СВЯЗИ:  Да уж, босс! Один слишком быстро действует, другой слишком медленно думает. Вместо слаженной работы получился хаос.#П
~ попытка = 1
-> Этап1_Кухня_Обнаружение

=== Кухня_После_Обнаружения ===
Гроза над пещерой бушует по-прежнему. Грумбель, довольный своим творением, помешивает суп, напевая что-то себе под нос.
ФОЛИАНТ ПО СВЯЗИ: Нужно уговорить Грумбеля выкинуть синие ягоды. Он доверчивый, но его легко разозлить. И он не терпит лжи.
-> Этап2_Кухня_Решение

=== Этап2_Кухня_Решение ===
ТЫ: Как мы убедим великана, что его обманули?
+ { дровосек == true} [Спросить Железного Дровосека] -> Дровосек_кухня_предложение2
+ { дриада == true} [Спросить Флору] -> Флора_кухня_предложение2
+ { кощей == true} [Спросить Кощея] -> Кощей_кухня_предложение2
+ { иван == true} [Спросить Ивана Царевича] -> Иван_кухня_предложение2
+ { кот == true} [Спросить Кота Ученого] -> Кот_кухня_предложение2
+ { василиса == true} [Спросить Василису] -> Василиса_кухня_предложение2

+{ кощей == true && иван == true } [Отправить Кощея и Ивана работать вместе] -> Кощей_Иван_комбо_решение
+{ дровосек == true && кот == true } [Отправить Дровосека и Кота работать вместе] -> Дровосек_Кот_комбо_решение
+{ дровосек == true && дриада == true } [Отправить Дровосека и Флору работать вместе] -> Дровосек_Флора_комбо_решение
+{ кощей == true && дриада == true } [Отправить  Кощея и Флору работать вместе] -> Кощей_Флора_комбо_решение
+{ василиса == true && дриада == true } [Отправить Василису и Флору работать вместе] -> Флора_Василиса_кухня_решение
+{ кощей == true && дровосек == true } [Отправить Кощея и Дровосека работать вместе] -> Кощей_Дровосек_комбо_решение
+{ кощей == true && василиса == true } [Отправить Кощея и Василису работать вместе] -> Кощей_Василиса_комбо_решение
+{ дровосек == true && василиса== true } [Отправить Дровосека и Василису работать вместе] ->Дровосек_Василиса_кухня_комбо_решение
+{ иван == true && василиса== true } [Отправить Ивана и Василису работать вместе] ->Иван_Василиса_кухня_комбо_решение

  === Иван_Василиса_кухня_комбо_решение ===
ВАСИЛИСА: Грумбель, посмотри сюда. Иван принёс тебе кое-что снаружи.#З
Иван показывает сломанную ветку дуба толщиной в руку, и градины, размером с кулак. На плече у него сидит перепуганная белка, дрожащая от холода.
ИВАН ЦАРЕВИЧ: Это — с окраины леса. Твой «грозовой суп» ломает деревья и пугает животных. А через час будет ещё хуже.#З
ВАСИЛИСА: (мягко) Ты же не хотел никому вредить, правда? Ты хотел победить в "Битве шефов", но победить честно, своим мастерством.
Грумбель смотрит на сломанную ветку и дрожащую белку, и его лицо мрачнеет. 
ГРУМБЕЛЬ: Я не знал, что так получится. Я хотел, чтобы суп был самым громким! Чтобы все ахнули!#П
ВАСИЛИСА: Мы знаем. Поэтому мы хотим помочь тебе. Отдай нам эти ягоды и буря прекратится.
Великан тяжко вздыхает и протягивает мешок. Его рука дрожит от обиды.
ГРУМБЕЛЬ: Забирайте. И накажите обманщиков. Скажите... скажите, что Грумбель не игрушка в чужих руках.#П
~ улика = улика + 2
ФОЛИАНТ: Отличная командная работа!!#Р
-> Кухня_После_Нейтрализации

=== Дровосек_Василиса_кухня_комбо_решение ===
ВАСИЛИСА: Грумбель, посмотри буря усиливается. Нужно безопасно удалить ягоды из твоего котла. Доверься нам, пожалуйста.#З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Предлагаю двухэтапный протокол. Термальный шок для деактивации в котле, затем сбор частиц гравитационным полем. Риск повреждения котла: 0.5%.
ГРУМБЕЛЬ: Ладно уж... Только... котёл не трогайте. Он у меня с душой.
ВАСИЛИСА: Конечно. Мы хотим помочь, а не навредить.
Дровосек выполняет ювелирно точную операцию. Яркая вспышка в котле, затем тихий гул. Частицы пара из котла стягиваются в маленькую сферу над ладонью Дровосека.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Процесс завершён. Атмосферные показатели нормализуются. Образцы сохранены. В котле остался только оригинальный бульон.
ВАСИЛИСА: Спасибо, Грумбель, твой суп в порядке. И теперь он будет честным — а значит, вкусным.
Гроза за окном пещеры стихает так же быстро, как и началась.
ФОЛИАНТ ПО СВЯЗИ: Прекрасный пример командной работы! Сочетание технически точного исполнения и дипломатичного подхода.
~ улика = улика + 2 // Максимум за решение
-> Кухня_После_Нейтрализации


=== Флора_Василиса_кухня_решение ===
ВАСИЛИСА: Грумбель, у тебя же такой талант! Ты можешь создавать вкусы из настоящих, живых продуктов! Зачем тебе эти фальшивки?
ФЛОРА: Я знаю травы, которые дают настоящую "громкость"! Не грозу на небе, а взрыв вкуса во рту! Острый перец Громотравка, ароматный чеснок Браво... Они растут на солнечных склонах, а не в лабораториях!
ГРУМБЕЛЬ: Но... "Битва шефов" через два дня! Я не успею их вырастить!
ВАСИЛИСА: Мы поможем. Флора ускорит рост, а я договорюсь с жюри об отсрочке — объясню, что ты стал жертвой мошенничества.
ФЛОРА: (протягивает семена) Вот. Они ещё пахнут солнцем. Давай попробуем?
Грумбель смотрит на фальшивые синие ягоды, потом на семена в руке Флоры.
ГРУМБЕЛЬ: Ладно. Выбросите эти железные ягодки. И... спасибо. Что не дали мне опозориться
Он протягивает мешок с ягодами. Василиса аккуратно берёт его для анализа.
ФОЛИАНТ ПО СВЯЗИ: Идеально! Великан не только отказался от опасных ингредиентов, но и доверяет вам. И мы получили образцы ягод как улику!
~ улика = улика + 3 
-> Кухня_После_Нейтрализации

=== Флора_кухня_предложение2 ===
ФЛОРА: Давайте предложим ему цветы Шепчущего Ветра - они гармонично впишутся в рецепт и нейтрализуют побочные эффекты! Грумбель поймёт, что мы предлагаем не запрет, а улучшение!#З
+ [Действуй!] -> Флора_кухня_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Кухня_Решение

=== Флора_кухня_нейтрализация ===
        ФЛОРА:(протягивая букет серебристых цветов) Слушай, Грумбель, эти фальшивые ягодки, пахнут неаппетитно! Вот попробуй эти цветочки, они добавят летние ароматы! Представляешь как другие шефы удивятся?#Р
        Великан с обидой швыряет мешок с ягодами и радостно хватает цветы.
ГРУМБЕЛЬ: Ах, жулики! Фальшивку подсунули!  #П
Буря стихает.
        ФОЛИАНТ ПО СВЯЗИ:  Образец ягод как вещдок - отличная работа!#Р
        ~ улика = улика + 1
        -> Кухня_После_Нейтрализации

=== Василиса_кухня_предложение2 ===
ВАСИЛИСА: Он ведёт себя как обманутый ребёнок! Нужно не приказывать, а раскрыть ему правду. Я спокойно объясню, что кто-то использовал его для создания хаоса. это заденет его гордость, и он сам откажется от ягод.#З
+ [Действуй!] -> Василиса_кухня_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Кухня_Решение

=== Василиса_кухня_нейтрализация ===
        ВАСИЛИСА: (с искренним огорчением) Грумбель, дорогой, тот человечек над тобой просто смеялся. Он хотел, чтобы твой суп все ругали, а твою кухню - боялись!
        Лицо великана наливается кровью. 
ГРУМБЕЛЬ: ЧТООО?! Да я его... Я его! Так и знал! Это конкуренты с "Битвы шефов"! Не хотят, чтоб я победил! #У
Он хватает полный мешок ягод и швыряет его в стену. 
ФОЛИАНТ ПО СВЯЗИ:  Василиса, захвати немного ягод для анализа!#З
        ~ улика = улика + 1
-> Кухня_После_Нейтрализации

=== Кот_кухня_предложение2 ===
КОТ УЧЕНЫЙ: У меня есть старинный эльфийский кулинарный рецепт! Подарю ему этот свиток — уверен, он тут же увлечётся новым блюдом и забудет про грозовые ягоды.#У
+ [Действуй!] -> Кот_кухня_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Кухня_Решение

=== Кот_кухня_нейтрализация ===
        Кот с важным видом вручает великану пожелтевший свиток.
        КОТ УЧЕНЫЙ: Многоуважаемый шеф! Позвольте вручить вам рецепт Супа Спокойного Неба от самого повара Короля-Солнце!#Р
        ГРУМБЕЛЬ:(внимательно изучает свиток) О, теперь я знаю, как победить на "Битве шефов"!
        ГРУМБЕЛЬ: Эх, жаль, ягодки были неплохие, использую их для следующих рецептов.
        Он достает огромную стеклянную банку с этикеткой "Сомнительные ингредиенты" и аккуратно складывает туда ягоды.
        ФОЛИАНТ ПО СВЯЗИ:  Босс, великан запер улики в свою личную коллекцию. Юридически мы не можем их изъять без разрешения Совета...#У
-> Кухня_После_Нейтрализации

=== Иван_кухня_предложение2 ===
ИВАН ЦАРЕВИЧ: Хватит уговоров! Я вытащу его из пещеры и ткну носом в последствия - пусть сам увидит снесённые крыши и перепуганных соседей! Иногда нужно показать правду напрямую!#У
+ [Действуй!] -> Иван_кухня_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Кухня_Решение

=== Иван_кухня_нейтрализация ===
 ИВАН ЦАРЕВИЧ: Эй, приятель! Твой суп устраивает ураган и не дает спать соседям! Прекрати кидать эти ягоды!#З
        Великан хмурится, но, выглянув из пещеры и увидев последствия, смущенно бормочет: "Ладно, ты прав, пожалуй..."
        Грумбель берет мешок с ягодами и швыряет их в огонь. "Плохие, плохие ягодки, устроили беспорядок!.."
        ФОЛИАНТ ПО СВЯЗИ: Буря прекращена, но ягоды сожжены. Улик не осталось.#П
        -> Кухня_После_Нейтрализации

=== Дровосек_кухня_предложение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК:  Предлагаю представить расчёты и диаграммы атмосферного дисбаланса. Когда он увидит, что его блюдо нарушает законы физики на 347%, это вынудит его немедленно прекратить процесс.#У
+ [Действуй!] -> Дровосек_кухня_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Кухня_Решение

=== Дровосек_кухня_нейтрализация ===
        Железный Дровосек демонстрирует великану схемы и диаграммы, выводя в воздухе сложные формулы атмосферной турбулентности.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Вот видите, коэффициент температурной аномалии превышает допустимые нормы.
        Грумбель смотрит на мерцающие цифры с нарастающим недоумением. 
        ГРУМБЕЛЬ: Значит, эти ягодки - плохие? Но они такие красивые...Я не хочу делать беспорядок! Я хочу варить суп! #П
        Великан швыряет мешок с ягодами в огонь. Буря постепенно стихает.
ФОЛИАНТ ПО СВЯЗИ: Проблема решена, но ягоды сгорели. Улики потеряны.#У
-> Кухня_После_Нейтрализации

=== Кощей_кухня_предложение2 ===
КОЩЕЙ: Создам иллюзию, что его суп превращается в ядовитую жижу... Мгновенный шок - лучший учитель.#У
+ [Действуй!] -> Кощей_кухня_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Кухня_Решение

=== Кощей_кухня_нейтрализация ===
        // Только Кощей - катастрофа
        Кощей создает перед великаном иллюзию, что суп в котле превращается в зловещую зеленую жижу.
        КОЩЕЙ: Отравой балуешься, глупец?#У
        Великан в ужасе отскакивает от котла, а потом приходит в ярость. 
ГРУМБЕЛЬ: Это был мой лучший суп! Для "Битвы шефов"! Ты его сглазил!#З
Он пытается поймать Кощея и случайно опрокидывает котел.
#Location: Kuhnia_katastrofa
        Котел с оглушительным бульканьем взрывается, разбрызгивая ядовито-фиолетовую похлёбку. Над пещерой начинается настоящий ураган. Молнии бьют в землю, сметая деревья, а град размером с голову человека засыпает окрестности. Великан забивается в дальний угол своей разрушенной кухни, бормоча проклятия, а вы еле успеваете телепортироваться в офис.
        ~ катастрофа = true
        -> Кухня_Итоги
        
      === Дровосек_Кот_комбо_решение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Профессор, мне нужна точная температура распада магической связи в этих ягодах.
КОТ УЧЕНЫЙ: (листая свиток) Так-так... Вот! «Грозовник теряет активность при ровно 47 градусах по Цельсию в вакуумной среде! И есть антидот — пыльца лунного мха!#P
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Создаю вакуумную камеру с точным терморегулятором.
Ягоды тихо светятся, затем гаснут. Из камеры выходит лёгкий пар.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Процесс завершён. Магическая составляющая нейтрализована. Ягоды теперь безопасны и пригодны для анализа как вещественное доказательство.#Р
КОТ УЧЕНЫЙ: И я записал весь процесс! Это будет отличным материалом для обвинения! #Р
~ улика = улика + 1
ФОЛИАНТ ПО СВЯЗИ:   Вот это научный подход! И улики сохранены, и великан не пострадал!#Р
->Кухня_После_Нейтрализации 
  
 === Кощей_Флора_комбо_решение ===
КОЩЕЙ: Эти ягоды опасны. Их нужно не уничтожать, а лишить силы. Постепенно, чтобы не вызвать взрыв.
ФЛОРА: Давай я выращу «цветы-поглотители» из семян лунного лотоса. Они впитают яд, как губка.
Кощей кивает и проводит сложный жест руками, создавая над ягодами тёмную сферу, замедляющую время.
КОЩЕЙ: У тебя будет десять минут, дриада. Я стабилизирую магический фон.
Флора закрывает глаза, и из её рук вырастают серебристые цветы. Они обвивают ягоды, поглощая их магическое свечение. Цветы темнеют, а ягоды становятся бледно-голубыми.
ФЛОРА: Готово! Магия ягод теперь в цветах... они просто уснут до весны. А ягоды остались целыми.#Р
КОЩЕЙ: Они лишены магии и совершенно безвредны!#Р
~ улика = улика + 2
ФОЛИАНТ ПО СВЯЗИ: Не забудьте захватить пору ягодок для анализа!#Р
-> Кухня_После_Нейтрализации      
        
 === Кощей_Дровосек_комбо_решение ===
КОЩЕЙ: Лучший способ сохранить улику — в буквальном смысле остановить для нее время. Заморозить в момент наибольшей активности.#З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Согласен. Предлагаю криомагическую капсулу с последующим спектральным анализом без размораживания.#З
Кощей произносит короткое заклинание, и ягоды покрываются инеем. Вокруг них образуется мерцающая сфера замедленного времени.
КОЩЕЙ: Температура: абсолютный ноль в магическом эквиваленте. Теперь твоя очередь, железный человек.
Дровосек аккуратно помещает замороженные ягоды в прозрачный контейнер, который сразу же заполняется голубым светом. На стенках контейнера появляются голографические схемы.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Начинаю дистанционный анализ... Обнаружены магические метки-подписи. Они идентичны тем, что находили на устройствах «Громовых Шестерён». 
КОЩЕЙ: Тогда я знаю, как убрать негативный эффект. Ягоды станут безопасными!
~ улика = улика + 2
ФОЛИАНТ ПО СВЯЗИ:  Холодный расчёт и ледяная магия! Я восхищен!#Р
-> Кухня_После_Нейтрализации

===Кощей_Иван_комбо_решение===
КОЩЕЙ: (холодно) Зачем что-то выпытывать? Просто заберём ягоды и проанализируем. Быстро и эффективно.#У
ИВАН ЦАРЕВИЧ: Согласен. Иногда прямой путь — самый короткий. Я возьму образец. А ты отвлеки внимание великана.#У
КОЩЕЙ: Я тебе что, клоун? Не мешай, я перенесу ягоды с помощью телекинеза!#У
ИВАН ЦАРЕВИЧ: Не верю я твоей магии. Смотри, как надо.#У
Иван пытается незаметно взять несколько ягод из мешка, но в этот момент Кощей создаёт заклинание телекинеза. Мешок с ягодами приподнимается в воздух и... врезавшись в лоб Царевича с грохотом падает на пол.
ГРУМБЕЛЬ: (оборачивается) ЭЙ! ТЫ ЧТО, ЯГОДЫ ВОРУЕШЬ?!#З
Великан хватает гигантскую метлу и начинает размахивать ею, как мечом. Иван и Кощей едва успевают увернуться от сметающих ударов.
ИВАН ЦАРЕВИЧ: (отскакивая) Всё испортил... Великий чародей и волшебник!
КОЩЕЙ: (сквозь зубы, уклоняясь от очередного взмаха) В следующий раз работай тише, следопыт!
Пытаясь поймать непрошенных гостей, великан опрокидывает котел. 
#Location: Kuhnia_katastrofa
        Котел с оглушительным бульканьем взрывается, разбрызгивая ядовито-фиолетовую похлёбку. Над пещерой начинается настоящий ураган. Молнии бьют в землю, сметая деревья, а град размером с голову человека засыпает окрестности. Великан забивается в дальний угол своей разрушенной кухни, бормоча проклятия, а вы еле успеваете телепортироваться в офис.
        ~ катастрофа = true
        -> Кухня_Итоги

=== Дровосек_Флора_комбо_решение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Предлагаю термальную утилизацию. Высокая температура нейтрализует магические связи. 800 градусов по Цельсию будет достаточно.
ФЛОРА: Нет-нет! Нельзя жечь ягоды! Они живые, их нужно успокоить! Дайте мне поговорить с ними! #З
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Не вижу логической составляющей.
Дровосек направляет тепловой луч на мешок. Флора в панике пытается вырастить защитные лианы, чтобы закрыть ягоды.
ФЛОРА: Я не дам тебе их спалить! Они страдают!#З
ГРУМБЕЛЬ: (оборачиваясь) ЧТО ВЫ ДЕЛАЕТЕ В МОЕЙ КУХНЕ?!#З
Пытаясь поймать непрошенных гостей, великан опрокидывает котел. 
#Location: Kuhnia_katastrofa
     Котел с оглушительным бульканьем взрывается, разбрызгивая ядовито-фиолетовую похлёбку. Над пещерой начинается настоящий ураган. Молнии бьют в землю, сметая деревья, а град размером с голову человека засыпает окрестности. Великан забивается в дальний угол своей разрушенной кухни, бормоча проклятия, а вы еле успеваете телепортироваться в офис.
        ~ катастрофа = true
        -> Кухня_Итоги

=== Кощей_Василиса_комбо_решение ===
ВАСИЛИСА: Давай попробуем просто убедить великана?#З
КОЩЕЙ: Без наглядности ничего не выйдет. Надо показать ему последствия его действий#З
Василиса мягко подводит Грумбеля к центру пещеры, где Кощей создаёт магический экран
ВАСИЛИСА: Грумбель, посмотри, что происходит снаружи.#П
КОЩЕЙ: (произнося заклинание) Speculum veritatis...
На экране появляются образы: разрушенные дома, перепуганные животные, люди, прячущиеся от града.
ГРУМБЕЛЬ:  Это... из-за меня?#П
ВАСИЛИСА: Из-за того, кто тебя обманул. Ты хотел создать шедевр, а тебе подсунули оружие. #П
КОЩЕЙ: И это оружие имеет конкретного владельца. Смотри.#З
На экране появляются молот и шестерню, мелькающие в каплях дождя, в узорах облаков.
ГРУМБЕЛЬ: (сжимая кулаки) Они... использовали мою кухню. Моё искусство.#У
ВАСИЛИСА: Но ты можешь это остановить. Прямо сейчас.
Грумбель молча подходит к мешку с ягодами и протягивает его Василисе. 
~ улика = улика + 2
ФОЛИАНТ ПО СВЯЗИ:   Идеальный баланс! Я восхищен!#Р
-> Кухня_После_Нейтрализации


=== Кухня_После_Нейтрализации ===
#Location: Kuhnia
Буря прекращается. Грумбель, хоть и недовольный, возвращается к приготовлению нормального супа. Над пещерой снова светит солнце.
+[Осмотреть улики] ->Osmotr4
+[Вернуться на базу] ->Кухня_Итоги

===Osmotr4===
#Location: Osmotr4
ФОЛИАНТ ПО СВЯЗИ: Это расчётные данные для портативного генератора. Интересно, почему тестировали на кухне великана...
-> Кухня_Итоги

=== Кухня_Итоги ===
#Location: Ofis
~ всего_улик = всего_улик + улика
ФОЛИАНТ: Передаю итоговый отчет.

{ катастрофа:
ФОЛИАНТ: Босс... это полный провал. Грумбель в ярости разрушил свою кухню, а его ядовитый суп спровоцировал климатическую катастрофу местного масштаба. Окрестные деревни затоплены, мосты снесены. "Громовые Шестерни" превратили великана в орудие массового разрушения.#П
}

{ улика >=3:
ФОЛИАНТ: Великолепно! Мы не только остановили бурю, но и получили прямые вещественные доказательства против «Громовых Шестерней». Они создают хаос, манипулируют, подставляя других.#Р
}

{ улика == 2  && катастрофа==false:
ФОЛИАНТ: Буря прекращена. Мы установили связь с гильдией, но доказательств недостаточно для серьезного обвинения.
}

{ улика <=1  && катастрофа==false:
ФОЛИАНТ: Погода нормализовалась, но великан так и не понял, что им манипулировали. «Громовые Шестерни» остаются в тени.
}

КОГНИТИВНЫЙ КРИСТАЛЛ: Найдено улик в этом деле: {улика}. Всего улик: {всего_улик}
~катастрофа=false
ФОЛИАНТ: Четвертое дело закрыто. Теперь мы знаем, что наши противники действуют не только напрямую, но и через подставных лиц. Это новая, опасная тактика.

ФОЛИАНТ: Босс, ситуация становится яснее. Мы можем предъявить обвинение сейчас. Улик хватит, чтобы привлечь гильдию к ответственности за разрушения и манипуляции. Но... их главная цель, их финальный замысел — всё ещё в тумане. Ваше слово, босс. Заканчиваем или идём до конца?
+ [Хватит играть с ними! Улик достаточно — предъявляем обвинение!] -> Предъявить_обвинение_после_4
+ [Нет, продолжаем расследование. Хочу знать, что за игру они затеяли.] -> Продолжить_после_4

=== Предъявить_обвинение_после_4 ===
ФОЛИАНТ: Понял. Игра в кошки-мышки закончена. Давайте соберем воедино все улики, включая сегодняшние с кухни Грумбеля. Мастер Гром больше не сможет прятаться за подставными лицами.
-> ФИНАЛ_СЦЕНА

=== Продолжить_после_4 ===
~пройдено=5
ФОЛИАНТ: Тогда готовьтесь к следующему вызову. На главном рынке столицы что-то опять случилось!

+ [Перебрасывай нас на рынок!] -> РЫНОК_ШЕПОТОВ
+ [Что случилось на рынке?] -> Инфо_Рынок_Шепотов

=== Инфо_Рынок_Шепотов ===
КОГНИТИВНЫЙ КРИСТАЛЛ: На главной рыночной площади столицы творится что-то странное. Люди теряют голоса — не могут говорить громче шепота. Никаких видимых причин, только легкий звон и запах карамели в воздухе.

-> РЫНОК_ШЕПОТОВ

=== РЫНОК_ШЕПОТОВ ===
#Location: Market_anomalia

Вы на главной рыночной площади столицы. Еще вчера шумный и пестрый базар теперь напоминает призрачный город. Воздух, обычно дрожащий от криков торговцев, споров покупателей и детского смеха, теперь неподвижен и тяжел.
Люди озираются, бросают тревожные взгляды по сторонам, словно ищут кого-то. Никаких щелчков механизмов или вспышек магии — лишь лёгкий, едва уловимый звон невидимых колокольчиков. В воздухе явственно ощущается знакомый, но неуместный сейчас запах леденцовой карамели.

ФОЛИАНТ ПО СВЯЗИ: (голос звучит приглушенно, будто и он боится нарушить тишину) Команда, вы на месте. Это... жутковато. Люди не оглохли, не онемели. Они просто разучились говорить громко.

~ улика = 0
~ попытка = 0

// Начало первого этапа
-> Этап1_Рынок_Обнаружение
=== Этап1_Рынок_Обнаружение ===
ТЫ: С чего начнем?

+ { дровосек == true} [Спросить Железного Дровосека] -> Дровосек_рынок_предложение
+ { дриада == true} [Спросить Флору] -> Флора_рынок_предложение
+ { кощей == true} [Спросить Кощея] -> Кощей_рынок_предложение
+ { иван == true} [Спросить Ивана Царевича] -> Иван_рынок_предложение
+ { кот == true} [Спросить Кота Ученого] -> Кот_рынок_предложение
+ { василиса == true} [Спросить Василису] -> Василиса_рынок_предложение

+{ дровосек == true && кот == true } [Отправить Дровосека и Кота работать вместе] -> Дровосек_Кот_комбо_обнаружение2
//+{ кощей == true && дриада == true } [Отправить Кощея и Флору работать вместе] -> Кощей_Флора_комбо_обнаружение2
+{ василиса == true && иван == true } [Отправить Василису и Ивана работать вместе] -> Иван_Василиса_комбо_обнаружение2
+{ кощей == true && иван == true } [Отправить Кощея и Ивана работать вместе] -> Кощей_Иван_комбо_обнаружение2
+{ иван == true && дриада == true } [Отправить Ивана и Флору работать вместе] -> Флора_Иван_комбо_обнаружение2
+{ кощей == true && дровосек == true } [Отправить Кощея и Дровосека работать вместе] -> Кощей_Дровосек_комбо_обнаружение2
+{ кощей == true && кот == true } [Отправить Кощея и Кота работать вместе] -> Кот_Кощей_обнаружение2
+{ кот == true && василиса == true } [Отправить Кота и Василису работать вместе] -> Кот_Василиса_обнаружение2
+{ дровосек == true && дриада == true } [Отправить Дровосека и Флору работать вместе] -> Дровосек_Флора_обнаружение2
+{ кот == true && иван == true } [Отправить Кота и Ивана работать вместе] -> Иван_Кот_рынок_обнаружение2

=== Иван_Кот_рынок_обнаружение2 ===
ИВАН ЦАРЕВИЧ: (прищурившись) Торговец в синем плаще кажется мне подозрительным. У него глаза бегают. И правую руку в кармане держит. Там что-то есть. Я подойду под видом покупателя, посмотрю реакцию.#З
КОТ УЧЕНЫЙ: (заметно нервничая) Это преждевременно! Сначала нужно проанализировать данные! Я нашёл упоминание о «Камне шепчущих обетов» — для его активации нужны особые условия! Надо проверить фазу луны..#У
ИВАН ЦАРЕВИЧ: Фазу луны он в кармане не прячет. Я действую.
Иван принимая вид простого прохожего, неспешно направляется к лотку. Его движения естественны, взгляд рассеян. Он почти у цели.
КОТ УЧЕНЫЙ: (кричит вслед) Стойте! А вдруг это не «Камень шепчущих обетов», а «Амулет молчаливого согласия»? Для первого нужен святой источник, а для второго — зола драконьего зуба! Совершенно разные ритуалы!
Резкий крик заставляет всех вздрогнуть. Торговец в синем плаще оглядывается на шум, резко хватает сумку и, не раздумывая, ныряет в толпу, используя возникшую суматоху.
ИВАН ЦАРЕВИЧ: Поздравляю. Ты его спугнул. Я даже не рассмотрел, что у него в руке.#П
ФОЛИАНТ ПО СВЯЗИ: Подозреваемый скрылся, улик нет. Надо начинать сначала.#П
~ попытка = 1
~ улика = 0
-> Этап1_Рынок_Обнаружение

=== Дровосек_Флора_обнаружение2 ===

ЖЕЛЕЗНЫЙ ДРОВОСЕК: Для локализации источника аномального акустического поля нужна сеть из 12 датчиков по периметру площади. Они зафиксируют малейшие вибрации, и мы построим точную карту распространения звукоподавляющих волн.#З
ФЛОРА: Как практично!... А я с помощью растений закреплю их! #Р
ЖЕЛЕЗНЫЙ ДРОВОСЕК: В этом нет необходимости.#У
ФЛОРА: Но я же хочу помочь!#Р
Дровосек расставляет датчики. Флора, следуя за ним, шепчет заклинание роста растений. Из-под датчиков мгновенно пробивается густая трава и мелкие цветы, которые, разрастаясь, приподнимают и перекашивают точные приборы.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: (раздражённо) Прекратите! Биомасса изменяет уровень и угол наклона датчиков на 3-17 миллиметров! Показания становятся бесполезными! Величина погрешности зашкаливает!
На экране Дровосека вместо чёткой карты — хаотичные помехи. 
~ попытка = 1
-> Этап1_Рынок_Обнаружение


=== Кощей_Иван_комбо_обнаружение2 ===

ИВАН ЦАРЕВИЧ: Слушай, Кощей. Ты же маг. Сделай какое-нибудь яркое представление на площади. Фейерверк, иллюзию, что угодно. Пока все смотрят на тебя, я незаметно прощупаю, у кого в карманах или под прилавком есть неизвестный артефакт.#З
КОЩЕЙ: Развлекать толпу? Я вам что, трубадур?.. Ладно, попробую...#У
Кощей встаёт в центре площади и щёлкает пальцами. Из воздуха рождается огненная птица, которая с резким криком взмывает в небо и рассыпается на сотни искр. Люди, и без того напуганные, взвизгивают от ужаса. 
ИВАН ЦАРЕВИЧ:(пытается пробиться сквозь мечущуюся толпу) Чёрт! Все носятся как угорелые! Я не могу ни к кому подойти незаметно!#П
КОЩЕЙ: (пожимает плечами) Ну я же вам не клоун...#У
~ попытка = 1
-> Этап1_Рынок_Обнаружение

=== Флора_Иван_комбо_обнаружение2 ===
ИВАН ЦАРЕВИЧ: У меня отличная идея. Используем городских голубей, на них никто не обращает внимания. Прикрепим миниатюрный видеокристалл к птице, пусть она облетит всю площадь. Так мы незаметно все обследуем.#З
ФЛОРА: (восторженно) О, какая прекрасная мысль! Но голуби... они такие серые и грустные. Давай сделаем это красиво! Я создам не просто пернатого свидетеля, а настоящее чудо!#Р
Прежде чем Иван успевает возразить, Флора подзывает маленького воробья, сидящего на карнизе. Она дует на него, осыпая блестящей пыльцой. Воробей начинает расти, его перья переливаются, и через мгновение в воздух взмывает огромная, сияющая неземным светом Жар-Птица.
ИВАН ЦАРЕВИЧ: Флора... Мы же должны были наблюдать скрытно! #П
ФЛОРА: Но... птичка же такая красивая! Я хотела порадовать людей, им так страшно... И воробей счастлив...#П
ФОЛИАНТ ПО СВЯЗИ: Босс, у нас есть запись того, как полсотни человек пялятся на Жар-Птицу. Красиво, но бесполезно. #П
~ попытка = 1
-> Этап1_Рынок_Обнаружение

=== Кот_Василиса_обнаружение2 ===
 // Комбинация Василиса + Кот
        ВАСИЛИСА: Я расспрошу людей, а ты, Котик, ищи в архивах подтверждение их слов.
        КОТ УЧЕНЫЙ: Хорошо. Записывай детали.
        Василиса беседует с людьми, Кот сверяется с текстами.
        ВАСИЛИСА: Все говорят о "сделке" — торговец предлагал удачу в обмен на голос. Люди думали, что это шутка и соглашались. Он давал им леденец и что-то шептал.
        КОТ УЧЕНЫЙ: Да, это классическая схема Камня Тихой Сделки! В летописях сказано, что артефакт нужно активировать, шепча определенные слова!#З
        ВАСИЛИСА: Этот торговец явно не сам придумал эту схему. Кто-то его научил.#З
        КОТ УЧЕНЫЙ: Судя по всему это работа «Громовых Шестерней». Они усовершенствовали древнюю технологию.
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ: Отличная совместная работа#Р
}
~ попытка = 1
-> Рынок_После_Обнаружения

=== Кот_Кощей_обнаружение2 ===
        // Комбинация Кот + Кощей
        КОТ УЧЕНЫЙ: Я подниму архивы, а Кощей пусть определит источник магии.
        КОЩЕЙ: Договорились. Сравним данные.
        Кот зарывается в свитки, а Кощей анализирует магическое поле.
        КОТ УЧЕНЫЙ: Нашел! Камень Тихой Сделки — артефакт, который заключает негласный договор: жертва соглашается отдать мошеннику голос в обмен на мимолетную удачу! Они думали, что это шутка и соглашались. Мошенник давал им леденец и что-то шептал.#З
        КОЩЕЙ: Это энергетический вампиризм. Но не живой. Артефакт. Судя по всему, это вон тот торговец сладостями.#Р
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ: Отличная совместная работа#Р
}
~ попытка = 1
-> Рынок_После_Обнаружения

==Иван_Василиса_комбо_обнаружение2 ===
        ИВАН ЦАРЕВИЧ: Я прослежу за перемещениями людей, а ты, Василиса, поговори с жертвами, узнай детали.#З
        ВАСИЛИСА: Хорошо, я расспрошу тех, кто еще может шептать.#З
        Иван наблюдает за движением толпы, а Василиса беседует с пострадавшими.
        ИВАН ЦАРЕВИЧ: Все следы ведут к тому торговцу в синем плаще. Смотрите — у него в руках что-то тяжелое, вроде камень.
        ВАСИЛИСА: Люди говорят, что он предлагал им "удачу" в обмен на голос. Они думали, что это шутка и брали угощение. Каждый раз он что-то шептал, когда брал плату.
        ИВАН ЦАРЕВИЧ: Видишь, как он нервничает? Судорожно сжимает камень. Это точно он.#З
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ: Отлично, мы нашли злоумышленника.#Р
}
~ попытка = 1
-> Рынок_После_Обнаружения

=== Кощей_Дровосек_комбо_обнаружение2 ===
КОЩЕЙ: Я буду искать магический след, а ты, Железный, фиксировать параметры.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Принято. Могу создать  детектор для записи магических частот.
КОЩЕЙ: Действуй, Железный#Р
Кощей проводит руками, и в воздухе появляются едва видимые светящиеся полупрозрачные нити. Они поднимаются от каждого безмолвного человека и тянутся к одному из торговцев у лотка со сладостями. Дровосек наводит устройство на этот энергетический узел.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Наблюдаю структурированную систему. Каждый голос - отдельный канал с уникальным ID.#З
КОЩЕЙ: Все ясно. Энергетический вампир... но не живой. Чувствуется след работы артефакта.#Р
 ЖЕЛЕЗНЫЙ ДРОВОСЕК: Подтверждаю. Работает мощный артефакт. Содержит микрочипы и усилители. Источник - торговец сладостями.#Р
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ: Отлично, мы получили новые данные!#Р
}
~ попытка = 1
-> Рынок_После_Обнаружения

=== Кощей_Флора_комбо_обнаружение2 ===
КОЩЕЙ: Дриада, попробуй почувствовать искажения в природной магии. А я просканирую техномагический след.#З
ФЛОРА: Хорошо... Дай мне сосредоточиться...
Флора закрывает глаза, Кощей проводит руками в воздухе.
        ФЛОРА: Ой! Я чувствую холодное пятно. Там, где стоит вон тот торговец. Вокруг него пустота — даже насекомые облетают стороной!#П
        КОЩЕЙ: Энергетический вампиризм. Но не живой. Артефакт. И этот артефакт явно техномагического происхождения. 
ФЛОРА: Точно, торговец что-то сжимает в руках. Похожее на камень.#З
{
    - попытка == 0:
    ~ улика = улика + 1
    ФОЛИАНТ ПО СВЯЗИ: Отличная совместная работа, команда!#Р
}
~ попытка = 1
-> Рынок_После_Обнаружения

=== Дровосек_Кот_комбо_обнаружение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Начинаю сканирование акустических аномалий. Профессор, сопоставьте с известными вам случаями.
КОТ УЧЕНЫЙ: Уже ищу! В "Анналах акустической магии" есть описание подобного..."Безмолвный резонанс", вызываемый артефактом, который поглощает звуковые волны определенной частоты!#У
Дровосек сканирует, Кот листает свитки.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Обнаружено: устройство генерирует поле на частоте 17000 Гц, блокирующее голосовые связки. 
КОТ УЧЕНЫЙ: Артефакт - предмет в руках вон того торговца сладостями.
{
    - попытка == 0:
    ~ улика = улика + 2
    ФОЛИАНТ ПО СВЯЗИ: Мы знаем и принцип работы, и модификацию!#Р
}
~ попытка = 1
-> Рынок_После_Обнаружения

=== Дровосек_рынок_предложение ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Попытаюсь провести акустический анализ среды. Возможно, здесь действует подавляющий резонанс, который физически глушит голосовые связки.#У
+ [Действуй!] -> Дровосек_рынок_действие
+ [Послушаем других] -> Этап1_Рынок_Обнаружение

=== Дровосек_рынок_действие ===
        Железный Дровосек направляет сенсоры на толпу.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Акустический анализ не выявляет аномалий. Голосовые связки людей физически здоровы. Мои методы здесь неприменимы.#П
        ~ попытка = 1
        -> Этап1_Рынок_Обнаружение

=== Флора_рынок_предложение ===
ФЛОРА: Я попробую поговорить с местными растениями... Может, пыль на листьях или дрожь корней подскажет причину неестественной тишины.#У
+ [Действуй!] -> Флора_рынок_действие
+ [Послушаем других] -> Этап1_Рынок_Обнаружение

=== Флора_рынок_действие ===
        Флора прикладывает ладони к стволу старого дерева на краю рынка и закрывает глаза.
        ФЛОРА: Они... они не понимают. Для деревьев тишина - это мир. Они чувствуют страх людей, но не понимают его.#П
        ~ попытка = 1
        -> Этап1_Рынок_Обнаружение

=== Кощей_рынок_предложение ===
КОЩЕЙ: Я могу провести диагностику на предмет вампирических чар. Если что-то высасывает силу, я это почую.#У
+ [Действуй!] -> Кощей_рынок_действие
+ [Послушаем других] -> Этап1_Рынок_Обнаружение

=== Кощей_рынок_действие ===
        Кощей проводит рукой по воздуху, и его пальцы на мгновение оставляют в пространстве бледный светящийся след.
        КОЩЕЙ: Похоже это действительно энергетический вампир... но не живой. Чувствуется след работы артефакта. Следы ведут вон к тому торговцу сладостями. Но у нас нет прямых доказательств.
~ попытка = 1
-> Рынок_После_Обнаружения

=== Иван_рынок_предложение ===
ИВАН ЦАРЕВИЧ: Уверен, это дело рук вора, а вор оставляет следы. Я выясню, к кому подходили люди до того, как лишились голоса. Злоумышленник должен быть среди них.#З
+ [Действуй!] -> Иван_рынок_действие
+ [Послушаем других] -> Этап1_Рынок_Обнаружение

=== Иван_рынок_действие ===
        Иван, как тень, скользит между притихшими людьми, его глаза выхватывают детали. Через несколько минут он возвращается.
        ИВАН ЦАРЕВИЧ: Вон там. Торговец сладостями в синем плаще. Все, с кем я говорил, покупали у него леденцы. И каждый раз он что-то шептал им на ухо, когда брал плату. Смотрите - та женщина только что купила сладости, а теперь она схватилась за горло.#З
        {
            - попытка == 0:
                ~ улика = улика + 1
                ФОЛИАНТ ПО СВЯЗИ: Установленный торговец-посредник - явная улика! Я заношу в базу.
        }
~ попытка = 1
-> Рынок_После_Обнаружения

=== Кот_рынок_предложение ===
КОТ УЧЕНЫЙ: Кража голоса - магия не новая! Я поищу в архивах аналогичные случаи. Если мы поймем, как это делалось в прошлом, мы найдем и нынешний инструмент!#З
+ [Действуй!] -> Кот_рынок_действие
+ [Послушаем других] -> Этап1_Рынок_Обнаружение

=== Кот_рынок_действие ===
        Кот зарывается носом в парящий свиток, бормоча себе под нос.
        КОТ УЧЕНЫЙ: Так-так-так, Камень Тихой Сделки... описан в Манускриптах Магического Шантажа эпохи Третьей Империи! Артефакт заключает негласный договор - жертва соглашается отдать голос в обмен на мимолетную удачу или исполнение мелкого желания! Все звуки должны быть заключены в артефакт. С виду он выглядит как обычный камень. Только холодный.
        ФОЛИАНТ ПО СВЯЗИ: Тогда обратите внимание вон того торговца! У него в руках, что-то тяжелое.
        {
            - попытка == 0:
                ~ улика = улика + 1
        }
~ попытка = 1
-> Рынок_После_Обнаружения

=== Василиса_рынок_предложение ===
ВАСИЛИСА: Страх - плохой советчик, но хороший свидетель. Я поговорю с людьми, попробую найти общее в их историях. Может, все они ели одно и то же, или трогали один и тот же предмет...#У
+ [Действуй!] -> Василиса_рынок_действие
+ [Послушаем других] -> Этап1_Рынок_Обнаружение

=== Василиса_рынок_действие ===
        Василиса подходит к группе перешептывающихся людей. После нескольких минут тихого разговора она возвращается.
        ВАСИЛИСА: Некий торговец предлагал людям: "Хочешь, чтобы тебе сопутствовала удача? Съешь мое пирожное". "Возьми мою конфетку, она принесет тебе счастье". "А ты взамен отдашь мне свой голос". Люди думали, что это шутка и соглашались. Возможно, это вон тот торговец сладостями. Он держится подозрительно и явно нервничает.
~ попытка = 1
-> Рынок_После_Обнаружения

=== Рынок_После_Обнаружения ===
ФОЛИАНТ ПО СВЯЗИ: Значит, люди, сами того не понимая, заключали какую-то сделку. Мошенник - торговец сладостями с камнем в руке. Наша задача - вернуть людям их голоса. И постарайтесь сохранить артефакт - он нам нужен как доказательство.
-> Этап2_Рынок_Решение

=== Этап2_Рынок_Решение ===
ТЫ: Нужно аннулировать сделку. Как поступим?

+ { дровосек == true} [Спросить Железного Дровосека] -> Дровосек_рынок_предложение2
+ { дриада == true} [Спросить Флору] -> Флора_рынок_предложение2
+ { кощей == true} [Спросить Кощея] -> Кощей_рынок_предложение2
+ { иван == true} [Спросить Ивана Царевича] -> Иван_рынок_предложение2
+ { кот == true} [Спросить Кота Ученого] -> Кот_рынок_предложение2
+ { василиса == true} [Спросить Василису] -> Василиса_рынок_предложение2

+{ кощей == true && дриада == true } [Отправить Кощея и Флору работать вместе] -> Кощей_Флора_комбо_решение2
+{ кот == true && дриада == true } [Отправить Кота и Флору работать вместе] -> Кот_Флора_рынок_решение2
+{ кощей == true && дровосек == true } [Отправить Кощея и Дровосека работать вместе] -> Кощей_Дровосек_комбо_решение2
+{ кощей == true && кот == true } [Отправить Кощея и Кота работать вместе] -> Кощей_Кот_комбо_решение2
+{ василиса == true && дровосек == true } [Отправить Василису и Дровосека работать вместе] -> Василиса_Дровосек_рынок_решение2
+{ кощей == true && иван == true } [Отправить Кощея и Ивана работать вместе] -> Кощей_Иван_комбо_решение2
+{ дровосек == true && дриада == true } [Отправить Дровосека и Флору работать вместе] -> Дровосек_Флора_комбо_решение2
+{ кощей == true && василиса == true } [Отправить Кощея и Василису работать вместе] -> Василиса_Кощей_рынок_решение2
+{ кот == true && василиса == true } [Отправить Кота и Василису работать вместе] -> Василиса_Кот_рынок_решение2

=== Василиса_Кот_рынок_решение2 ===
ВАСИЛИСА: Я верю, что торговец не злодей, его обманули. Надо убедить его отдать артефакт.#З
ВАСИЛИСА: (мягко, торговцу) Гильдия Громовых Шестерней тебя использовала, как и многих до тебя. Хочешь знать, чем это для них закончилось?#У
КОТ УЧЕНЫЙ тут же разворачивает потрёпанный свиток, поправляя очки.
КОТ УЧЕНЫЙ: Согласно «Судебным хроникам», в 1247 году гильдия «Молчаливые Тени» применяла идентичные артефакты. Посредников объявили виновными, они закончили дни в темнице.#У
ТОРГОВЕЦ: Они предложили мне денег... сказали, ничего страшного не произойдет...
КОТ УЧЕНЫЙ: Механизм прост! Сделка, заключенная через подобный артефакт, держится на добровольном согласии. 
ВАСИЛИСА: Достаточно от неё отказаться и вернуть камень – и все связи разорвутся. Произнеси: «Я отрекаюсь от этой сделки и возвращаю то, что взял».
Торговец, дрожащими руками, протягивает камень Василисе и повторяет слова.
Артефакт меркнет, и от него во все стороны расходятся тихо звенящие волны. Люди на площади один за другим начинают кашлять, прочищать горло, а затем – говорить.
КОТ УЧЕНЫЙ: Работает! Сделки аннулированы! Артефакт деактивирован!#Р
ВАСИЛИСА: (торговцу) Теперь помоги нам описать тех, кто тебя нанял. Это твой шанс всё исправить.
ФОЛИАНТ ПО СВЯЗИ: Команда сработала безупречно! #Р
~ улика = улика + 2
-> Рынок_После_Нейтрализации

=== Василиса_Кощей_рынок_решение2 ===
ВАСИЛИСА: Я поговорю с торговцем, постараюсь убедить его отдать камень.
КОЩЕЙ: Сентиментально и ненадёжно. Я могу принудительно разорвать связи, но это будет болезненно. Возможно, смертельно для слабых.
ВАСИЛИСА: Дай мне сначала поговорить с ним. 
Кощей молча кивает, наблюдая.
ВАСИЛИСА: (обращаясь к торговцу) Посмотри, что происходит с людьми. Они напуганы, они страдают. Уверена, ты не хотел этого! Что обещали тебе Громовые Шестерни?
ТОРГОВЕЦ: (дрожа) Они обещали мне дать денег... Я не хотел...
КОЩЕЙ: Не хотел? Ты просто жалкий и жадный мошенник! Тебе нет пощады!
Он проводит рукой, и тёмная аура окутывает камень в руках торговца.
Камень трескается, и из него вырывается тихий вихрь шёпотов. Люди падают на колени, крича от боли. Но голоса возвращаются — медленно, но верно. Торговец бросает осколки и скрывается в толпе.
ВАСИЛИСА: Ты смог сохранить артефакт для анализа?
КОЩЕЙ: Нет. Энергетическая структура разрушена. Но я зафиксировал след заклинания — оно имеет техномагическую природу. Стиль «Громовых Шестерней».
ФОЛИАНТ ПО СВЯЗИ: Задача выполнена. Голоса возвращены, источник угрозы устранён, но материальная улика утрачена. 
~ улика = улика + 1
-> Рынок_После_Нейтрализации


=== Кот_Флора_рынок_решение2 ===
КОТ УЧЕНЫЙ: Я нашёл! В «Манускриптах Расторжения» описан именно такой случай. Нужно заставить камень отдать голоса — для этого требуется натуральный катализатор, живой и чистый!#З
ФЛОРА: Я могу вырастить «цветы правды» — они расцветают только от честных слов. Если люди скажут, что отказываются от сделки, цветы вберут ложную магию камня и нейтрализуют её!#З
КОТ УЧЕНЫЙ: Да! Я знаю заклинание, которое усилит связь между цветами и камнем — оно из «Травника эльфийских мудрецов»!
Флора рассыпает семена. Кот читает древний стих. Семена мгновенно прорастают в серебристые цветы с полупрозрачными лепестками.
ФЛОРА: (обращаясь к людям) Скажите просто: «Я не согласен». Шёпотом!
Люди начинают шептать. Каждый лепесток цветка начинает светиться, и из камня вырываются тонкие звуковые волны. Камень постепенно тускнеет.
КОТ УЧЕНЫЙ: Работает! Магическая схема артефакта перезаписывается цветочной пыльцой — теперь она хранит не голоса, а память о нарушенной сделке!#З
Камень раскалывается с тихим звоном. Из него выпадает маленькая металлическая шестерёнка с клеймом «Громовые Шестерни».
ФОЛИАНТ ПО СВЯЗИ: Комбинация магии природы и исторических знаний сработала! Голоса возвращены, улика найдена, но механизм работы артефакта остался неисследованным.
~ улика = улика + 2
-> Рынок_После_Нейтрализации

=== Василиса_Дровосек_рынок_решение2 ===

ВАСИЛИСА: Я уверена, этот торговец не понимает, что делает. Я хочу поговорить с ним.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Это риск. Любое резкое движение — и может активироваться цепная реакция. Нужна стабилизация.
ВАСИЛИСА: Ты займись этим. (подходит к торговцу) Посмотри, что происходит... Люди напуганы, они теряют голоса... Гильдия обманула тебя.
ТОРГОВЕЦ: (дрожит) Человек в плаще обещал мне золото… я не знал, что так будет…
ВАСИЛИСА: Ты можешь всё исправить. Просто отпусти камень — медленно, в мои руки. Мы его обезвредим, и никто не пострадает.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Готовлю нейтрализующее поле.
Торговец, кивая, разжимает пальцы. Василиса аккуратно принимает камень и помещает его в силовую сферу, созданную Дровосеком.
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Контейнер герметичен. Энергетические связи начали рваться… плавно. Голоса возвращаются.
По площади прокатывается волна облегчённых возгласов, смешков, криков. Камень в сфере темнеет.
ВАСИЛИСА: Всё. Артефакт обезврежен, люди в безопасности. Но торговец ничего не знает о заказчиках — только про «человека в плаще».
ФОЛИАНТ ПО СВЯЗИ: Задача выполнена. Голоса возвращены, улика сохранена. 
~ улика = улика + 1
-> Рынок_После_Нейтрализации

=== Кощей_Флора_комбо_решение2 ===

КОЩЕЙ: Я извлеку энергию, которую артефакт украл. Но это опасная субстанция.#У
ФЛОРА: А обездвижу злодея, чтобы он не успел активировать артефакт.#З
Кощей поднимает руки, и воздух наполняется магией. Флора шепчет заклинание и из земли вытягиваются лианы, опутывающие торговца по рукам и ногам.
Энергия возвращается к людям как тихое эхо - сначала шёпот, потом нормальная речь.

КОЩЕЙ: Контракты аннулированы. Энергия возвращена. И у нас есть полный набор отпечатков для отслеживания.#Р
ФЛОРА: И никто не пострадал!#Р
ФОЛИАНТ ПО СВЯЗИ: И злодея задержали! Блестящая работа! #Р
~ улика = улика + 2
-> Рынок_После_Нейтрализации

=== Дровосек_Флора_комбо_решение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Я соберу простого дроида-курьера. Он подойдёт к торговцу с запросом на «доставку партии карамели». Дроид незаметно просканирует артефакт и запишет все данные, после чего уйдёт под предлогом уточнения адреса. Риск минимален.#З
ФЛОРА: (с искренним участием) Бедняжка! Он будет таким механическим и неживым. Я могу вдохнуть в его схему немного жизни! Чтобы он вызывал доверие!#У
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Это не предусмотрено конструкцией. Его убедительность — в чётких алгоритмах и правдоподобной легенде.
Но Флора уже подошла к металлическому дроиду и положила на его холодный корпус ладонь. По дроиду побежали зелёные искры. Его голос стал тёплым и задушевным, а в  «глазах»-индикаторах появилось добродушное выражение.
ДРОИД: (идя к торговцу, почти с пафосом) О, уважаемый торговец! Ваши дивные сладости творят чудеса! Моя бабушка очень нуждается в удаче! Не могли бы вы...
Торговец с радостью вступает в диалог. Дроид сканирует артефакт. Но артефакт, чувствуя перед собой не просто механизм, а нечто, наделённое магией, срабатывает.
#Location: Market_KATASTРОФА
Камень вспыхивает ослепляющим светом и гаснет. Тишина становится абсолютной, тягучей и физически давящей. Люди замирают на месте, их глаза стекленеют. Рынок превращается в жуткую живую картину: окаменевшие фигуры, неподвижные, с открытыми ртами, в которых больше не родится ни звука.
ФОЛИАНТ ПО СВЯЗИ(голос еле слышен сквозь наступившую немоту): К-катастрофа... Аварийная активация... Срочное возвращение... #П
~ катастрофа = true
-> Рынок_Итоги

=== Кощей_Иван_комбо_решение2===
ИВАН ЦАРЕВИЧ: Кощей, ты выглядишь солидно. Подойди к торговцу как важный, но капризный клиент. Начни торговаться, придирайся к карамелькам, одним словом, отвлекай его. А я незаметно вытащу эту штуковину.#З
КОЩЕЙ (брезгливо): Общаться с уличным торговцем?.. Я просто потребую показать товар.#У
Кощей надменно подходит к лотку.
КОЩЕЙ: Эй, торговец. Это что за дешёвая крашеная патока? В моих кладовых такие сладости выбрасывают. Дай-ка посмотреть на твой «секретный ингредиент», если он, конечно, существует.#У
ТОРГОВЕЦ: (начинает нервничать) Простите, господин, это просто карамель...
КОЩЕЙ: (раздражённо, повышая голос) Я чувствую здесь магию! Грубую, топорную, дешёвую магию! Жалкий шарлатан!#У
Торговец в панике хватает камень и со всей силы швыряет его на землю, чтобы уничтожить улики.
#Location: Market_KATASTРОФА
Камень вспыхивает ослепляющим светом и гаснет. Тишина становится абсолютной, тягучей и физически давящей. Люди замирают на месте, их глаза стекленеют. Рынок превращается в жуткую живую картину: окаменевшие фигуры, неподвижные, с открытыми ртами, в которых больше не родится ни звука.
ФОЛИАНТ ПО СВЯЗИ (голос еле слышен сквозь наступившую немоту): К-катастрофа... Аварийная активация... Срочное возвращение... #П
~ катастрофа = true
-> Рынок_Итоги

===Кощей_Кот_комбо_решение2===
 // Комбинация Кощей + Кот
        КОЩЕЙ: Я разорву связи, а ты, Кот, найди способ сохранить информацию о контрактах.#З
        КОТ УЧЕНЫЙ: Уже ищу заклинание архивации... Вот! Глава шестая, "Сохранение следов магических сделок"!#З
        Кощей проводит ритуал, а Кот архивирует информацию. Камень трескается, из него вылетают призрачные светящиеся свитки — копии всех заключенных сделок. Торговец скрывается в толпе людей.
        КОЩЕЙ: Готово. Доказательства сохранены.#Р
        КОТ УЧЕНЫЙ: И артефакт достаточно цел для анализа технологий врага.#Р
        ~ улика = улика + 2
         ФОЛИАНТ ПО СВЯЗИ: Прекрасное решение!#Р
         -> Рынок_После_Нейтрализации

===Кощей_Дровосек_комбо_решение2===
// Комбинация Кощей + Дровосек
        КОЩЕЙ: Я начну разрывать магические связи, а ты, Дровосек, стабилизируй артефакт, чтобы он не взорвался.#З
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Создаю силовое поле для сдерживания энергии.#З
        Кощей щелкает пальцами. Торговец вскрикивает и роняет камень. Черные молнии рвут невидимые нити, но силовое поле Дровосека сдерживает разрушительную энергию.
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Артефакт стабилизирован. Магическая схема повреждена, но сохранилась для изучения.
        КОЩЕЙ: Контракты аннулированы. Голоса возвращаются. И мы получили образец технологии «Громовых Шестерней».
        ~ улика = улика + 2
        ФОЛИАНТ ПО СВЯЗИ: Прекрасное решение!#Р
        -> Рынок_После_Нейтрализации

===Дровосек_Кот_комбо_решение2===
// Комбинация Дровосек + Кот
        ЖЕЛЕЗНЫЙ ДРОВОСЕК: Создаю усилитель. Профессор, подскажите безопасную частоту, чтобы не повредить артефакт.
        КОТ УЧЕНЫЙ: В "Трактате о звуковых артефактах" сказано: нужно использовать частоту 432 Гц — она гармонична и не разрушает магические структуры!#У
        Дровосек настраивает устройство на указанную частоту. Устройство издает низкий гул. Усилитель работает. Голоса постепенно возвращаются, но торговец успевает скрыться в толпе. 
        КОТ УЧЕНЫЙ: Я успел записать магическую схему артефакта до его деактивации!#Р
        ~ улика = улика + 2
		ФОЛИАНТ ПО СВЯЗИ: Прекрасная работа!#Р
               -> Рынок_После_Нейтрализации

=== Дровосек_рынок_предложение2 ===
ЖЕЛЕЗНЫЙ ДРОВОСЕК: Попробую создать резонансный усилитель голосовых связок. Он усилит звуковые колебания на 500%, позволяя преодолеть магическое шумоподавление.#У
+ [Действуй!] -> Дровосек_рынок_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Рынок_Решение

=== Дровосек_рынок_нейтрализация ===
        Устройство Дровосека издаёт низкий гул. Внезапно камень в руках торговца начинает пульсировать красным светом.
        // КАТАСТРОФА
        ФОЛИАНТ ПО СВЯЗИ: Вон артефакт! Мы нашли его!! Но он перешел в режим максимальной защиты!
#Location: Market_KATASTРОФА
        Камень вспыхивает ослепляющим светом и гаснет. Тишина становится абсолютной, тягучей и физически давящей. Люди замирают на месте, их глаза стекленеют. Рынок превращается в жуткую живую картину: окаменевшие фигуры, неподвижные, с открытыми ртами, в которых больше не родится ни звука. Торговец с камнем в панике убегает.
ФОЛИАНТ ПО СВЯЗИ (голос еле слышен сквозь наступившую немоту): К-катастрофа... Аварийная активация... Срочное возвращение... #П
        ~ катастрофа = true
        -> Рынок_Итоги

=== Флора_рынок_предложение2 ===
ФЛОРА: Эта тишина пахнет страхом! Я наполню воздух пыльцой солнечных лютиков - они несут в себе энергию чистого радостного возгласа! Когда люди вдохнут её, их лёгкие сами захотят издать счастливый крик, и это разорвёт искусственные оковы! Природа всегда найдёт способ выразить себя!#Р
+ [Действуй!] -> Флора_рынок_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Рынок_Решение
=== Флора_рынок_нейтрализация ===
        Пыльца Флоры вызывает у людей лишь беззвучное чихание, от которого у них слезятся глаза. Чары остаются непоколебимыми.
        ФЛОРА: Почему пыльца не работает? Это же чистая радость...#П
        ФОЛИАНТ ПО СВЯЗИ: Босс, эффект нулевой. Теперь люди не только не могут говорить, но и постоянно чихают.#У
        -> Рынок_Итоги


=== Кощей_рынок_предложение2 ===
КОЩЕЙ: Я проведу ритуал принудительного расторжения магических контрактов. Это болезненно для артефакта и, возможно, для его носителя, но это единственный способ разорвать все связи разом.#З
+ [Действуй!] -> Кощей_рынок_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Рынок_Решение

=== Кощей_рынок_нейтрализация ===
        Кощей с презрением щелкает пальцами. Торговец, держащий камень, вскрикивает и роняет его.
        Камень зависает в воздухе, и от него во все стороны бьют черные молнии, словно рвутся невидимые нити.
        КОЩЕЙ: Контракты аннулированы. Артефакт более не функционирует.#Р
        По рынку прокатывается волна - сначала тихого удивления, потом сдавленного смеха, а затем и полнозвучного крика: "Я МОГУ ГОВОРИТЬ!"
        ФОЛИАНТ ПО СВЯЗИ: Нейтрализованное ядро артефакта - бесценная улика!
        ~ улика = улика + 1

-> Рынок_После_Нейтрализации

=== Иван_рынок_предложение2 ===
ИВАН ЦАРЕВИЧ: Я просто уничтожу камень. Нет артефакта - некому поддерживать чары. Голоса должны вернуться сами.#У
+ [Действуй!] -> Иван_рынок_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Рынок_Решение

=== Иван_рынок_нейтрализация ===
        Иван резким движением выхватывает камень из рук торговца и с силой швыряет его оземь. Тот разбивается вдребезги. От него во все стороны бьют черные молнии, словно рвутся невидимые нити.
        Эффект немоты спадает не мгновенно, а в течение нескольких минут, вызывая замешательство, но в итоге голоса возвращаются. Торговец, пользуясь суматохой, сбегает.
        ФОЛИАНТ ПО СВЯЗИ: Проблема решена, но улика уничтожена, а торговец скрылся.
-> Рынок_После_Нейтрализации

=== Кот_рынок_предложение2 ===
КОТ УЧЕНЫЙ: Найду контрзаклинание из "Манускриптов Расторжения". Оно заставит камень извергнуть поглощённые голоса. Безопасно, но потребует долгого времени...#У
+ [Действуй!] -> Кот_рынок_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Рынок_Решение

=== Кот_рынок_нейтрализация ===
        Кот, бормоча заклинание, заставляет камень светиться и испускать тихий, мелодичный звон. Голоса возвращаются, но путаются - некоторые люди начинают говорить чужими голосами, что вызывает временный хаос. Только через полчаса все приходит в норму. Пользуясь суетой, торговец успевает скрыться.
        КОТ УЧЕНЫЙ: М-да, побочный эффект. В древних текстах об этом не упоминалось... Кажется, голоса немного перемешались.#П
        ФОЛИАНТ ПО СВЯЗИ:  Артефакт разрушен, улик нет. Но хоть голоса вернулись...
        -> Рынок_После_Нейтрализации

=== Василиса_рынок_предложение2 ===
ВАСИЛИСА: Я спою старую колыбельную - ту, что знает каждый с детства. Если я смогу заставить их вспомнить мелодию и подпеть мне, их собственные голоса пробьются сквозь чары.#З
+ [Действуй!] -> Василиса_рынок_нейтрализация
+ [Послушаем другие варианты] -> Этап2_Рынок_Решение

=== Василиса_рынок_нейтрализация ===
        Василиса встает у колодца и начинает петь старую колыбельную. Её голос плывёт над притихшим рынком. Это простая, знакомая каждому с детства колыбельная. Сначала в ответ - тишина. Но вот старушка у лавки шевелит губами. Ребёнок шепотом подхватывает припев. К нему присоединяется ещё один голос, ещё один... Шёпот набирает силу, превращаясь в хор, а затем - в мощное пение, сотрясающее воздух. Камень в руках торговца трескается и рассыпается в пыль. Торговец в ужасе закрывает уши и бросается наутек.
        ФОЛИАНТ ПО СВЯЗИ: Невероятно! Артефакт не выдержал напора! Мы получили запись этого момента - это бесценная улика!#Р
        ~ улика = улика + 1
-> Рынок_После_Нейтрализации

=== Рынок_После_Нейтрализации ===
#Location: Market
На рынке медленно, но верно воцаряется нормальный, здоровый шум. Кто-то плачет от радости, обнимая соседа, кто-то ругает торговца, который уже скрылся, кто-то просто смеется, с наслаждением слушая собственный голос.
+[Осмотреть улики] ->Osmotr5
+[Вернуться на базу] -> Рынок_Итоги

===Osmotr5===
#Location: Osmotr5
ФОЛИАНТ ПО СВЯЗИ:  Преобразование звуковых волн в энергию... Технически сложная задача. И этический вопрос — добровольность испытуемых.
-> Рынок_Итоги

=== Рынок_Итоги ===
#Location: Ofis
~ всего_улик = всего_улик + улика
ФОЛИАНТ: Передаю итоговый отчет.

{ катастрофа:
ФОЛИАНТ: Босс... тишина победила. Рынка больше не существует - теперь это музей под открытым небом, где застыли живые статуи. "Громовые Шестерни" провели полевые испытания нового оружия... Лучшие маги пытаются нейтрализовать заклятие. Надеюсь им это удастся...
}

{ улика >=3  && катастрофа==false:
ФОЛИАНТ: Блестяще! Мы не только вернули городу голос, но и получили бесценные данные. Мы понимаем принцип действия их нового оружия и имеем материальные доказательства. Это наша первая стратегическая победа.
}

{ улика == 2  && катастрофа==false:
ФОЛИАНТ: Голоса возвращены, паника остановлена. Мы установили связь с тактикой врага, но у нас недостаточно улик, чтобы предъявить их Совету. Шестерни остаются в тени.
}

{ улика <=1  && катастрофа==false:
ФОЛИАНТ: Немота отступила, но Громовые Шестерни остались безнаказанными. Они получили все данные своих полевых испытаний. В следующий раз их оружие будет совершеннее.
}

Найдено улик в этом деле: {улика}
Всего улик: {всего_улик}
~катастрофа=false
ФОЛИАНТ: Босс, пятое дело закрыто. Пришло время сложить все части пазла. Все улики, все свидетельства. Противостояние с Громовыми Шестернями неизбежно, и теперь мы идем на нее, зная врага в лицо.

+ [Собрать все улики для итогового отчета] -> ФИНАЛ_СЦЕНА


===ФИНАЛ_СЦЕНА===
#Location: Ofis1
Стол завален уликами: обломки устройств, кристаллы с выжженными рунами{пройдено==4: , мешок с синими ягодами}{пройдено==5: , осколок Камня Сделки}. Когнитивный Кристалл парит в центре комнаты, его экраны мерцают, проецируя сложные переплетения линий и символов между всеми артефактами.

ФОЛИАНТ: Босс! Все связи сходятся. Каждая аномалия — не случайность, а тест! Испытание разных методов преобразования магической энергии в электрическую! Грот — акустический резонанс. Верфь — осаждение материализованной магии. Библиотека — химико-магический реагент. {пройдено==4: Кухня великана — использование биологического проводника.} {пройдено==5: Рынок — социально-магический паразитизм!} Это все части одного уравнения!#У
#Location: Portal
Неожиданно все экраны КОГНИТИВНОГО КРИСТАЛЛА разом гаснут. Воздух в кабинете застывает, с тихим щелчком пространство перед столом искажается.

С другой стороны открывшегося прохода — просторная техномагическая мастерская. В центре, спиной к вам, у огромного окна, из которого открывается вид на бушующую магическим штормом долину, стоит невысокий человек с темными растрепанными волосами.

МАСТЕР ГРОМ: (Не оборачиваясь, спокойно) Я оценил вашу дотошность. И эффективность. Проходите. Ваши спутники пусть останутся. Канал связи с вашим архивариусом тоже временно приостановлен. Для его же безопасности.

Тебе не остается выбора. Шаг вперед — и портал за спиной беззвучно закрывается. Ты в логове гильдии «Громовые Шестерни». Один.
#Location: Gildia

Мастер Гром наконец поворачивается. В его взгляде — не безумие фанатика, а холодная, расчетливая уверенность ученого, убежденного в своей правоте.

МАСТЕР ГРОМ: Приветствую, директор Агентства Сказочных Аномалий. Или, позвольте вас так называть, коллега. Мы ведь оба выпускники Академии, хоть и с разницей в пару десятилетий. Вы расследовали симптомы. Я предлагаю взглянуть на диагноз и лекарство.#Р

МАСТЕР ГРОМ: Коллега, наш мир смертельно болен. Болен своей собственной сутью — неконтролируемой, дикой, иррациональной магией.#У
МАСТЕР ГРОМ: Магия. Это энергия. Но энергия, зависящая от фаз луны, настроения фей, чихания дракона за тридевять земель! Мы строим цивилизацию на зыбучем песке безумия!#П
МАСТЕР ГРОМ: Технология. Чистая, надежная, предсказуемая. Она может дать нам свет без светлячков, тепло без огненных саламандр, связь без почтовых духов. Но ей не хватает энергии. ЕЙ НЕ ХВАТАЕТ СИЛЫ.
МАСТЕР ГРОМ: Это - Преобразователь энергии. Он берет хаотичный, опасный магический фон — причину всех ваших «аномалий» — и преобразует его в чистую, управляемую, безопасную энергию. Тот визг в гроте? Побочный эффект настройки. Железная пыль? Отходы процесса очистки. Я не творил зло. Я проводил полевые испытания, собирая данные для финальной калибровки.#У

Он подходит к центру зала, где на низком пьедестале парит сердцевина всего — ЯДРО ПРЕОБРАЗОВАТЕЛЯ. Это произведение искусства: сложнейшее переплетение сияющих самоцветов, идеально отполированных металлических конструкций и текучих линий чистого света. Оно и пугает, и завораживает.

МАСТЕР ГРОМ: Представьте мир, где эта энергия питает города! Где свет в доме не гаснет, потому что домовой в отпуске. Где урожай не гибнет от случайного проклятия. Где машины лечат болезни с точностью, недоступной самому искусному магу-целителю! Мир без голода, холода, страха перед капризами стихий! Я предлагаю не смерть сказке, коллега. Я предлагаю нашему миру вырасти. Из детских страхов и неустойчивых чудес — в надежную, взрослую реальность.#Р

Внезапно воздух в зале вспыхивает золотым светом. Рядом с тобой, с гневным шипением разрывая пространство, возникает Мерлин. Его плащ развевается, посох дрожит в руке, глаза пылают. Он выглядит уставшим, но непоколебимым.

МЕРЛИН: Прекрасная речь, Гром! Очень благостная! И абсолютно никчемная! Ты видишь в магии лишь нестабильный ресурс, как уголь или пар. А я вижу в ней ДУШУ мира!#З
МЕРЛИН: Ты говоришь о болезнях и голоде. Благородно. Но ты предлагаешь лечить лихорадку, отрубая пациенту голову! Что будет с духом леса, когда его оцифруют? Что будет с русалкой, чья песня — это чистая, нефильтрованная магия? Они просто... исчезнут? Станут энергией твоих машин?#З
МЕРЛИН: Да, магия нестабильна! Да, она непредсказуема! Потому что она ЖИВАЯ! А ты хочешь заменить душу мира — на батарейку!#З
МЕРЛИН: Мир без сказок, которые рассказывает ветер? Без колыбельных песен звезд? Без возможности ЧУДА, которое нельзя просчитать алгоритмом?#З

МАСТЕР ГРОМ: (Холодно) Чудо, которое оборачивается мором? Симфонию, в которой случайная нота губит деревни? Я предлагаю порядок, Мерлин. Прогресс. Безопасность. Ты же цепляешься за романтику темных веков.#З

МЕРЛИН: Я цепляюсь за жизнь! Ту самую, что делает наш мир уникальным, а не еще одной скучной фабрикой в мироздании!#З

Они замолкают, тяжело дыша. Их аргументы повисли в воздухе, не достигнув цели. Ни один не смог переубедить другого. В глазах Мерлина — яростная убежденность хранителя. В глазах Грома — холодная уверенность хирурга, готового ампутировать больной орган для спасения пациента.

МЕРЛИН: (Тихо, с ледяной яростью) Значит, слова бессильны. Как я и предполагал.#З

МАСТЕР ГРОМ: (Так же тихо) Да. Диалог исчерпан.#З
#Location: Protivostojanie
Мерлин поднимает посох. Из его наконечника изливается сокрушительная волна магии, нацеленная на Ядро Преобразователя.

На пути магии встает Сфера Абсолютного Порядка — барьер. Хаотичная сила Мерлина бьётся о него, замедляется, дробится.

Силы равны. Хаос не может сломать Порядок. Порядок не может поглотить Хаос. Зал дрожит от столкновения двух непоколебимых воль. Пот градом льётся с лица Мерлина. Лицо Грома искажено от напряжения.

МЕРЛИН: (Сквозь стиснутые зубы) Твоя наука... против воли мира... не устоит...

МАСТЕР ГРОМ: (Голосом, звенящим от усилия) И твоя воля... против законов реальности... тщетна...

Они могут стоять так вечно. Или до тех пор, пока их противостояние не сотрёт с лица земли и зал, и гору, и всё вокруг!

МЕРЛИН: Ученик! Рычаг аварийного отключения! Гром не сможет помешать, он держит меня! Останови Преобразователь!

МАСТЕР ГРОМ: Коллега! Кнопка финального запуска! На главном пульте! Пока маг занят мной, ты дашь миру будущее!

*   [УНИЧТОЖИТЬ МАШИНУ] -> destroy_machine
*   [ЗАПУСТИТЬ ПРЕОБРАЗОВАТЕЛЬ] -> activate_machine
*   [НАЙТИ КОМПРОМИСС] -> attempt_compromise
*   [БЕЖАТЬ] -> run_away

=== destroy_machine ===
#Location: Mashin_STOP
ТЫ: "Я не стану палачом чуда! Магия должна остаться свободной, какой бы опасной она ни была!" Ты резко дергаешь аварийный рычаг! 
-> ending_magic

=== activate_machine ===
#Location: Mashin_PUSK
ТЫ: "Люди заслуживают мира и прогресса! Пора выйти из колыбели сказок!" Ты решительно нажимаешь кнопку ПУСК! 
-> ending_technology

=== run_away ===
Ты в ужасе отступаешь на шаг, зажимая уши. "Нет... Я не могу... Это слишком сложное решение..." Ты разворачиваешься и бросаешься к выходу. Куда угодно! В любое место, лишь бы не делать этот выбор!
-> ending_flee

=== attempt_compromise ===
ТЫ: Ваша борьба ведёт в тупик! Вы оба видите только половину правды!

МЕРЛИН: Он хочет уничтожить всё, что делает наш мир живым!#У

МАСТЕР ГРОМ: А он защищает хаос, который приносит только страдания!#У

ТЫ: Именно! Один из вас видит проблему, но его решение слишком грубое. А другой видит красоту мира, но не хочет признать его проблемы. Во всех случаях, которые мы расследовали, была одна закономерность: когда магия и техника сталкивались, возникало что-то новое. Непредсказуемое, иногда опасное, но новое. Нужно понять эту силу и научиться управлять ею.

{всего_улик >= 10:     ->compromise_success }
{всего_улик < 10:   ->compromise_success }

=== compromise_fail===
МЕРЛИН: Слишком мудрено для простого выбора, ученик. Сейчас нужна не философия, а решимость. Выбирай сторону.#У

МАСТЕР ГРОМ: Твои идеи не подкреплены фактами. Реальность требует ясных действий. Присоединяйся к порядку или останься с хаосом.#У

Ты чувствуешь, что тебе не хватает аргументов, нужных фактов и доказательств... Попытка достичь компромисса не удалась...

ТЫ:
*   [А. УНИЧТОЖИТЬ МАШИНУ.] -> destroy_machine
*   [Б. ЗАПУСТИТЬ ПРЕОБРАЗОВАТЕЛЬ.] -> activate_machine
*   [Г. БЕЖАТЬ.] -> run_away

=== compromise_success===
Ты вспоминаешь все факты и улики, собранные вами за время расследования.
ТЫ: У меня есть факты! Все эти аномалии — не просто атаки или поломки. Это признаки того, что две силы — старая и новая — начали взаимодействовать. Жёстко, неуклюже, но начали. Эта машина — инструмент, который впервые по-настоящему «заметил» магию и попытался с ней работать. Да, работает он ужасно! Но это только начало. Давайте изменим его цель. Пусть он не подавляет и не выжимает силу, а помогает найти баланс. Помогает магии не быть разрушительной, а технологии — не быть бездушной.

Наступает тягостная пауза. Твои слова, подкреплённые собранными доказательствами, заставляют обоих задуматься.

МАСТЕР ГРОМ: (Первым нарушает молчание) Баланс... Не максимальная эффективность, но минимальный риск... Возможно, стоит провести ряд экспериментов...#У

МЕРЛИН: (С тяжёлым вздохом опускает посох) Учить железо чувствовать... и учить стихии быть устойчивыми... Безумная идея. Но продолжать эту дуэль — ещё большее безумие. Ладно. Будем пробовать.#У

Напряжение спадает. Силовое поле между ними гаснет. Компромисс достигнут.
    -> ending_balance
#Location: Epilog
Прошло некоторое время...

=== ending_magic ===
#Location: END_MAGIA
Ты стоишь у окна в своём кабинете в Министерстве по чрезвычайным магическим ситуациям. На площади в честь Праздника Весны танцуют огненные фениксы, а бродячие чародеи запускают в небо фонтанчики чистой магии. 
За твоей спиной появляется Мерлин. Его голос спокоен, но в глазах заметна усталость.
МЕРЛИН: Пришло срочное сообщение, агент. Драконы сошли с гор и уничтожили три приграничные деревни. Вам нужно немедленно отправляться туда, оценить масштаб разрушения.
Кивая, ты берёшь плащ с вешалки. Мир, который вы выбрали, ярок и прекрасен, но цена этой красоты — вечная готовность тушить пожары, которые он же и порождает.
КОНЕЦ
-> END

=== ending_technology ===
#Location: END_Tehnika
Преобразователь запущен. Голод и болезни побеждены. Мир стал намного безопаснее... и безмолвнее. Ты и Мастер Гром у главного пульта в Центре Управления. На экранах — графики: Заболеваемость: 0% Преступность: 0% Урожайность: 100%
МАСТЕР ГРОМ: Мы победили страдание, коллега. Оставшейся энергии с лихвой хватит на то, чтобы снарядить экспедицию к иным мирам.
Ты киваешь, смотришь на небоскребы города-мечты и ловишь себя на мысли, что ищешь в небе хоть одну настоящую фею...
КОНЕЦ
-> END

=== ending_balance ===
#Location: END_Balanse
В новом Совете Равновесия Мерлин и Мастер Гром спорят над картой. "Здесь — заповедник дикой магии, — указывает Мерлин". "А здесь мы построим новую фабрику дроидов, — парирует Гром".
Ты выходишь на балконю Перед тобой открывается город-гармония. Над арочным мостом, сплетённым из живого дерева, бесшумно скользит корпус техномагического корабля, оставляя за собой лёгкий, переливающийся на солнце след. Внизу, в парке, смех детей сливается с весёлым чириканьем и всполохами безобидных искорок — это механический дракончик, гибкий и почти живой, играет с малышнёй, будто настоящее волшебное существо.
Ты невольно улыбаешься.
КОНЕЦ
-> END

=== ending_flee ===
#Location: END_Les
Уже давно ты живёшь один в глухом лесу. Иногда ты видишь в небе странные аппараты — то ли магические, то ли технологические.
Каждое утро ты всматриваешься в мир, пытаясь понять — кто же, в конце концов, победил? Мерлин или Гром? Магия или технология?
Но ответа всё нет...
КОНЕЦ
-> END

